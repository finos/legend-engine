// Copyright 2022 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::functions::collection::*;
import meta::pure::persistence::metamodel::*;
import meta::pure::persistence::metamodel::persister::*;
import meta::pure::persistence::test::*;
import meta::pure::persistence::validation::*;

/**********
 * tds service tests
 **********/

function <<test.Test>> meta::pure::persistence::test::validateTarget_tdsService_flatPersistence(): Boolean[1]
{
  let persistence = TdsServiceWithFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_tdsService_flatPersistence(): Boolean[1]
{
  let persistence = TdsServiceWithFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateTarget_tdsService_multiFlatPersistence(): Boolean[1]
{
  let persistence = TdsServiceWithMultiFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_tdsService_multiFlatPersistence(): Boolean[1]
{
  let persistence = TdsServiceWithMultiFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert(!$result.valid());
}

/**********
 * flat graph fetch service tests
 **********/

function <<test.Test>> meta::pure::persistence::test::validateTarget_flatM2mService_flatPersistence(): Boolean[1]
{
  let persistence = FlatM2mServiceWithFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_flatM2mService_flatPersistence(): Boolean[1]
{
  let persistence = FlatM2mServiceWithFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateTarget_flatM2mService_multiFlatPersistence(): Boolean[1]
{
  let persistence = FlatM2mServiceWithMultiFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_flatM2mService_multiFlatPersistence(): Boolean[1]
{
  let persistence = FlatM2mServiceWithMultiFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert(!$result.valid());
}

/**********
 * nested graph fetch service tests
 **********/

function <<test.Test>> meta::pure::persistence::test::validateTarget_nestedM2mService_flatPersistence(): Boolean[1]
{
  let persistence = NestedM2mServiceWithFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_nestedM2mService_flatPersistence(): Boolean[1]
{
  let persistence = NestedM2mServiceWithFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert(!$result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateTarget_nestedM2mService_multiFlatPersistence(): Boolean[1]
{
  let persistence = NestedM2mServiceWithMultiFlatPersistence();
  let result = validateTarget($persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

function <<test.Test>> meta::pure::persistence::test::validateServiceAndTarget_nestedM2mService_multiFlatPersistence(): Boolean[1]
{
  let persistence = NestedM2mServiceWithMultiFlatPersistence();
  let result = validateServiceAndTarget($persistence.service, $persistence.persister->cast(@BatchPersister).targetShape);
  assert($result.valid());
}

/**********
 * classes
 **********/

Class meta::pure::persistence::test::WrapperInput
{
  flatInput1: FlatInput[1];
  flatInput2: FlatInput[1];
  flatInput3: FlatInput[1];
}

Class meta::pure::persistence::test::FlatInput
{
  idIn: Integer[1];
  booleanIn: Boolean[1];
  integerIn: Integer[1];
  floatIn: Float[1];
  decimalIn: Decimal[1];
  strictDateIn: StrictDate[1];
  dateTimeIn: DateTime[1];
  stringIn: String[1];
}

Class meta::pure::persistence::test::WrapperOutput
{
  flatOutput1: FlatOutput[1];
  flatOutput2: FlatOutput[1];
  flatOutput3: FlatOutput[1];
}

Class meta::pure::persistence::test::FlatOutput
{
  idOut: Integer[1];
  booleanOut: Boolean[1];
  integerOut: Integer[1];
  floatOut: Float[1];
  decimalOut: Decimal[1];
  strictDateOut: StrictDate[1];
  dateTimeOut: DateTime[1];
  stringOut: String[1];
}

/**********
 * relational database, mapping, connection
 **********/

###Relational
Database meta::pure::persistence::test::RelationalDatabase
(
  Table FlatTable1 (
    id INTEGER PRIMARY KEY,
    boolean_col BIT,
    integer_col INTEGER,
    float_col DOUBLE,
    decimal_col DECIMAL(10,2),
    strict_date_col DATE,
    date_time_col TIMESTAMP,
    string_col VARCHAR(200)
  )

  Table FlatTable2 (
    id INTEGER PRIMARY KEY,
    boolean_col BIT,
    integer_col INTEGER,
    float_col DOUBLE,
    decimal_col DECIMAL(10,2),
    strict_date_col DATE,
    date_time_col TIMESTAMP,
    string_col VARCHAR(200)
  )

  Table FlatTable3 (
    id INTEGER PRIMARY KEY,
    boolean_col BIT,
    integer_col INTEGER,
    float_col DOUBLE,
    decimal_col DECIMAL(10,2),
    strict_date_col DATE,
    date_time_col TIMESTAMP,
    string_col VARCHAR(200)
  )
)

###Mapping
import meta::pure::persistence::test::*;

Mapping meta::pure::persistence::test::RelationalMapping
(
  *meta::pure::persistence::test::FlatInput: Relational
  {
    ~primaryKey
    (
      [RelationalDatabase] FlatTable1.id
    )
    ~mainTable [RelationalDatabase] FlatTable1
    booleanIn: [RelationalDatabase] FlatTable1.boolean_col,
    integerIn: [RelationalDatabase] FlatTable1.integer_col,
    floatIn: [RelationalDatabase] FlatTable1.float_col,
    decimalIn: [RelationalDatabase] FlatTable1.decimal_col,
    strictDateIn: [RelationalDatabase] FlatTable1.strict_date_col,
    dateTimeIn: [RelationalDatabase] FlatTable1.date_time_col,
    stringIn: [RelationalDatabase] FlatTable1.string_col
  }
)

###Pure
import meta::pure::alloy::connections::*;
import meta::pure::alloy::connections::alloy::authentication::*;
import meta::pure::alloy::connections::alloy::specification::*;
import meta::pure::runtime::*;
import meta::relational::runtime::*;

function meta::pure::persistence::test::RelationalConnection(): Connection[1]
{
  ^RelationalDatabaseConnection(
    element = meta::pure::persistence::test::RelationalDatabase,
    type = DatabaseType.H2,
    authenticationStrategy = ^TestDatabaseAuthenticationStrategy(),
    datasourceSpecification = ^LocalH2DatasourceSpecification()
  );
}

/**********
 * tds runtime, service, persistence
 **********/

###Pure
import meta::legend::service::metamodel::*;
import meta::pure::mapping::*;
import meta::pure::persistence::metamodel::*;
import meta::pure::persistence::metamodel::notifier::*;
import meta::pure::persistence::metamodel::persister::*;
import meta::pure::persistence::metamodel::persister::deduplication::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::merge::*;
import meta::pure::persistence::metamodel::persister::transactionmilestoning::*;
import meta::pure::persistence::metamodel::persister::sink::*;
import meta::pure::persistence::metamodel::persister::targetshape::*;
import meta::pure::persistence::metamodel::trigger::*;
import meta::pure::runtime::*;
import meta::pure::test::assertion::*;

function meta::pure::persistence::test::RelationalRuntime(): Runtime[1]
{
  ^EngineRuntime(
    mappings = meta::pure::persistence::test::RelationalMapping,
    connections = meta::pure::persistence::test::RelationalConnection()
  );
}

function meta::pure::persistence::test::TdsService(): Service[1]
{
  ^Service
  (
    pattern = '/',
    owners = ['dummy'],
    documentation = '',
    autoActivateUpdates = true,
    execution = ^PureSingleExecution
    (
      func = | meta::pure::persistence::test::FlatInput.all()->project(
        [
          x | $x.booleanIn, x | $x.integerIn, x | $x.floatIn, x | $x.decimalIn, x | $x.strictDateIn, x | $x.dateTimeIn, x | $x.stringIn
        ],
        [
          'Boolean', 'Integer', 'Float', 'Decimal', 'StrictDate', 'DateTime', 'String'
        ]
      ),
      mapping = meta::pure::persistence::test::RelationalMapping,
      runtime = meta::pure::persistence::test::RelationalRuntime()
    ),
    test = ^SingleExecutionTest(
      data = '',
      asserts = ^TestContainer(assert = x:Result<Any|m>[1] | true)),
    tests = ^ServiceTest(id = 'test1', assertions = ^EqualTo(id = 'assertion1', expected = ''))
  );
}

function meta::pure::persistence::test::TdsServiceWithFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Tds service into a flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::TdsService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^FlatTarget(
        targetName = 'FlatTable2',
        deduplicationStrategy = ^NoDeduplicationStrategy()
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}

function meta::pure::persistence::test::TdsServiceWithMultiFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Tds service into a flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::TdsService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^MultiFlatTarget(
        modelClass = meta::pure::persistence::test::WrapperOutput,
        transactionScope = TransactionScope.ALL_TARGETS,
        parts = [
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput2')->first()->toOne(),
            targetName = 'FlatTable2',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          ),
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput3')->first()->toOne(),
            targetName = 'FlatTable3',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          )
        ]
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}

/**********
 * flat mapping, runtime, service, persistence
 **********/

###Mapping
Mapping meta::pure::persistence::test::FlatM2mMapping
(
  *meta::pure::persistence::test::FlatOutput: Pure
  {
    ~src meta::pure::persistence::test::FlatInput
    idOut: $src.idIn
    booleanOut: $src.booleanIn
    integerOut: $src.integerIn
    floatOut: $src.floatIn
    decimalOut: $src.decimalIn
    strictDateOut: $src.strictDateIn
    dateTimeOut: $src.dateTimeIn
    stringOut: $src.stringIn
  }
)

###Pure
import meta::legend::service::metamodel::*;
import meta::pure::graphFetch::execution::*;
import meta::pure::mapping::*;
import meta::pure::mapping::modelToModel::*;
import meta::pure::persistence::metamodel::*;
import meta::pure::persistence::metamodel::notifier::*;
import meta::pure::persistence::metamodel::persister::*;
import meta::pure::persistence::metamodel::persister::deduplication::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::merge::*;
import meta::pure::persistence::metamodel::persister::transactionmilestoning::*;
import meta::pure::persistence::metamodel::persister::sink::*;
import meta::pure::persistence::metamodel::persister::targetshape::*;
import meta::pure::persistence::metamodel::trigger::*;
import meta::pure::runtime::*;
import meta::pure::test::assertion::*;

function meta::pure::persistence::test::FlatM2mRuntime(): Runtime[1]
{
  ^EngineRuntime(
    mappings = meta::pure::persistence::test::FlatM2mMapping,
    connections = ^JsonModelConnection(
      element=^ModelStore(),
      class = meta::pure::persistence::test::FlatInput,
      url = 'executor:default'));
}

function meta::pure::persistence::test::FlatM2mService(): Service[1]
{
  ^Service
  (
    pattern = '/',
    owners = ['dummy'],
    documentation = '',
    autoActivateUpdates = true,
    execution = ^PureSingleExecution
    (
      func = | meta::pure::persistence::test::FlatOutput.all()->graphFetch(
        #{
          meta::pure::persistence::test::FlatOutput
          {
            idOut,
            booleanOut,
            integerOut,
            floatOut,
            decimalOut,
            strictDateOut,
            dateTimeOut,
            stringOut
          }
        }#
      )->serialize(
        #{
          meta::pure::persistence::test::FlatOutput
          {
            idOut,
            booleanOut,
            integerOut,
            floatOut,
            decimalOut,
            strictDateOut,
            dateTimeOut,
            stringOut
          }
        }#
      ),
      mapping = meta::pure::persistence::test::FlatM2mMapping,
      runtime = meta::pure::persistence::test::FlatM2mRuntime()
    ),
    test = ^SingleExecutionTest(
      data = '',
      asserts = ^TestContainer(assert = x:Result<Any|m>[1] | true)),
    tests = ^ServiceTest(id = 'test1', assertions = ^EqualTo(id = 'assertion1', expected = ''))
  );
}

function meta::pure::persistence::test::FlatM2mServiceWithFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Graph fetch m2m service into a flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::FlatM2mService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^FlatTarget(
        targetName = 'FlatTable1',
        deduplicationStrategy = ^NoDeduplicationStrategy()
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}

function meta::pure::persistence::test::FlatM2mServiceWithMultiFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Graph fetch m2m service into a multi flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::FlatM2mService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^MultiFlatTarget(
        modelClass = meta::pure::persistence::test::WrapperOutput,
        transactionScope = TransactionScope.ALL_TARGETS,
        parts = [
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput1')->first()->toOne(),
            targetName = 'FlatTable1',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          ),
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput2')->first()->toOne(),
            targetName = 'FlatTable2',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          )
        ]
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}

/**********
 * nested mapping, runtime, service, persistence
 **********/

###Mapping
Mapping meta::pure::persistence::test::NestedM2mMapping
(
  *meta::pure::persistence::test::WrapperOutput: Pure
  {
    ~src meta::pure::persistence::test::WrapperInput
    flatOutput1: $src.flatInput1
    flatOutput2: $src.flatOutput2
  }

  *meta::pure::persistence::test::FlatOutput: Pure
  {
    ~src meta::pure::persistence::test::FlatInput
    idOut: $src.idIn
    booleanOut: $src.booleanIn
    integerOut: $src.integerIn
    floatOut: $src.floatIn
    decimalOut: $src.decimalIn
    strictDateOut: $src.strictDateIn
    dateTimeOut: $src.dateTimeIn
    stringOut: $src.stringIn
  }
)

###Pure
import meta::legend::service::metamodel::*;
import meta::pure::graphFetch::execution::*;
import meta::pure::mapping::*;
import meta::pure::mapping::modelToModel::*;
import meta::pure::persistence::metamodel::*;
import meta::pure::persistence::metamodel::notifier::*;
import meta::pure::persistence::metamodel::persister::*;
import meta::pure::persistence::metamodel::persister::deduplication::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::*;
import meta::pure::persistence::metamodel::persister::ingestmode::delta::merge::*;
import meta::pure::persistence::metamodel::persister::transactionmilestoning::*;
import meta::pure::persistence::metamodel::persister::sink::*;
import meta::pure::persistence::metamodel::persister::targetshape::*;
import meta::pure::persistence::metamodel::trigger::*;
import meta::pure::runtime::*;
import meta::pure::test::assertion::*;

function meta::pure::persistence::test::NestedM2mRuntime(): Runtime[1]
{
  ^EngineRuntime(
    mappings = meta::pure::persistence::test::NestedM2mMapping,
    connections = ^JsonModelConnection(
      element=^ModelStore(),
      class = meta::pure::persistence::test::WrapperInput,
      url = 'executor:default'));
}

function meta::pure::persistence::test::NestedM2mService(): Service[1]
{
  ^Service
  (
    pattern = '/',
    owners = ['dummy'],
    documentation = '',
    autoActivateUpdates = true,
    execution = ^PureSingleExecution
    (
      func = | meta::pure::persistence::test::WrapperOutput.all()->graphFetch(
        #{
          meta::pure::persistence::test::WrapperOutput
          {
            flatOutput1
            {
              idOut,
              booleanOut,
              integerOut,
              floatOut,
              decimalOut,
              strictDateOut,
              dateTimeOut,
              stringOut
            },
            flatOutput2
            {
              idOut,
              booleanOut,
              integerOut,
              floatOut,
              decimalOut,
              strictDateOut,
              dateTimeOut,
              stringOut
            }
          }
        }#
      )->serialize(
        #{
          meta::pure::persistence::test::WrapperOutput
          {
            flatOutput1
            {
              idOut,
              booleanOut,
              integerOut,
              floatOut,
              decimalOut,
              strictDateOut,
              dateTimeOut,
              stringOut
            },
            flatOutput2
            {
              idOut,
              booleanOut,
              integerOut,
              floatOut,
              decimalOut,
              strictDateOut,
              dateTimeOut,
              stringOut
            }
          }
        }#
      ),
      mapping = meta::pure::persistence::test::NestedM2mMapping,
      runtime = meta::pure::persistence::test::NestedM2mRuntime()
    ),
    test = ^SingleExecutionTest(
      data = '',
      asserts = ^TestContainer(assert = x:Result<Any|m>[1] | true)),
    tests = ^ServiceTest(id = 'test1', assertions = ^EqualTo(id = 'assertion1', expected = ''))
  );
}

function meta::pure::persistence::test::NestedM2mServiceWithFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Graph fetch m2m service into a flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::NestedM2mService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^FlatTarget(
        targetName = 'FlatTable',
        deduplicationStrategy = ^NoDeduplicationStrategy()
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}

function meta::pure::persistence::test::NestedM2mServiceWithMultiFlatPersistence(): Persistence[1]
{
  ^Persistence(
    documentation = 'Graph fetch m2m service into a multi flat target',
    trigger = ^ManualTrigger(),
    service = meta::pure::persistence::test::NestedM2mService(),
    persister = ^BatchPersister(
      ingestMode = ^UnitemporalDelta(
        mergeStrategy = ^NoDeletesMergeStrategy(),
        transactionMilestoning = ^BatchIdTransactionMilestoning(
          batchIdInName = 'IN_Z',
          batchIdOutName = 'OUT_Z'
        )
      ),
      sink = ^RelationalSink(database = meta::pure::persistence::test::RelationalDatabase),
      targetShape = ^MultiFlatTarget(
        modelClass = meta::pure::persistence::test::WrapperOutput,
        transactionScope = TransactionScope.ALL_TARGETS,
        parts = [
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput1')->first()->toOne(),
            targetName = 'FlatTable1',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          ),
          ^MultiFlatTargetPart(
            modelProperty = meta::pure::persistence::test::WrapperOutput.properties->filter(p | $p.name == 'flatOutput2')->first()->toOne(),
            targetName = 'FlatTable2',
            deduplicationStrategy = ^NoDeduplicationStrategy()
          )
        ]
      )
    ),
    notifier = ^Notifier(notifyees = [])
  );
}
