import meta::pure::graphFetch::execution::*;
import meta::pure::router::preeval::*;
import meta::external::format::openapi::metamodel::*;
import meta::pure::service::*;
import meta::legend::service::metamodel::*;
import meta::external::format::openapi::generation::*;
import meta::pure::functions::meta::*;
import meta::pure::metamodel::path::*;
import meta::pure::service::*;
import meta::external::format::openapi::profiles::*;
import meta::pure::generation::metamodel::*;
import meta::external::query::sql::transformation::queryToPure::tests::*;
import meta::pure::graphFetch::*;
import meta::external::format::openapi::tostring::*;

Profile meta::external::format::openapi::profiles::ServiceSpecGeneration {
  stereotypes: [OpenAPI];
  tags: [specification, definition];
}

Class meta::external::format::openapi::generation::FunctionInfo
{
  version: String[0..1];
  title: String[0..1];
  func: Function<Any>[1];
  tags: String[*];
  urlPattern: String[1];
  paramsExample: Map<String, String>[1];
}

Class meta::external::format::openapi::generation::OpenAPISchemaConfig extends GenerationConfiguration
{
  {doc.doc='Generate a components collection' }createSchemaCollection:Boolean[0..1];
  
}

Class meta::external::format::openapi::generation::OpenApiGenerationOutput extends GenerationOutput
{

}

function <<access.private>> meta::external::format::openapi::generation::defaultConfig():OpenAPISchemaConfig[1]
{
 ^OpenAPISchemaConfig(createSchemaCollection=false);
}

function <<Generation.Configuration>> meta::external::format::openapi::generation::describeConfiguration():GenerationParameter[*]
{
   meta::pure::generation::describeConfiguration(OpenAPISchemaConfig,meta::external::format::openapi::generation::defaultConfig__OpenAPISchemaConfig_1_,[]);
}

function meta::external::format::openapi::generation::generateOpenApiFromPure(config:OpenAPISchemaConfig[1]):OpenApiGenerationOutput[*]
{
  $config.scopeElements->filter(e|!$e->instanceOf(Package))->map(e|assert($e->instanceOf(Service),'OpenApi generation supported only for legend services'));
  let scopeElements = $config.allPackageScopeElements->filter(e|$e->instanceOf(Service));
  let specs=$scopeElements->map(s|^OpenApiGenerationOutput(content=$s->cast(@Service)->serviceToOpenApi(^Server(url='/test')),fileName=$s->typeName(),format='json'));
  $specs;
}

function <<ServiceSpecGeneration.OpenAPI>> meta::external::format::openapi::generation::serviceToOpenApi(service: Service[1], server: Server[1]):String[1]
{
  ^FunctionInfo(
    func = $service.execution->cast(@PureExecution).func,
    tags = 'definition',
    urlPattern = $service.pattern,
    version = '1.0.0',
    title = 'Legend API',
    paramsExample = ^Map<String,String>()
  )->meta::external::format::openapi::generation::funcInfoToOpenApi($server)
  ->asString()->makeString()->replace('\\/','/');
}

function <<access.private>> meta::external::format::openapi::generation::funcInfoToOpenApi(funcInfos:FunctionInfo[*], server:Server[1]):OpenApi[1]
{
  let allClasses = $funcInfos->returnTypesFromFunctions();
  ^OpenApi(
    openapi='3.0.0',
    info=^Info(title='Legend API', version='1.0.0'),
    servers=[^Server(url=$server.url)],
    paths=newMap($funcInfos->map(fi|let url = $fi.urlPattern;
                                    let paramsExample = $fi.paramsExample;
                                    let func = $fi->getEvaluatedExecutionFunction();
                                    let ret = $fi->getReturnType();
                                    let responseSchema = $ret->getResponseSchema([]);

                                    
                                    pair($url,
                                            ^PathItem(
                                                      get=^Operation(
                                                      tags=$fi.tags,
                                                      parameters = $func->buildParameterSchema($paramsExample),
                                                      responses = $responseSchema
                                                      ),
                                                      post=^Operation(
                                                      tags=$fi.tags,
                                                      responses = $responseSchema
                                                      )
                                                    )
                                          );
                                  )
                  ),  
    components= ^Components(schemas=newMap(
                    $allClasses->map(
                          c|pair(
                            $c.rawType.name->toOne(),
                            ^Schema
                            (
                              type='object',
                              properties=newMap(
                                $c.rawType->cast(@Class<Any>).properties
                                  ->filter(p|$p.name!='elementOverride' && $p.name!='classifierGenericType')
                                  ->map(p|
                                          pair
                                          (
                                            $p.name->toOne(),
                                            $p->functionReturnType()->buildComponent($p.multiplicity)
                                          )
                                      )
                              )
                            )
                          )
                    )
    )));
}

function <<access.private>> meta::external::format::openapi::generation::getEvaluatedExecutionFunction(funcInfo:FunctionInfo[1]):FunctionDefinition<Any>[1]
{
  $funcInfo.func->cast(@FunctionDefinition<Any>)->preval(meta::pure::extension::defaultExtensions())
}

function <<access.private>> meta::external::format::openapi::generation::getReturnType(funcInfo:FunctionInfo[1]): Any[1]
{
  let func = $funcInfo->getEvaluatedExecutionFunction();
  let ftype = $func->functionReturnType();
  if($ftype.rawType==TabularDataSet,|$ftype, | let expr = $func.expressionSequence->evaluateAndDeactivate()->last()->cast(@FunctionExpression);
                                               assert($expr->cast(@FunctionExpression)->toOne()->isSerialize());
                                               $expr.parametersValues->at(1)->cast(@InstanceValue).values->toOne(););
}


function <<access.private>> meta::external::format::openapi::generation::returnTypesFromFunctions(funcInfos:FunctionInfo[*]):GenericType[*]
{
  $funcInfos->map(f|$f->getReturnType())
  ->map(r| if($r->instanceOf(RootGraphFetchTree), | $r.classifierGenericType->toOne(), | $r))
  ->map(r|$r->cast(@GenericType)->toOne())
  ->map(r|if($r.rawType==JSONResult||$r.rawType==RootGraphFetchTree,|$r.typeArguments,|$r))
  ->filter(c|!$c.rawType->isEmpty() && $c.rawType->toOne()->instanceOf(Class))
  ->map(t|if($t.rawType.specializations->isEmpty(),|$t,|$t.rawType->toOne().classifierGenericType.typeArguments))
  ->removeDuplicates(cmpGenericType_GenericType_1__GenericType_1__Boolean_1_)
  ->map(t|$t->scanClass([]))
  ->removeDuplicates(cmpGenericType_GenericType_1__GenericType_1__Boolean_1_)
  ->sortBy(a|$a.rawType.name->toOne());
}



function <<access.private>> meta::external::format::openapi::generation::buildParameterSchema(func:FunctionDefinition<Any>[1], paramsExample:Map<String,String>[1]):ParameterOrReference[*]
{
  $func->functionType().parameters->evaluateAndDeactivate()
      ->map(p|let val=$paramsExample->get($p.name->toOne());
              let required = $p.multiplicity->hasLowerBound() && $p.multiplicity->getLowerBound() == 1;
              let type = $p.genericType.rawType->match([
              p:PrimitiveType[1] | $p->primitiveType(),
              e:Enumeration<Enum>[1]| 'string'
              ]);
              let enums = $p.genericType.rawType->match([
                  p: PrimitiveType[1] | [],
                  e: Enumeration<Enum>[1] | $e->enumValues().name,
                  c: Class<Any>[1] | []
              ]);
              ^Parameter(
              name=$p.name->toOne(),
              in = if($required, | In.path, | In.query),
              required = $required
              );
            );
}



function <<access.private>> meta::external::format::openapi::generation::getResponseSchema(schema:Any[1],m:Multiplicity[0..1]):Map<String,Response>[1]
{
  newMap(
    pair('200', ^Response(
                  description='success',
                  content=$schema->buildResponseSchema([])
                )
    )
  )
}

function <<access.private>> meta::external::format::openapi::generation::buildResponseSchema(tree:Any[1], m:Multiplicity[0..1]):Map<String,MediaType>[1]
{
  if($tree->instanceOf(GenericType), | $tree->cast(@GenericType)->buildComponent($m)->map(res|newMap(pair('application/json', ^MediaType(schema=$res)))), 
  | $tree->getSchemaFromTree()->map(res|newMap(pair('application/json',^MediaType(schema=$res)))));
}

function meta::external::format::openapi::generation::getSchemaFromTree(tree: Any[1]): SchemaOrReference[1]
{
  assert($tree->instanceOf(RootGraphFetchTree));
  let graph = $tree->cast(@RootGraphFetchTree<Any>);
  $graph.classifierGenericType->toOne()->buildComponent([]);
}

function <<access.private>> meta::external::format::openapi::generation::buildComponent(type:GenericType[1], m:Multiplicity[0..1]):SchemaOrReference[1]
{
  $type->map(r|if($r.rawType==JSONResult||$r.rawType==RootGraphFetchTree,|$r.typeArguments,|$r)).rawType
  ->match(
    [
      r: PrimitiveType[1]|^Schema(type=$r->primitiveType()),
      e: Enumeration<Enum>[1]|^Schema(type='string', enum=$e->enumValues().name),
      c: Class<Any>[1]| if(!$m->isEmpty() && $m->toOne()->isToMany(),
                           |^Schema(type='array', items=^Reference(ref=$c.name->toOne())),
                           |^Reference(ref=$c.name->toOne())
      ),
      a: Any[*]| ^Schema(type='object')
    ]
  )
}

function <<access.private>> meta::external::format::openapi::generation::primitiveType(type:PrimitiveType[1]):String[1]
{
  [
    pair(String, 'string'),
    pair(Date, 'dateTime'),
    pair(DateTime, 'dateTime'),
    pair(StrictDate, 'date'),
    pair(Boolean, 'boolean'),
    pair(Integer, 'integer'),
    pair(Float, 'float'),
    pair(Number, 'float')
  ]->filter(t|$t.first==$type).second->toOne();
}


function <<access.private>> meta::external::format::openapi::generation::scanClass(c:GenericType[1], allClasses:GenericType[*]):GenericType[*]
{
  if($c.rawType->toOne()==GenericType, | [], | 
  
  let propertyTypes = $c->properties()->filter(p|$p.owner!=Any)
                        ->map(p|
                                $p->functionReturnType()
                                  ->filter(c|!$c.rawType->isEmpty() && $c.rawType->toOne()->instanceOf(Class))
                                  ->filter(t|!$t.rawType->toOne()->eq(GenericType) && !$t.rawType->toOne()->eq(Generalization))
                              );
  let diff = $propertyTypes->removeAll($allClasses, cmpGenericType_GenericType_1__GenericType_1__Boolean_1_)
                            ->removeDuplicates(cmpGenericType_GenericType_1__GenericType_1__Boolean_1_);
  $c->concatenate($diff->map(z|$z->scanClass($allClasses->concatenate($c->concatenate($diff))))););
  
}

function <<access.private>> meta::external::format::openapi::generation::cmpGenericType(t1: GenericType[1], t2:GenericType[1]):Boolean[1]
{
  $t1.rawType == $t2.rawType &&
  $t1.typeArguments->size() == $t2.typeArguments->size() &&
  $t1.typeArguments->zip($t2.typeArguments)->fold({ta,f|$f && $ta.first.rawType==$ta.second.rawType}, true)
}

function <<access.private>> meta::external::format::openapi::generation::processParamsExample(val: String[0..1]):Map<String,String>[1]
{
  if($val->isEmpty(),
  |^Map<String,String>(),
  |newMap($val->toOne()->split(',')->map(s|$s->trim())->map(v|let res=$v->split('='); pair($res->at(0)->trim(), $res->at(1)->trim());))
  );
}