// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::extension::*;
import meta::external::language::java::metamodel::*;

import meta::pure::executionPlan::engine::java::*;

import meta::pure::graphFetch::*;
import meta::pure::graphFetch::routing::*;
import meta::pure::mapping::*;
import meta::pure::router::clustering::*;
import meta::pure::extension::*;
import meta::pure::store::*;
import meta::pure::runtime::*;

import meta::java::generation::code::*;

import meta::external::store::service::extension::*;
import meta::external::store::service::router::systemMapping::*;
import meta::external::store::service::executionPlan::engine::java::*;
import meta::external::store::service::executionPlan::nodes::*;
import meta::external::store::service::executionPlan::generation::*;
import meta::external::store::service::executionPlan::toString::*;
import meta::external::store::service::metamodel::*;
import meta::external::store::service::metamodel::mapping::*;
import meta::external::store::service::metamodel::runtime::*;


Class meta::external::store::service::extension::ServiceStoreExtension extends ModuleExtension
{
   executionPlan_print_securitySchemeToString : Function<{Nil[1] -> String[1]}>[*];
}

function meta::external::store::service::extension::serviceStoreExtensions()  : meta::pure::extension::Extension[1]
{
   serviceStoreExtension('serviceStore')
}

function meta::external::store::service::extension::serviceStoreSerializerExtension(version:String[1]):String[1]
{
   'meta::protocols::pure::'+$version+'::extension::store::service::getServiceStoreExtension_String_1__SerializerExtension_1_';
}

function <<access.private>> meta::external::store::service::extension::serviceStoreExtension(type:String[1]) : meta::pure::extension::Extension[1]
{
   ^Extension
   (
      type = $type,
      availableStores = [meta::external::store::service::contract::serviceStoreStoreContract()],
      serializerExtension =  meta::external::store::service::extension::serviceStoreSerializerExtension_String_1__String_1_,
      graphExtension_calculateSourceTree = {tree:RootGraphFetchTree<Any>[1] |
         {rsi: RootServiceInstanceSetImplementation[1] |
            $tree;
         }
      },
      graphFetch_executionPlan_addCrossStoreChildren = {
         clusteredTree: ClusteredGraphFetchTree[1], orderedPaths: String[*], parentPath: String[1], inScopeVars: Map<String, List<Any>>[1], mapping: Mapping[1], runtime: Runtime[1], exeCtx: ExecutionContext[1], enableConstraints: Boolean[1], checked: Boolean[1], extensions: Extension[*], debug: DebugContext[1] |
         {s : ServiceStore[1] | meta::external::store::service::executionPlan::generation::planCrossStoreGraphFetchExecutionServiceStore($clusteredTree, $orderedPaths, $parentPath, $inScopeVars, $mapping, $runtime, $exeCtx, $enableConstraints, $checked, $extensions, $debug)}
      },
      java_graphFetch_inMemory_applyTypeFilter = {
         set:InstanceSetImplementation[1], src:Code[1], withoutFilter:Code[1..*], context:GenerationContext[1], debug:DebugContext[1] | [
            rsi: RootServiceInstanceSetImplementation[1] | $withoutFilter,
            esi: EmbeddedServiceStoreSetImplementation[1] | $withoutFilter
         ]
      },
      java_graphFetch_inMemory_generatePropertyMapping1 = [
         {spm : ServiceStorePropertyMapping[1] | let dummyLambda = {|'ok'};
                                                 let gen = ^GenericType
                                                            (
                                                               rawType = LambdaFunction,
                                                               typeArguments = ^GenericType
                                                                                 (
                                                                                    rawType = ^meta::pure::metamodel::type::FunctionType
                                                                                             (
                                                                                                parameters = [],
                                                                                                returnType = $spm.property->functionReturnType(),
                                                                                                returnMultiplicity= $spm.property->functionReturnMultiplicity()
                                                                                             )
                                                                                 )
                                                            );
                                                 let exp = ^SimpleFunctionExpression(func = $spm.property,
                                                                                     genericType = $spm.property.genericType,
                                                                                     importGroup = system.children->at(0)->cast(@Package).children->at(0)->cast(@ImportGroup),
                                                                                     multiplicity = $spm.property.multiplicity,
                                                                                     parametersValues = ^VariableExpression(genericType=^GenericType(rawType = $spm.property.owner->cast(@meta::pure::metamodel::type::Class<Any>)), multiplicity = PureOne, name = 'src'),
                                                                                     propertyName = ^InstanceValue(genericType = ^GenericType(rawType = String), multiplicity = PureOne, values = $spm.property.name));
                                                 ^$dummyLambda(classifierGenericType = $gen, expressionSequence = $exp);},
         {epm : EmbeddedServiceStoreSetImplementation[1] | let dummyLambda = {|'ok'};
                                                 let gen = ^GenericType
                                                            (
                                                               rawType = LambdaFunction,
                                                               typeArguments = ^GenericType
                                                                                 (
                                                                                    rawType = ^meta::pure::metamodel::type::FunctionType
                                                                                             (
                                                                                                parameters = [],
                                                                                                returnType = $epm.property->functionReturnType(),
                                                                                                returnMultiplicity= $epm.property->functionReturnMultiplicity()
                                                                                             )
                                                                                 )
                                                            );
                                                 let exp = ^SimpleFunctionExpression(func = $epm.property,
                                                                                     genericType = $epm.property.genericType,
                                                                                     importGroup = system.children->at(0)->cast(@Package).children->at(0)->cast(@ImportGroup),
                                                                                     multiplicity = $epm.property.multiplicity,
                                                                                     parametersValues = ^VariableExpression(genericType=^GenericType(rawType = $epm.property.owner->cast(@meta::pure::metamodel::type::Class<Any>)), multiplicity = PureOne, name = 'src'),
                                                                                     propertyName = ^InstanceValue(genericType = ^GenericType(rawType = String), multiplicity = PureOne, values = $epm.property.name));
                                                 ^$dummyLambda(classifierGenericType = $gen, expressionSequence = $exp);}
      ],
      java_graphFetch_inMemory_generatePropertyMapping2 = [
         spm : ServiceStorePropertyMapping[1] |  [],
         epm : EmbeddedServiceStoreSetImplementation[1] | []
      ],
      plan_javaRuntime_main_generatePlatformCode1 =
            {path:String[1], context:GenerationContext[1], extensions:Extension[*], debug:DebugContext[1] |
               [
                  s:ServiceParametersResolutionExecutionNode[1] | $s->generateImplementationForServiceParametersResolutionNode($path, $context, $debug)
               ]
            },
      plan_javaRuntime_main_prepare2 =
            {path:String[1], contextFromOtherNodes:GenerationContext[1], extensions:Extension[*], debug:DebugContext[1] |
               [
                  l:LimitExecutionNode[1] | $l->prepareForLimitExecutionNode($path, $contextFromOtherNodes, $debug)
               ]
            }
   )
}
