// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Relational
Database demo::stores::DemoDb
(
  Schema DEMO_SCHEMA
  (
    Table EMPLOYEE
    (
      ID VARCHAR(16777216) PRIMARY KEY,
      EMPLOYEE_NAME VARCHAR(16777216)
    )
  )
)


###Snowflake
SnowflakeM2MUdf test::udf::MyUDF
{
   udfName : 'MyUDF';
   function : test::query::getFirmDetailsWithInput(String[1]):String[1];
   ownership : Deployment { identifier: '1234'};
   description : 'A simple Snowflake M2M Udf!';
   deploymentSchema : 'legend_native_apps_1';
   deploymentStage : 'demo_schema.snowflakeStage';
   activationConfiguration : demo::connections::DeploymentConnection;
}

###Pure
Class test::model::Firms
{
  firms: test::model::Firm[1..*];
}

Class test::model::Firm
{
  legalName: String[1];
  employees: test::model::Person[*];
}

Class test::model::Person
{
  firstName: String[1];
  lastName: String[1];
}

function test::query::getFirmDetailsWithInput(input: String[1]): String[1]
{
  test::model::Firms.all()->graphFetch(
    #{
      test::model::Firms{
        firms{
          legalName,
          employees{
            firstName,
            lastName
          }
        }
      }
    }#
  )->from(
       test::mapping::firmMapping,
       test::model::Firms->getRuntimeWithModelQueryConnection(
         'application/json',
         $input
       )
     )->serialize(
    #{
      test::model::Firms{
        firms{
          legalName,
          employees{
            firstName,
            lastName
          }
        }
      }
    }#
  )
}

###Mapping
Mapping test::mapping::firmMapping
(
  *test::model::Firms: Pure
  {
    ~src test::model::Firms
    firms: $src.firms
  }
  *test::model::Firm: Pure
  {
    ~src test::model::Firm
    legalName: $src.legalName,
    employees: $src.employees
  }
)

###Connection
JsonModelConnection test::connection::testConnectionWithInput
{
  class: test::model::Firms;
  url: 'data:application/json,${input}';
}

###Runtime
Runtime test::runtime::testRuntimeWithInput
{
  mappings:
  [
    test::mapping::firmMapping
  ];
  connections:
  [
    ModelStore:
    [
      connection_1: test::connection::testConnectionWithInput
    ]
  ];
}

###Connection
RelationalDatabaseConnection demo::connections::DeploymentConnection
{
  store: demo::stores::DemoDb;
  type: Snowflake;
  specification: Snowflake
      {
           name: 'dbName';
           account: 'account';
           warehouse: 'warehouse';
           region: 'region';
      };
    auth: DefaultH2;
}
