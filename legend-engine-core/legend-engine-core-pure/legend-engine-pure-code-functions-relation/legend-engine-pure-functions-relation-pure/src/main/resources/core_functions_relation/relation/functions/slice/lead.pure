// Copyright 2024 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::test::pct::*;
import meta::pure::metamodel::relation::*;
import meta::pure::functions::relation::*;

function <<functionType.NormalizeRequiredFunction,
           PCT.function>> meta::pure::functions::relation::lead<T>(w:Relation<T>[1],r:T[1]):T[0..1]
{
  lead($w, $r, 1)
}

function <<functionType.NormalizeRequiredFunction,
           PCT.function>> meta::pure::functions::relation::lead<T>(w:Relation<T>[1],r:T[1], offset:Integer[1]):T[0..1]
{
  offset($w, $r, $offset);
}

function <<PCT.test, PCTRelationQualifier.relation, PCTRelationQualifier.olap>> meta::pure::functions::relation::tests::lead::testOLAPWithPartitionAndOrderWindow<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
    let expr = {
                | #TDS
                  id, grp, name
                  1, 2, A
                  2, 1, B
                  3, 3, C
                  4, 4, D
                  5, 2, E
                  6, 1, F
                  7, 3, G
                  8, 1, H
                  9, 5, I
                  10, 0, J
                #->extend(over(~grp, ~id->descending()), ~newCol:{p,w,r|$p->lead($r).id})
               };


    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   id,grp,name,newCol\n'+
                  '   10,0,J,null\n'+
                  '   8,1,H,6\n'+
                  '   6,1,F,2\n'+
                  '   2,1,B,null\n'+
                  '   5,2,E,1\n'+
                  '   1,2,A,null\n'+
                  '   7,3,G,3\n'+
                  '   3,3,C,null\n'+
                  '   4,4,D,null\n'+
                  '   9,5,I,null\n'+
                  '#', $res->sort([~grp->ascending(), ~id->descending()])->toString());
}
