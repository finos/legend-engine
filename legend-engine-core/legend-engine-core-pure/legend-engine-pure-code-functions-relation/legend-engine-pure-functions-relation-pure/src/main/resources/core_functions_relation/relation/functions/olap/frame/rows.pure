// Copyright 2024 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::test::pct::*;

Class meta::pure::functions::relation::Rows extends Frame
{
}

function
    <<functionType.NormalizeRequiredFunction,
    PCT.function>>
    {
         doc.doc = 'Returns a window frame, which defines a physical number of rows specified by starting and ending points. For example, rows(-3, 0) means start 3 rows before the current row and include all rows up to and including the current one, which is equivalent to ROWS BETWEEN 3 PRECEDING AND CURRENT ROW. Note that negative numbers represent N PRECEDING, positive numbers represent N FOLLOWING, and 0 represents CURRENT ROW.'
    }
    meta::pure::functions::relation::rows(offsetFrom:Integer[1], offsetTo:Integer[1]):Rows[1]
{
  assert($offsetFrom <= $offsetTo, 'Invalid window frame boundary - lower bound of window frame cannot be greater than the upper bound!');
  ^Rows(offsetFrom = ^FrameIntValue(value=$offsetFrom), offsetTo = ^FrameIntValue(value=$offsetTo));
}

function
    <<functionType.NormalizeRequiredFunction,
    PCT.function>>
    {
         doc.doc = 'Returns a window frame, which defines a physical number of rows specified by starting and ending points. For example, rows(meta::pure::functions::relation::unbounded(), -2) means start from the very first row in the partition and include all rows up to 2 rows before current row, which is equivalent to ROWS BETWEEN UNBOUNDED PRECEDING AND 2 PRECEDING. Note that negative numbers represent N PRECEDING, positive numbers represent N FOLLOWING, and 0 represents CURRENT ROW.'
    }
    meta::pure::functions::relation::rows(offsetFrom:meta::pure::functions::relation::UnboundedFrameValue[1], offsetTo:Integer[1]):Rows[1]
{
  ^Rows(offsetFrom = $offsetFrom, offsetTo = ^FrameIntValue(value=$offsetTo));
}

function
    <<functionType.NormalizeRequiredFunction,
    PCT.function>>
    {
         doc.doc = 'Returns a window frame, which defines a physical number of rows specified by starting and ending points. For example, rows(3, meta::pure::functions::relation::unbounded()) means start counting 3 rows after the current row and include all rows after that, all the way to the end of the partition, which is equivalent to ROWS BETWEEN 3 FOLLOWING AND UNBOUNDED FOLLOWING. Note that negative numbers represent N PRECEDING, positive numbers represent N FOLLOWING, and 0 represents CURRENT ROW.'
    }
    meta::pure::functions::relation::rows(offsetFrom:Integer[1], offsetTo:meta::pure::functions::relation::UnboundedFrameValue[1]):Rows[1]
{
  ^Rows(offsetFrom = ^FrameIntValue(value=$offsetFrom), offsetTo = $offsetTo);
}

function
    <<functionType.NormalizeRequiredFunction,
    PCT.function>>
    {
         doc.doc = 'Returns a window frame, which defines a physical number of rows specified by starting and ending points. For example, rows(meta::pure::functions::relation::unbounded(), meta::pure::functions::relation::unbounded()) means include every row in the partition - from the very first row to the very last row - no matter which row you are currently on, which is equivalent to ROWS BETWEEN UNBOUNDED PRECEDING AND UNBOUNDED FOLLOWING. Note that negative numbers represent N PRECEDING, positive numbers represent N FOLLOWING, and 0 represents CURRENT ROW.'
    }
    meta::pure::functions::relation::rows(offsetFrom:meta::pure::functions::relation::UnboundedFrameValue[1], offsetTo:meta::pure::functions::relation::UnboundedFrameValue[1]):Rows[1]
{
  ^Rows(offsetFrom = $offsetFrom, offsetTo = $offsetTo);
}