// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::functions::variant::convert::tests::toMany::*;
import meta::pure::test::pct::*;
import meta::pure::metamodel::variant::*;
import meta::pure::functions::variant::convert::*;


native function
    <<PCT.function>>
    {
        doc.doc='Converts the $variant to the given $type if the variant represent an array, failing if is not an array.\n' +
                 'This will fail if array values cannot be coerced.'
    }
meta::pure::functions::variant::convert::toMany<T>(variant: Variant[0..1], type: T[1], typeKeyName: String[1], typeLookup: Pair<String,Class<Any>>[*]): T[*];

native function
    <<PCT.function>>
    {
        doc.doc='Converts the $variant to the given $type if the variant represent an array, failing if is not an array.\n' +
                 'This will fail if array values cannot be coerced.'
    }
meta::pure::functions::variant::convert::toMany<T>(variant: Variant[0..1], type: T[1]): T[*];
//{
//    toMany($variant, $type, '_type', [])
//}

function <<PCT.test, PCTCoreQualifier.variant>> meta::pure::functions::variant::convert::tests::toMany::testToManyVariant<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
    let listVariant = $f->eval(|fromJson('[1, 2, 3]')->toMany(@Variant));
    assertEquals(1, $listVariant->at(0)->to(@Integer));
    assertEquals(2, $listVariant->at(1)->to(@Integer));
    assertEquals(3, $listVariant->at(2)->to(@Integer));
}

function <<PCT.test, PCTCoreQualifier.variant>> meta::pure::functions::variant::convert::tests::toMany::testToManyInteger<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
    let listInteger = $f->eval(|fromJson('[1, 2, 3]')->toMany(@Integer));
    assertEquals(1, $listInteger->at(0));
    assertEquals(2, $listInteger->at(1));
    assertEquals(3, $listInteger->at(2));
}

function <<PCT.test, PCTCoreQualifier.variant>> meta::pure::functions::variant::convert::tests::toMany::testToManyFromNonArray<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
    assertError(|$f->eval(|fromJson('"not an array"')->toMany(@Variant)), 'Expect variant that contains an \'ARRAY\', but got \'STRING\'');
}

function <<PCT.test, PCTCoreQualifier.variant>> meta::pure::functions::variant::convert::tests::toMany::testToClass<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
    let result = $f->eval(|fromJson('[{"firstName": "John", "lastName": "Smith"}, {"firstName": "Jane", "lastName": "Smith"}]')->toMany(@Person));
    assertEquals([^Person(firstName = 'John', lastName = 'Smith'), ^Person(firstName = 'Jane', lastName = 'Smith')], $result);
}

function <<PCT.test, PCTCoreQualifier.variant>> meta::pure::functions::variant::convert::tests::toMany::testToClassWithInheritance<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
    let result = $f->eval(|fromJson('[{"_type": "Dog", "name": "Doggo", "isPuppy": true}, {"_type": "Cat", "name": "Gato", "isKitten": false}]')->toMany(@Pet));
    assertEquals([^Dog(name = 'Doggo', isPuppy = true), ^Cat(name = 'Gato', isKitten = false)], $result);
}

//written as standard test as we are testing special handling of @type only triggered via reactivate
function <<test.Test>> meta::pure::functions::variant::convert::tests::toMany::testReactivate():Boolean[1]
{
    let listInteger = {|fromJson('[1, 2, 3]')->toMany(@Integer)}.expressionSequence->at(0)->reactivate();
    assertEquals(1, $listInteger->at(0));
    assertEquals(2, $listInteger->at(1));
    assertEquals(3, $listInteger->at(2));
}

Class <<access.private>> meta::pure::functions::variant::convert::tests::toMany::Person
{
  <<equality.Key>> firstName: String[1];
  <<equality.Key>> lastName: String[1];
}

Class <<access.private>> meta::pure::functions::variant::convert::tests::toMany::Pet
{
  <<equality.Key>> name: String[1];  
  <<equality.Key>> owner: Person[0..1]; 
}


Class <<access.private>> meta::pure::functions::variant::convert::tests::toMany::Dog extends meta::pure::functions::variant::convert::tests::toMany::Pet
{
  <<equality.Key>> isPuppy: Boolean[1];
}

Class <<access.private>> meta::pure::functions::variant::convert::tests::toMany::Cat extends meta::pure::functions::variant::convert::tests::toMany::Pet
{
  <<equality.Key>> isKitten: Boolean[1];  
}