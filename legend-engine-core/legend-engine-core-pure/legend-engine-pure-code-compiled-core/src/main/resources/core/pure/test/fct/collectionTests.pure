// // Copyright 2024 Goldman Sachs
// //
// // Licensed under the Apache License, Version 2.0 (the "License");
// // you may not use this file except in compliance with the License.
// // You may obtain a copy of the License at
// //
// //      http://www.apache.org/licenses/LICENSE-2.0
// //
// // Unless required by applicable law or agreed to in writing, software
// // distributed under the License is distributed on an "AS IS" BASIS,
// // WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// // See the License for the specific language governing permissions and
// // limitations under the License.
import meta::pure::mapping::*;
import meta::pure::graphFetch::execution::*;
import meta::pure::testCoverage::featureMatrix::*;
import meta::pure::test::fct::model::FCT::*;
import meta::pure::fct::tests::collection::*;
import meta::pure::fct::*;

Class meta::pure::fct::tests::collection::EntityWithAddress
{
  address:meta::pure::fct::tests::collection::Address[0..1];
}

Class meta::pure::fct::tests::collection::Address
{
   street : String[1];
   type: AddressType[1];
}

Class meta::pure::fct::tests::collection::Firm extends EntityWithAddress
{
   legalName : String[1];
   optionalDescription : String[0..1];
   
}

Class meta::pure::fct::tests::collection::Person extends EntityWithAddress

{
   firstName : String[1];
   lastName : String[1];
   description : String[0..1];
   birthDate: StrictDate[1];
}

Enum meta::pure::fct::tests::collection::AddressType
{
  PRIMARY,
  SECONDARY

}
 
Association meta::pure::fct::tests::collection::PersonFirm
{
   firm : Firm[1];
   employees : Person[*];
}



function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterPrimitive,QueryType.tds>>  meta::pure::fct::tests::collection::filter::filterTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->filter(i|$i.firstName  == 'Joe')->project(p|[$p.firstName],['firstName'])};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterPrimitive,QueryType.tds>>  meta::pure::fct::tests::collection::filter::filterPropertyFromSuperTypeTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->filter(i|$i.address.street  == 'FIRST')->project(p|[$p.firstName],['firstName'])};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterAssociation,QueryType.tds>>  meta::pure::fct::tests::collection::filter::filterAssociationTDS():FunctionDefinition<{->Any[*]}>[1]
{
   {|Person.all()->filter(i|$i.firm.legalName->in(['Co']))->project(p|[$p.firstName],['firstName'])};
}


function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterAssociationIsEmpty,QueryType.tds>>  meta::pure::fct::tests::collection::filter::filterAssociationIsEmptyTDS():FunctionDefinition<{->Any[*]}>[1]
{
   {|Person.all()->filter(i|$i.firm.optionalDescription->isEmpty())->project([p|$p.firstName,p|$p.firm.legalName],['firstName','legalName'])};
}


function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.sort,QueryType.tds>>  meta::pure::fct::tests::collection::sort::sortTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->sortBy(i|$i.firstName)->project(p|[$p.firstName],['firstName'])};

}
function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.sort, QueryType.tds>>  meta::pure::fct::tests::collection::sort::sortAssociationTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->sortBy(i|$i.firm.legalName)->project(p|[$p.firstName],['firstName'])};

}



function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.groupBy,QueryType.tds>>  meta::pure::fct::tests::collection::groupBy::groupByTDS():FunctionDefinition<{->Any[*]}>[1]
{
 
   {|Person.all()->groupBy(i|[$i.firstName],
                             agg( x|$x.lastName,
                                  y|$y->count()),  ['name', 'cnt'])};
}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.groupBy,QueryType.tds>>  meta::pure::fct::tests::collection::groupBy::groupByMultiAggTDS():FunctionDefinition<{->Any[*]}>[1]
{
 
   {|Person.all()->groupBy(i|[$i.firstName],
                             [agg( x|$x.lastName,
                                  y|$y->count()),
                             agg( x|$x.birthDate,
                                  y|$y->count())]      
                                  ,  ['name', 'cnt','cnt2'])};
}


function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.take,QueryType.tds>>  meta::pure::fct::tests::collection::take::takeTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->project(p|[$p.firstName],['firstName'])->take(1)};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.first,QueryType.tds>>  meta::pure::fct::tests::collection::first::firstTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->project(p|[$p.firstName],['firstName'])->first()};

}



function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.drop,QueryType.tds>>  meta::pure::fct::tests::collection::drop::dropTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->project(p|[$p.firstName],['firstName'])->drop(0)};

}


function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.slice,QueryType.tds>>  meta::pure::fct::tests::collection::slice::sliceTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->project(p|[$p.firstName],['firstName'])->slice(0,1)};

}
function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.exists,QueryType.tds>>  meta::pure::fct::tests::collection::exists::existsRecursiveTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->filter(p | $p.firm.employees->exists(e | $e.firstName == 'Joe'))->project(p|[$p.firstName],['firstName'])};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.exists,QueryType.tds>>  meta::pure::fct::tests::collection::exists::existsTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Firm.all()->filter(p | $p.employees->exists(e | $e.firstName == 'Joe'))->project(p|[$p.legalName],['legalName'])};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.distinct,QueryType.tds>>  meta::pure::fct::tests::collection::distinct::distinctTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
 {  
   {|Person.all()->project(p|[$p.firstName],['firstName'])->distinct()};

}



function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterPrimitive,QueryType.graphFetch>>  meta::pure::fct::tests::collection::filter::filterGraphFetch():FunctionDefinition<{->Any[*]}>[1]
 { 
   {|Person.all()->filter(i|$i.firstName  == 'Joe')->graphFetch(#{Person{firstName}}#)};

}

function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterAssociation,QueryType.graphFetch>>  meta::pure::fct::tests::collection::filter::filterAssociationGraphFetch():FunctionDefinition<{->Any[*]}>[1]
 { 
   {|Person.all()->filter(i|$i.firm.legalName ->in(['Co']))->graphFetch(#{Person{firstName}}#)};

}


function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.filter, QuerySubFeature.filterAssociationIsEmpty,QueryType.graphFetch>>  meta::pure::fct::tests::collection::filter::filterAssociationIsEmptyGraphFetch():FunctionDefinition<{->Any[*]}>[1]
 { 
   {|Person.all()->filter(i|$i.firm.optionalDescription->isEmpty())->graphFetch(#{Person{firstName}}#)};

}



//TODO - these patterns fail in sql generation only with the ->from()
// function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.take,QueryType.tds>>  meta::pure::fct::tests::collection::take::takeBeforeProjectTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
//  {  
//    {|Person.all()->take(1)->project(p|[$p.firstName],['firstName'])};

// }

// function  <<meta::pure::test::fct::model::FCT.feature, QueryFeature.take,QueryType.tds>>  meta::pure::fct::tests::collection::last::lastTDS():FunctionDefinition<{->TabularDataSet[*]}>[1]
//  {  
//    {|Person.all()->project(p|[$p.firstName],['firstName'])->last()};

// }