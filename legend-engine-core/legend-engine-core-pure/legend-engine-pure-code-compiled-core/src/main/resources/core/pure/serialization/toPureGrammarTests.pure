import meta::pure::metamodel::serialization::grammar::*;
import meta::pure::metamodel::serialization::grammar::tests::testNew::*;

Enum meta::pure::metamodel::serialization::grammar::tests::testNew::MyEnum
{
  Val,
  Other
}

Class meta::pure::metamodel::serialization::grammar::tests::testNew::TestClass
{
  a : String[1];
  b : Boolean[1];
  c : Integer[1];
  d : Float[1];
  e : Date[1];
  f : MyEnum[1];
  g : Decimal[1];
}

function <<test.Test>> meta::pure::metamodel::serialization::grammar::tests::testNew::simple():Boolean[1]
{
  let val = ^TestClass(a = 'ok', b = true, c = 1, d = 1.0, e=%2015-04-15T17:09:21.398, f = MyEnum.Other, g = 10D);
  assertEquals('^meta::pure::metamodel::serialization::grammar::tests::testNew::TestClass(a=\'ok\',b=true,c=1,d=1.0,e=%2015-04-15T17:09:21.398+0000,f=meta::pure::metamodel::serialization::grammar::tests::testNew::MyEnum.Other,g=10D)', $val->printNew());
  true;
}

 
//these tests the correct fullPath serialization, In Legend Compiler it wrongly gets set so we handle this here until fixed.
function <<test.Test>> meta::pure::metamodel::serialization::grammar::tests::testFunctionDefinition():Boolean[1]
{
  let func = meta::pure::metamodel::serialization::grammar::tests::testFunction__String_1_;

  meta::pure::metamodel::serialization::grammar::tests::functionTest(meta::pure::metamodel::serialization::grammar::tests::testFunction__String_1_, 'meta::pure::metamodel::serialization::grammar::tests::testFunction(): String[1]\n{\n  \'abc\'\n}', true);
  meta::pure::metamodel::serialization::grammar::tests::functionTest(meta::pure::metamodel::serialization::grammar::tests::testFunction__String_1_, 'testFunction(): String[1]\n{\n  \'abc\'\n}', false);
}

function <<test.Test>> meta::pure::metamodel::serialization::grammar::tests::testFunctionNameReference():Boolean[1] 
{
  let lambda = {| meta::pure::metamodel::serialization::grammar::tests::testFunction()};
  let func = meta::pure::metamodel::serialization::grammar::tests::testFunction__String_1_;

  meta::pure::metamodel::serialization::grammar::tests::expressionTest(
    $lambda,
    $func,
    '|meta::pure::metamodel::serialization::grammar::tests::testFunction()',
    true
  );

  meta::pure::metamodel::serialization::grammar::tests::expressionTest(
    $lambda,
    $func,
    '|testFunction()',
    false
  );  
}

function <<test.Test>> meta::pure::metamodel::serialization::grammar::tests::testFunctionNameReferenceParameters():Boolean[1] 
{
  let lambda = {| meta::pure::metamodel::serialization::grammar::tests::testFunctionWithParams('a')};
  let func = meta::pure::metamodel::serialization::grammar::tests::testFunctionWithParams_String_1__String_1_;

  meta::pure::metamodel::serialization::grammar::tests::expressionTest(
    $lambda,
    $func,
    '|\'a\'->meta::pure::metamodel::serialization::grammar::tests::testFunctionWithParams()',
    true
  );

  meta::pure::metamodel::serialization::grammar::tests::expressionTest(
    $lambda,
    $func,
    '|\'a\'->testFunctionWithParams()',
    false
  );  
}

function meta::pure::metamodel::serialization::grammar::tests::functionTest(func:ConcreteFunctionDefinition<Any>[1], expected:String[1], fullPath:Boolean[1]):Boolean[1]
{
  let config = ^Configuration(fullPath = $fullPath);

  assertEquals('###Pure\nfunction ' + $expected, printPackageableElements($func, $config));
  assertEquals('###Pure\nfunction ' + $expected, printPackageableElements(^$func(functionName = $func.package->toOne()->elementToPath() + '::' + $func.functionName->toOne()), $config));

  assertEquals($expected, printFunctionDefinition($func, $config, ^GContext(space='')));
  assertEquals($expected, printFunctionDefinition(^$func(functionName = $func.package->toOne()->elementToPath() + '::' + $func.functionName->toOne()), $config, ^GContext(space='')));  
}

function meta::pure::metamodel::serialization::grammar::tests::expressionTest(lambda:LambdaFunction<Any>[1], func:ConcreteFunctionDefinition<Any>[1], expected:String[1], fullPath:Boolean[1]):Boolean[1]
{
  let config = ^Configuration(fullPath = $fullPath);

  let exp = $lambda.expressionSequence->at(0)->cast(@SimpleFunctionExpression);

  assertEquals($expected, printFunctionDefinition($lambda, $config, ^GContext(space='')));
  assertEquals($expected, printFunctionDefinition(^$lambda(expressionSequence = ^$exp(func = ^$func(functionName = $func.package->toOne()->elementToPath() + '::' + $func.functionName->toOne()))), $config, ^GContext(space='')));
}

function meta::pure::metamodel::serialization::grammar::tests::testFunction():String[1]
{
  'abc';
}

function meta::pure::metamodel::serialization::grammar::tests::testFunctionWithParams(a:String[1]):String[1]
{
  'abc';
}