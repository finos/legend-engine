// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::extension::*;
import meta::pure::graphFetch::*;
import meta::pure::router::clustering::*;
import meta::pure::router::metamodel::clustering::*;
import meta::pure::router::utils::*;
import meta::core::runtime::*;
import meta::pure::mapping::*;
import meta::pure::executionPlan::*;

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   $f->executionPlan(^meta::pure::runtime::ExecutionContext(), $extensions, noDebug());
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   $f->executionPlan(^meta::pure::runtime::ExecutionContext(), $extensions, $debugContext);
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   $f->executionPlan($context, $extensions, noDebug());
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   $f->executionPlan($m, $runtime, ^meta::pure::runtime::ExecutionContext(), $extensions, $debugContext);
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   $f->executionPlan($m, $runtime, ^meta::pure::runtime::ExecutionContext(), $extensions, noDebug());
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], shouldDebug:Boolean[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   $f->executionPlan($m, $runtime, ^meta::pure::runtime::ExecutionContext(), $extensions, if($shouldDebug,|debug(),|noDebug()));
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   $f->executionPlan($m, $runtime, $context, $extensions, noDebug());
}

Class meta::pure::executionPlan::NodeAndVars
{
   nodes : ExecutionNode[*];
   vars : Map<String, List<Any>>[1];
}

Class meta::pure::executionPlan::ExecutionOption
{
}

Class meta::pure::executionPlan::MultiExecutionContext extends meta::pure::runtime::ExecutionContext
{
    <<equality.Key>> childExecutionContext:meta::pure::runtime::ExecutionContext[*];
    allContexts()  {$this.childExecutionContext->map(e |if($e->instanceOf(MultiExecutionContext),
                                                              | $e->concatenate($e->cast(@MultiExecutionContext)->collectionMultiExecutionContexts([])),
                                                              |$e)
                                                        );  
                       }:meta::pure::runtime::ExecutionContext[*];
}
function meta::pure::executionPlan::getContexts(context:meta::pure::runtime::ExecutionContext[1]):meta::pure::runtime::ExecutionContext[*]
{
      if($context->instanceOf(MultiExecutionContext),
          | $context->concatenate($context->cast(@MultiExecutionContext).allContexts()),
          |$context);
}

function meta::pure::executionPlan::allContextsOfType<T>(context:meta::pure::runtime::ExecutionContext[1],contextFilter:Class<T>[1]):T[*]
{
      $context->getContexts()->filter(c|$c->instanceOf($contextFilter))->cast(@T);
}


function meta::pure::executionPlan::mergeContext(rootContext:meta::pure::runtime::ExecutionContext[1],childContext:meta::pure::runtime::ExecutionContext[1]):MultiExecutionContext[1]
  {
      if($rootContext->instanceOf(MultiExecutionContext),
              | let mult = $rootContext->cast(@MultiExecutionContext);
                 ^$mult(childExecutionContext+=$childContext );,
               |^MultiExecutionContext(childExecutionContext =$rootContext ->concatenate($childContext) );

      );      
              
  }



function <<access.private>> meta::pure::executionPlan::collectionMultiExecutionContexts(context:MultiExecutionContext[1], visited : meta::pure::runtime::ExecutionContext[*]):meta::pure::runtime::ExecutionContext[*]
{
       $context.childExecutionContext->map(e |if($e->instanceOf(MultiExecutionContext) && !$e->in($visited),
                                                              |$e->concatenate( $e->cast(@MultiExecutionContext)->collectionMultiExecutionContexts( $visited->concatenate($e) )),
                                                              |$e)
                                                        )  
}

Class meta::pure::executionPlan::ExecutionOptionContext extends MultiExecutionContext
{
   <<equality.Key>> executionOptions : ExecutionOption[*];
}

// Move to platform


Class meta::pure::executionPlan::PlanVarPlaceHolder extends meta::pure::executionPlan::FunctionParameter
{
    routingStrategy: meta::pure::router::metamodel::RoutingStrategy[0..1];
}

Class meta::pure::executionPlan::PlanSetPlaceHolder
{
    name : String[1];
    tdsColumns: TDSColumn[*];
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   let routed = $f->routeFunction($context, $extensions, $debugContext);
   meta::pure::executionPlan::executionPlan($routed, $f, ^Mapping(package=meta::pure::executionPlan, name='dummy'), ^meta::core::runtime::Runtime(), $context, $extensions, $debugContext);
}

function meta::pure::executionPlan::executionPlan(f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   let routed = $f->routeFunction($m, $runtime, $context, $extensions, $debugContext);
   meta::pure::executionPlan::executionPlan($routed, $f, $m, $runtime, $context, $extensions, $debugContext);
}

function meta::pure::executionPlan::executionPlan(routedFunction:FunctionDefinition<Any>[1], f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   let clusters = $routedFunction.expressionSequence->evaluateAndDeactivate()->cast(@ClusteredValueSpecification);

   let openVars = $f->openVariableValues();
   let functionParameters = $f->stubFuncParameters();
   let inScopeVars = $functionParameters->map(ep|pair($ep.name, ^List<Any>(values=$ep)))->newMap()->putAll($openVars);

   meta::pure::executionPlan::executionPlan($clusters, $routedFunction, $f, $m, $runtime, $context, $inScopeVars, $extensions, $debugContext);
}

function meta::pure::executionPlan::executionPlan(clusters:ClusteredValueSpecification[*], routedFunction:FunctionDefinition<Any>[1], f:FunctionDefinition<Any>[1], m:Mapping[1], runtime:meta::core::runtime::Runtime[1], context:meta::pure::runtime::ExecutionContext[1], inScopeVars:Map<String, List<Any>>[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionPlan[1]
{
   let functionParameters = $f->stubFuncParameters();

   print(if($debugContext.debug,|'\nGenerating Execution Plan:\n',|''));
   let node = if ($clusters->size() == 1,
       | print(if($debugContext.debug,|'Generating plan for cluster: '+$clusters->at(0)->meta::pure::router::printer::asString()+'\n',|''));
         $clusters->at(0)->plan($inScopeVars, $context, $extensions, ^$debugContext(space = $debugContext.space+'   '));,
       | let allNodes = $clusters->fold({c,a|let x = $a.vars;
                                             print(if($debugContext.debug,|'Generating plan for cluster: '+$c->meta::pure::router::printer::asString()+'\n',|''));
                                             let node = $c->plan($x, $context, $extensions, ^$debugContext(space = $debugContext.space+'   '));
                                             let nVars = $node->match([a:AllocationExecutionNode[1]|let varValues = if($a.resultType.genericType.rawType->toOne() == TabularDataSet,
                                                                                                                       |^PlanSetPlaceHolder(name=$a.varName,tdsColumns = $a.resultType->match([x:TDSResultType[1]|$x.tdsColumns, a:Any[1]|[]])),
                                                                                                                       |^PlanVarPlaceHolder(name=$a.varName,genericType = $a.resultType.genericType, multiplicity=$a.resultSizeRange));
                                                                                                    $x->put($a.varName, ^List<Any>(values=$varValues));,
                                                                       a:Any[*]|$x]);
                                             ^$a(nodes+=$node, vars = $nVars);
                                        }, ^NodeAndVars(vars = $inScopeVars)).nodes;
         ^SequenceExecutionNode
         (
            resultType = $allNodes->last()->toOne().resultType,
            resultSizeRange = $allNodes->last()->toOne().resultSizeRange,
            executionNodes = $allNodes
         );
   );

   let executionOptionParameters = $context->match([
                                                p: meta::pure::executionPlan::ExecutionOptionContext[1] | $p->meta::pure::executionPlan::stubExecutionOptionParameters($extensions),
                                                c: meta::pure::runtime::ExecutionContext[1] | []
                                             ]);

   let updatedNode = addFunctionParametersValidationNode($node, $f, $functionParameters->concatenate($executionOptionParameters)->checkConflictingParameters(), $routedFunction);

   ^ExecutionPlan
   (
      func = $f,
      mapping = $m,
      runtime = $runtime,
      rootExecutionNode = $updatedNode,
      authDependent = false || $updatedNode->allNodes($extensions).authDependent->contains(true),
      kerberos =  $updatedNode->allNodes($extensions).kerberos->first(),
      processingTemplateFunctions = $updatedNode->allNodes($extensions).supportFunctions->removeDuplicates()
   );
}



function meta::pure::executionPlan::generatePlatformCode(plan:ExecutionPlan[1], platformId:String[1], config:meta::pure::executionPlan::platformBinding::PlatformBindingConfig[1], extensions:meta::pure::extension::Extension[*]):ExecutionPlan[1]
{
   meta::pure::executionPlan::generatePlatformCode($plan, $platformId, $config, $extensions, if(isOptionSet('DebugPlatformCodeGen'), |debug(), |noDebug()));
}

function meta::pure::executionPlan::generatePlatformCode(plan:ExecutionPlan[1], platformId:String[1], config:meta::pure::executionPlan::platformBinding::PlatformBindingConfig[1], extensions:meta::pure::extension::Extension[*], debug:DebugContext[1]):ExecutionPlan[1]
{
   let platformBinding = $extensions->meta::pure::executionPlan::platformBinding::extractPlatformBindingById($platformId);

   $platformBinding.bindPlanToPlatform->eval($plan, $config, $extensions, $debug);
}


function <<access.private>> meta::pure::executionPlan::addFunctionParametersValidationNode(node:ExecutionNode[1], f:FunctionDefinition<Any>[1], planVarPlaceHolders:PlanVarPlaceHolder[*], routedFunction:FunctionDefinition<Any>[1]):ExecutionNode[1]
{
   let collectionEnumParams = $planVarPlaceHolders->filter(p | $p.genericType.rawType->toOne()->instanceOf(Enumeration) && $p.multiplicity == ZeroMany);
   assert($collectionEnumParams->size() == 0, |'Collection of Enums is not supported as service parameter ' + $collectionEnumParams->map(p | $p.name)->makeString('[',', ',']'));
   let enumValidationContext = $planVarPlaceHolders->filter(p | $p.genericType.rawType->toOne()->instanceOf(Enumeration))->map(e | ^meta::pure::executionPlan::EnumValidationContext(varName = $e.name, validEnumValues = $e.genericType.rawType->toOne()->cast(@Enumeration<Any>)->enumValues()->map(e |$e->id())));
   let paramsSupportedForStreamInput = $routedFunction->findParamsSupportedForStreamInput();
   if($planVarPlaceHolders->isNotEmpty(),| let functionParameters = $planVarPlaceHolders->map(p|^FunctionParameter(name=$p.name, supportsStream = $p.name->in($paramsSupportedForStreamInput), multiplicity=$p.multiplicity, genericType=$p.genericType));
                                                                             let functionParametersValidationNode = ^FunctionParametersValidationNode(functionParameters = $functionParameters, resultType=^ResultType(genericType=^GenericType(rawType = Boolean)), parameterValidationContext = $enumValidationContext);
                                                                             $node->match([s:SequenceExecutionNode[1]|^$s(executionNodes=$functionParametersValidationNode->concatenate($s.executionNodes)),
                                                                                           e:ExecutionNode[1]|^SequenceExecutionNode(executionNodes=[$functionParametersValidationNode, $e], resultType=$e.resultType, resultSizeRange=$e.resultSizeRange)]);
                                                                         ,| $node);
}

 // todo todo
function <<access.private>> meta::pure::executionPlan::addFunctionParametersValidationNode(node:ExecutionNode[1], f:FunctionDefinition<Any>[1], planVarPlaceHolders:PlanVarPlaceHolder[*], routedFunction:FunctionDefinition<Any>[1]):ExecutionNode[1]
{
   let collectionEnumParams = $planVarPlaceHolders->filter(p | $p.type->instanceOf(Enumeration) && $p.multiplicity == ZeroMany);
   assert($collectionEnumParams->size() == 0, |'Collection of Enums is not supported as service parameter ' + $collectionEnumParams->map(p | $p.name)->makeString('[',', ',']'));
   let parameterValidationContexts = $planVarPlaceHolders->map(p |
                                             if(
                                               [
                                                 pair(|$p.type->instanceOf(Enumeration), |^meta::pure::executionPlan::EnumValidationContext(varName = $p.name, validEnumValues = $p.type->cast(@Enumeration<Any>)->enumValues()->map(e |$e->id()))),
                                                 pair(|$p.type->instanceOf(Class), |^meta::pure::executionPlan::ProtocolObjectValidationContext<Any>(varName = $p.name, parameterClass = $p.type->cast(@Class<Any>)))
                                               ],
                                               |[]
                                             );
                                       );
   let paramsSupportedForStreamInput = $routedFunction->findParamsSupportedForStreamInput();
   if($planVarPlaceHolders->isNotEmpty(),| let functionParameters = $planVarPlaceHolders->map(p|^FunctionParameter(name=$p.name, supportsStream = $p.name->in($paramsSupportedForStreamInput), multiplicity=$p.multiplicity, type=$p.type));
                                                                             let functionParametersValidationNode = ^FunctionParametersValidationNode(functionParameters = $functionParameters, resultType=^ResultType(type = Boolean), parameterValidationContext = $parameterValidationContexts);
                                                                             $node->match([s:SequenceExecutionNode[1]|^$s(executionNodes=$functionParametersValidationNode->concatenate($s.executionNodes)),
                                                                                           e:ExecutionNode[1]|^SequenceExecutionNode(executionNodes=[$functionParametersValidationNode, $e], resultType=$e.resultType, resultSizeRange=$e.resultSizeRange)]);
                                                                         ,| $node);
}

function <<access.private>> meta::pure::executionPlan::findParamsSupportedForStreamInput(routedFunction:FunctionDefinition<Any>[1]):String[*]
{
   let vars = $routedFunction.expressionSequence->evaluateAndDeactivate()->cast(@ClusteredValueSpecification)->map(cvs| let clusterVal                     = $cvs.val->deepByPassRouterInfo()->cast(@ValueSpecification);
                                                                                                                        let varsSupportedForStreamInput    = $clusterVal->findParamsForStreamInput($cvs->elementPathForCluster(), $cvs->streamSupportedFunctionEvaluator(), true, false);
                                                                                                                        let varsNotSupportedForStreamInput = $clusterVal->findParamsForStreamInput($cvs->elementPathForCluster(), $cvs->streamSupportedFunctionEvaluator(), false, false);
                                                                                                                        pair(^List<Pair<String, String>>(values=$varsSupportedForStreamInput), ^List<Pair<String, String>>(values=$varsNotSupportedForStreamInput)););
   let allVarsSupportedForStreamInput = $vars.first.values;
   let allVarsNotSupportedForStreamInput = $vars.second.values;
   let supportedVarsUsedWithMultipleStores = $allVarsSupportedForStreamInput->groupBy(x | $x.first)->keyValues()->filter(p | $p.second.values.second->distinct()->size() > 1).first;

   $allVarsSupportedForStreamInput.first->filter(p | !$p->in($allVarsNotSupportedForStreamInput.first) && !$p->in($supportedVarsUsedWithMultipleStores));
}

function <<access.private>> meta::pure::executionPlan::findParamsForStreamInput(vs:ValueSpecification[1], storePath:String[1], streamSupportedFunction:Function<{FunctionExpression[1]->Boolean[1]}>[1], getSupportedParams:Boolean[1], isSupportedForStream:Boolean[1]):Pair<String, String>[*]
{
   $vs->match([fe:FunctionExpression[1]           | $fe.parametersValues->map(v | $v->findParamsForStreamInput($storePath, $streamSupportedFunction, $getSupportedParams, $streamSupportedFunction->eval($fe->cast(@FunctionExpression)))),
               i:InstanceValue[1]                 | $i.values->map( v | $v->match([a: ValueSpecification[1] | $a->findParamsForStreamInput($storePath, $streamSupportedFunction, $getSupportedParams, $isSupportedForStream);,
                                                                                   k : KeyExpression[1] | $k.expression->findParamsForStreamInput($storePath, $streamSupportedFunction, $getSupportedParams, $isSupportedForStream);,
                                                                                   l : LambdaFunction<Any>[1] | $l.expressionSequence->map(e | $e->findParamsForStreamInput($storePath, $streamSupportedFunction, $getSupportedParams, $isSupportedForStream));,
                                                                                   t : Type[1] | [];,
                                                                                   a : Any[1] | [];]););,
               ve:VariableExpression[1]           | if(($isSupportedForStream && $getSupportedParams) || (!$isSupportedForStream && !$getSupportedParams), | pair($ve.name, $storePath), |[]);,
               cvs:ClusteredValueSpecification[1] | $cvs.val->findParamsForStreamInput($cvs->elementPathForCluster(), $cvs->streamSupportedFunctionEvaluator(), $getSupportedParams, false),
               vs:ValueSpecification[1]           | [];
             ]
          );
}

function meta::pure::executionPlan::stubFuncParameters(f:FunctionDefinition<Any>[1]):PlanVarPlaceHolder[*]
{
   $f->functionType().parameters->evaluateAndDeactivate()->map(p|^PlanVarPlaceHolder(name=$p.name, genericType = $p.genericType, multiplicity=$p.multiplicity));
}

function meta::pure::executionPlan::plan(cluster:ClusteredValueSpecification[1], inScopeVars:Map<String, List<Any>>[1], context:meta::pure::runtime::ExecutionContext[1], extensions:meta::pure::extension::Extension[*], debugContext:DebugContext[1]):ExecutionNode[1]
{
   $cluster->generateExecutionNodeFromCluster($inScopeVars, $context, $extensions, $debugContext)
}

function meta::pure::executionPlan::nodeFromConnection(c:Connection[1], tree:RootGraphFetchTree<Any>[0..1], enableConstraints:Boolean[1], checked:Boolean[1], extensions:meta::pure::extension::Extension[*], dflt:Function<{Nil[1]->ExecutionNode[1]}>[1..*]):ExecutionNode[1]
{
   $c->match($dflt)->toOne();
}

function meta::pure::executionPlan::isExecutionOptionPresent(execCtx: ExecutionOptionContext[1], execOption: Class<Any>[1]):Boolean[1]
{
   let executionOptionsList = $execCtx->allContextsOfType(ExecutionOptionContext).executionOptions->filter(f| $f->instanceOf($execOption)); 
   assert($executionOptionsList->size() <= 1, |'Execution Option :' + $execOption.name->toOne() + 'has more than one instance specified in the context');
   $executionOptionsList->isNotEmpty();
}

function meta::pure::executionPlan::validateAndReturnExecutionOptionOfType(execCtx: ExecutionOptionContext[1], execOption: Class<Any>[1]):ExecutionOption[1]
{
   let executionOptionsList =   $execCtx->allContextsOfType(ExecutionOptionContext).executionOptions->filter(f| $f->instanceOf($execOption));  
   assert($executionOptionsList->size() == 1, |'Execution Option:' + $execOption.name->toOne() + 'not mentioned or mentioned more than once as part of the context');
   $executionOptionsList->head()->toOne();
}

function meta::pure::executionPlan::stubExecutionOptionParameters(execOptionCtx: ExecutionOptionContext[1], extensions: meta::pure::extension::Extension[*]):PlanVarPlaceHolder[*]
{
   let planVarPlaceHolders = $extensions.executionPlanExtensions()->map(
      sc|$sc.extractVariablesFromExecutionOption->map(f|$execOptionCtx.executionOptions->map(x|$f->eval($x)));
   );
   $planVarPlaceHolders->checkConflictingParameters();
}

function meta::pure::executionPlan::checkConflictingParameters(planVarPlaceHolderList: PlanVarPlaceHolder[*]):PlanVarPlaceHolder[*]
{
   let removeDuplicatesFromList  = $planVarPlaceHolderList->removeDuplicatesBy(y|$y.name);
   assert($removeDuplicatesFromList->size()==$planVarPlaceHolderList->size(), |'Conflicting variable names present in the following list: ' + $planVarPlaceHolderList->map(y|$y.name)->makeString(', '));
   $planVarPlaceHolderList;
}


Profile meta::pure::executionPlan::engine::java::roadmap::feature
{
   stereotypes: [ M2MBasics, DataQuality, Enums, Filter, Project, Advanced, XML, Runtime ];
}
