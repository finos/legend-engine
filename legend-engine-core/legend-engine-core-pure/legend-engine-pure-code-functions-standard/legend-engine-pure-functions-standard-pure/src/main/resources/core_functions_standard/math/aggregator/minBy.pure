// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::functions::variant::convert::*;
import meta::pure::functions::math::mathUtility::*;
import meta::pure::test::pct::*;

function 
<<PCT.function>> 
    {
        doc.doc='Finds the row(s) containing the minimum value for a column and returns the value of another column in that row.'
    } 
meta::pure::functions::math::minBy<T>(col_to_return:T[*], col_containing_minimum:Number[*]):T[0..1]
{
  minBy($col_to_return, $col_containing_minimum, 1)->head();
}

function <<PCT.function>> meta::pure::functions::math::minBy<T>(col_to_return:T[*], col_containing_minimum:Number[*], n:Integer[1]): T[*]
{
  $col_to_return->zip($col_containing_minimum)->sort({x, y | $x.second->compare($y.second)})->slice(0, $n).first;
}


function  meta::pure::functions::math::minBy<T>(colRows: meta::pure::functions::math::mathUtility::RowMapper<T, Number>[*]):T[0..1]
{
  minBy($colRows.rowA, $colRows.rowB);
}

function  meta::pure::functions::math::minBy<T>(colRows: meta::pure::functions::math::mathUtility::RowMapper<T, Number>[*], n:Integer[1]): T[*]
{
  minBy($colRows.rowA, $colRows.rowB, $n);
}

function <<PCT.test>> meta::pure::functions::math::tests::minBy::testMinBy<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
  assertEquals(1, $f->eval(|minBy([1, 2], [10, 20])));
  assertEquals(2020, $f->eval(|minBy([1001, 1020, 1030, 900, 2010, 2020], [10000, 9000, 8000, 15000, 14000, 7000])));
  assertEquals([2020,1030,1020], $f->eval(|minBy([1001, 1020, 1030, 900, 2010, 2020], [10000, 9000, 8000, 15000, 14000, 7000], 3)));
}

function <<PCT.test, PCTRelationQualifier.relation, PCTRelationQualifier.aggregation>> meta::pure::functions::math::tests::minBy::testSimpleGroupByMinBy<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{

   let expr = {
                |#TDS
                  id, grp, name, employeeNumber
                  1, 1, A, 10000
                  2, 1, B, 9000
                  3, 1, C, 8000
                  4, 1, D, 15000
                  5, 1, E, 17000
                  6, 1, F, 8500
                  6, 2, G, 12000
                  6, 2, H, 13000
                  6, 2, I, 8000
                #->groupBy(~[grp], ~[newCol : x | meta::pure::functions::math::mathUtility::rowMapper($x.name, $x.employeeNumber) : y | $y->minBy()]);
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   grp,newCol\n'+
                  '   1,C\n'+
                  '   2,I\n'+
                  '#', $res->sort(~grp->ascending())->toString());
}

function <<PCTRelationQualifier.relation, PCTRelationQualifier.aggregation>> meta::pure::functions::math::tests::minBy::testSimpleGroupByMinByVariant<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{

   let expr = {
                |#TDS
                  id, grp, name, employeeNumber
                  1, 1, A, 10000
                  2, 1, B, 9000
                  3, 1, C, 8000
                  4, 1, D, 15000
                  5, 1, E, 17000
                  6, 1, F, 8500
                  6, 2, G, 12000
                  6, 2, H, 13000
                  6, 2, I, 8000
                #->groupBy(~[grp], ~[newCol : x | meta::pure::functions::math::mathUtility::rowMapper($x.name, $x.employeeNumber) : y | $y->minBy(3)->toVariant()]);
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   grp,newCol\n'+
                  '   1,[C,F,B]\n'+
                  '   2,[I,G,H]\n'+
                  '#', $res->sort(~grp->ascending())->toString());
}
