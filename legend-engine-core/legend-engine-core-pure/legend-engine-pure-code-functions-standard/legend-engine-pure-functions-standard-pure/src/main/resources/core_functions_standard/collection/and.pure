// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::test::pct::*;

function <<PCT.function>> meta::pure::functions::collection::and(vals:Boolean[*]):Boolean[1]
{
   $vals->fold({i,a|$i && $a}, true)
}

function <<PCT.test>> meta::pure::functions::collection::tests::and::testAnd<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
   assertEquals(true, $f->eval(|and(true)));
   assertEquals(true, $f->eval(|and([true, true])));
   assertEquals(false, $f->eval(|and([true, false])));
   assertEquals(false, $f->eval(|and([true, true, false])));
}

function <<PCT.test>> meta::pure::functions::collection::tests::and::testAnd_Relation_Aggregate<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
    let expr = {
                |#TDS
                  id, isManager
                  1, true
                  1, false
                  1, true
                  2, true
                  2, true
                  2, true
                #->groupBy(~[id], ~[newCol : x | $x.isManager : y | $y->and()])
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   id,newCol\n'+
                  '   1,false\n'+
                  '   2,true\n'+
                  '#', $res->sort(~id->ascending())->toString());
}

function <<PCT.test>> meta::pure::functions::collection::tests::and::testAnd_Relation_Window<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
    let expr = {
                |#TDS
                  id, val, isManager
                  1, 1.0, true
                  1, 2.0, false
                  1, 3.0, true
                  2, 2, true
                  2, 4, true
                  2, 6, true
                #->extend(over(~id), ~newCol:{p,w,r|$r.isManager}:y|$y->and())
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   id,val,newCol\n'+
                  '   1,1.0,false\n'+
                  '   1,2.0,false\n'+
                  '   1,3.0,false\n'+
                  '   2,2.0,true\n'+
                  '   2,4.0,true\n'+
                  '   2,6.0,true\n'+
                  '#', $res->sort(~id->ascending())->toString());
}
