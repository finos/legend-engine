// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::test::pct::*;

function <<PCT.function>> meta::pure::functions::math::covarSample(numbersA:Number[*], numbersB:Number[*]):Number[0..1]
{
  covariance($numbersA, $numbersB, min($numbersA->size(),$numbersB->size()), true);
}

function  meta::pure::functions::math::covarSample(covarRows: meta::pure::functions::math::mathUtility::RowMapper[*]):Number[0..1]
{
  covariance($covarRows.rowA, $covarRows.rowB, min($covarRows.rowA->size(),$covarRows.rowB->size()), true);
}

function <<PCT.test>> meta::pure::functions::math::tests::covarSample::testCovarSample<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
  assertEquals([], $f->eval(|covarSample([1], [10])));
  assertEquals(5.0, $f->eval(|covarSample([1, 2], [10, 20])));
  assertEquals(-10.0, $f->eval(|covarSample([1, 2], [30, 10])));
  assertEqWithinTolerance(31.666666666666666666666666666666665, $f->eval(|covarSample([1, 3.0, 6.0], [10, -20, 30]))->toOne(), 0.00000000001);
}

// function meta::pure::functions::math::tests::covarSample::testCovarSampleWithNull<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
// {
//  assertEquals([], $f->eval(|covarSample([1], [])));
//  assertEquals([], $f->eval(|covarSample([[]], [10])));
// }

function meta::pure::functions::math::tests::covarSample::testSimpleGroupByCovarSample<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
    let expr = {
                |#TDS
                  id, valA
                  1, 10
                  2, 10
                  2, 20
                  2, 25
                  2, 30
                  #->extend(~valB:c|if( $c.id == 1 || $c.valA == 25,
                  |[],
                  | if($c.valA == 30,
                      |35,
                      |$c.valA->toOne()+$c.valA->toOne()/10
                  
                  )
                    )
                    )->groupBy(~[id], ~[newCol : x | meta::pure::functions::math::mathUtility::rowMapper($x.valA->cast(@Number), $x.valB->cast(@Number)) : y | $y->covarSample()]);
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   id,newCol\n'+
                  '   1,[]\n'+
                  '   2,120.0\n'+
                  '#', $res->sort(~id->ascending())->toString());
}

function <<PCT.test>> meta::pure::functions::math::tests::covarSample::testSimpleWindowCovarSample<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{
    let expr = {
                |#TDS
                  id, valA, valB
                  1, 1, 10
                  1, 2, 20
                  2, 2, 40
                  2, 4, 15
                #->extend(over(~id), ~newCol:{p,w,r|meta::pure::functions::math::mathUtility::rowMapper($r.valA, $r.valB)}:y|$y->covarSample())
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   id,valA,valB,newCol\n'+
                  '   1,1,10,5.0\n'+
                  '   1,2,20,5.0\n'+
                  '   2,2,40,-25.0\n'+
                  '   2,4,15,-25.0\n'+
                  '#', $res->sort([~id->ascending(), ~valA->ascending()])->toString());
}
