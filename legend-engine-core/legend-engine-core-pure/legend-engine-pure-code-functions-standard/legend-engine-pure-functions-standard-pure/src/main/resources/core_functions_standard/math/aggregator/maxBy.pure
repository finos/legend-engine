// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::functions::variant::convert::*;
import meta::pure::functions::math::mathUtility::*;
import meta::pure::test::pct::*;

function <<PCT.function>> meta::pure::functions::math::maxBy<T>(col_to_return:T[*], col_containing_maximum:Number[*]):T[0..1]
{
  maxBy($col_to_return, $col_containing_maximum, 1)->head();
}


function  meta::pure::functions::math::maxBy<T>(colRows: meta::pure::functions::math::mathUtility::RowMapper<T, Number>[*]):T[0..1]
{
  maxBy($colRows.rowA, $colRows.rowB);
}

function <<PCT.function>> meta::pure::functions::math::maxBy<T>(col_to_return:T[*], col_containing_maximum:Number[*], n:Integer[1]): T[*]
{
  $col_to_return->zip($col_containing_maximum)->sort({x, y | $y.second->compare($x.second)})->slice(0, $n).first;
}

function  meta::pure::functions::math::maxBy<T>(colRows: meta::pure::functions::math::mathUtility::RowMapper<T, Number>[*], n:Integer[1]): T[*]
{
  maxBy($colRows.rowA, $colRows.rowB, $n);
}


function <<PCT.test>> meta::pure::functions::math::tests::maxBy::testMaxBy<Z|y>(f:Function<{Function<{->Z[y]}>[1]->Z[y]}>[1]):Boolean[1]
{
  assertEquals(2, $f->eval(|maxBy([1, 2], [10, 20])));
  assertEquals(900, $f->eval(|maxBy([1001, 1020, 1030, 900, 2010, 2020], [10000, 9000, 8000, 15000, 15000, 8000])));
  assertEquals([900,2010,1001], $f->eval(|maxBy([1001, 1020, 1030, 900, 2010, 2020], [10000, 9000, 8000, 15000, 15000, 8000], 3)));
}

function <<PCT.test, PCTRelationQualifier.relation, PCTRelationQualifier.aggregation>> meta::pure::functions::math::tests::maxBy::testSimpleGroupByMaxBy<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{

   let expr = {
                |#TDS
                  id, grp, name, employeeNumber
                  1, 1, A, 10000
                  2, 1, B, 9000
                  3, 1, C, 8000
                  4, 1, D, 15000
                  5, 1, E, 17000
                  6, 1, F, 8000
                  6, 2, G, 12000
                  6, 2, H, 13000
                  6, 2, I, 8000
                #->groupBy(~[grp], ~[newCol : x | meta::pure::functions::math::mathUtility::rowMapper($x.name, $x.employeeNumber) : y | $y->maxBy()]);
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   grp,newCol\n'+
                  '   1,E\n'+
                  '   2,H\n'+
                  '#', $res->sort(~grp->ascending())->toString());
}

function <<PCTRelationQualifier.relation, PCTRelationQualifier.aggregation>> meta::pure::functions::math::tests::maxBy::testSimpleGroupByMaxByVariant<T|m>(f:Function<{Function<{->T[m]}>[1]->T[m]}>[1]):Boolean[1]
{

   let expr = {
                |#TDS
                  id, grp, name, employeeNumber
                  1, 1, A, 10000
                  2, 1, B, 9000
                  3, 1, C, 8000
                  4, 1, D, 15000
                  5, 1, E, 17000
                  6, 1, F, 8000
                  6, 2, G, 12000
                  6, 2, H, 13000
                  6, 2, I, 8000
                #->groupBy(~[grp], ~[newCol : x | meta::pure::functions::math::mathUtility::rowMapper($x.name, $x.employeeNumber) : y | $y->maxBy(3)->toVariant()]);
               };

    let res =  $f->eval($expr);

    assertEquals( '#TDS\n'+
                  '   grp,newCol\n'+
                  '   1,[E,D,A]\n'+
                  '   2,[H,G,I]\n'+
                  '#', $res->sort(~grp->ascending())->toString());
}
