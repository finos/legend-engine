// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::relational::functions::sqlQueryToString::athena::*;
import meta::relational::functions::sqlQueryToString::default::*;
import meta::relational::functions::sqlQueryToString::*;
import meta::relational::metamodel::operation::*;
import meta::relational::metamodel::relation::*;
import meta::relational::metamodel::*;
import meta::relational::runtime::*;
import meta::pure::extension::*;
import meta::relational::extension::*;

function <<db.ExtensionLoader>> meta::relational::functions::sqlQueryToString::athena::dbExtensionLoaderForAthena():DbExtensionLoader[1]
{
  ^DbExtensionLoader(dbType = DatabaseType.Athena, loader = createDbExtensionForAthena__DbExtension_1_);
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::createDbExtensionForAthena():DbExtension[1]
{
   let reservedWords = defaultReservedWords();
   let literalProcessors = getDefaultLiteralProcessors()->putAll(getLiteralProcessorsForAthena());
   let literalProcessor = {type:Type[1]| $literalProcessors->get(if($type->instanceOf(Enumeration), | Enum, | $type))->toOne()};
   let dynaFuncDispatch = getDynaFunctionToSqlForAthena()->groupBy(d| $d.funcName)->getDynaFunctionDispatcher();

   ^DbExtension(
      isBooleanLiteralSupported = true,
      isDbReservedIdentifier = {str:String[1]| $str->in($reservedWords)},
      literalProcessor = $literalProcessor,
      selectSQLQueryProcessor = processSelectSQLQueryForAthena_SelectSQLQuery_1__SqlGenerationContext_1__Boolean_1__String_1_,
      identifierProcessor = processIdentifierForAthena_String_1__DbConfig_1__String_1_,
      dynaFuncDispatch = $dynaFuncDispatch,
      ddlCommandsTranslator = getDDLCommandsTranslatorForAthena()
   );
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::processIdentifierForAthena(identifier:String[1], dbConfig: DbConfig[1]):String[1]
{
   assert($identifier->matches('[a-zA-Z0-9_"]*'), '[unsupported-api] special chars are not supported in table/column names');
   $identifier->processIdentifierWithQuoteChar('"', $dbConfig);
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::getDDLCommandsTranslatorForAthena(): RelationalDDLCommandsTranslator[1]
{
  ^RelationalDDLCommandsTranslator(
                createSchema = translateCreateSchemaStatementForAthena_CreateSchemaSQL_1__String_MANY_,
                dropSchema =  translateDropSchemaStatementForAthena_DropSchemaSQL_1__String_MANY_,
                createTable =  translateCreateTableStatementForAthena_CreateTableSQL_1__DbConfig_1__String_MANY_,
                dropTable = translateDropTableStatementForAthena_DropTableSQL_1__String_MANY_,
                loadTable =  loadValuesToDbTableForAthena_LoadTableSQL_1__DbConfig_1__String_MANY_
              );
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::translateCreateSchemaStatementForAthena(createSchemaSQL:CreateSchemaSQL[1]) : String[*]
{
   if($createSchemaSQL.schema.name == 'default', |[], |'[Create Schema ' + $createSchemaSQL.schema.name + ';]');
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::translateDropSchemaStatementForAthena(dropSchemaSQL:DropSchemaSQL[1]) : String[*]
{
   [];
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::translateDropTableStatementForAthena(dropTableSQL:DropTableSQL[1]) : String[*]
{
  let t = $dropTableSQL.table;
  'Drop table if exists '+if($t.schema.name == 'default',|'',|$t.schema.name+'.')+$t.name+';';
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::translateCreateTableStatementForAthena(createTableSQL:CreateTableSQL[1], dbConfig:DbConfig[1]) : String[*]
{
  let t = $createTableSQL.table;
  let tableName = if($t.schema.name == 'default',|'',|$t.schema.name+'.')+$t.name;
  let currentTimeKey = now()->toString()->chunk(1)->filter(x | $x->matches('[0-9]'))->makeString();
  let tableKey = $tableName->chunk(1)->filter(x | $x->matches('[a-zA-Z0-9]'))->makeString();
  let tableLocationKey = $tableKey + $currentTimeKey;
  'Create External Table '+ $tableName +
      + '('
      + $t.columns->cast(@meta::relational::metamodel::Column)
         ->map(c | $c.name->processColumnName($dbConfig) + ' ' +  getColumnTypeSqlTextForAthena($c.type))
        ->joinStrings(',')
      +') Location \'s3://aws-athena-query-results-finos/' + $tableLocationKey + '/\';';
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::getColumnTypeSqlTextForAthena(columnType:meta::relational::metamodel::datatype::DataType[1]):String[1]
{
   $columnType->match([
      a : Any[*] | meta::relational::metamodel::datatype::dataTypeToSqlText($columnType)
   ])
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::loadValuesToDbTableForAthena(loadTableSQL:LoadTableSQL[1] , dbConfig: DbConfig[1]) : String[*]
{
  
  'insert into ' + if($loadTableSQL.table.schema.name=='default', |'' ,|$loadTableSQL.table.schema.name + '.') + $loadTableSQL.table.name + ' ('
        + $loadTableSQL.columnsToLoad.name->map(colName | $colName->processColumnName($dbConfig))->joinStrings(',')
        +') values '
        + $loadTableSQL.parsedData.values->map(row | '('
          + $row.values->convertValuesToCsvForAthena($loadTableSQL.columnsToLoad.type)
          + ')')->makeString(', ');
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::convertValuesToCsvForAthena(strs : String[*], types : Any[*]): String[1]
{
  range($types->size())->map(x |
    let literalStr = $strs->at($x)->meta::relational::functions::database::testDataSQLgeneration::convertValuesToCsv($types->at($x));
    $types->at($x)->match([
      d:meta::relational::metamodel::datatype::Date[1] | 'Date ' +  $literalStr,
      t:meta::relational::metamodel::datatype::Timestamp[1] | 'Timestamp ' + $literalStr,
      a:Any[1] | $literalStr
    ]);)->makeString(', ');
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::getLiteralProcessorsForAthena():Map<Type,LiteralProcessor>[1]
{
   newMap([
      pair(StrictDate,     ^LiteralProcessor(format = 'Date(\'%s\')',      transform = {d:StrictDate[1], dbTimeZone:String[0..1] | $d->convertDateToSqlString($dbTimeZone)})),
      pair(DateTime,       ^LiteralProcessor(format = 'Timestamp\'%s\'', transform = {d:DateTime[1], dbTimeZone:String[0..1] | $d->convertDateToSqlString($dbTimeZone)})),
      pair(Date,           ^LiteralProcessor(format = 'Timestamp\'%s\'', transform = {d:Date[1], dbTimeZone:String[0..1] | $d->convertDateToSqlString($dbTimeZone)})),
      pair(Boolean,        ^LiteralProcessor(format = '%s', transform = toString_Any_1__String_1_->literalTransform()))
   ])
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::getDynaFunctionToSqlForAthena(): DynaFunctionToSql[*]
{
  let allStates = allGenerationStates();

  [
    dynaFnToSql('and',                    $allStates,            ^ToSql(format='%s', transform={p:String[*]|$p->makeString(' and ')})),
    dynaFnToSql('count',                  $allStates,            ^ToSql(format='count(%s)', transform={p:String[*]|if($p->isEmpty(),|'*',|$p)})),
    dynaFnToSql('equal',                  $allStates,            ^ToSql(format='%s = %s')),
    dynaFnToSql('greaterThan',            $allStates,            ^ToSql(format='%s > %s')),
    dynaFnToSql('greaterThanEqual',       $allStates,            ^ToSql(format='%s >= %s')),
    dynaFnToSql('if',                     $allStates,            ^ToSql(format='case when %s then %s else %s end', parametersWithinWhenClause = [true, false, false])),
    dynaFnToSql('in',                     $allStates,            ^ToSql(format='%s in %s', transform={p:String[2] | if($p->at(1)->startsWith('(') && $p->at(1)->endsWith(')'), | $p, | [$p->at(0), ('(' + $p->at(1) + ')')])})),
    dynaFnToSql('isEmpty',                $allStates,            ^ToSql(format='%s is null')),
    dynaFnToSql('isNotEmpty',             $allStates,            ^ToSql(format='%s is not null')),
    dynaFnToSql('isNotNull',              $allStates,            ^ToSql(format='%s is not null')),
    dynaFnToSql('isNull',                 $allStates,            ^ToSql(format='%s is null')),
    dynaFnToSql('lessThan',               $allStates,            ^ToSql(format='%s < %s')),
    dynaFnToSql('lessThanEqual',          $allStates,            ^ToSql(format='%s <= %s')),
    dynaFnToSql('notEqual',               $allStates,            ^ToSql(format='%s != %s')),
    dynaFnToSql('or',                     $allStates,            ^ToSql(format='%s', transform={p:String[*]|$p->makeString(' or ')})),
    dynaFnToSql('sqlNull',                $allStates,            ^ToSql(format='null'))
  ];
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::processSelectSQLQueryForAthena(s:SelectSQLQuery[1], sgc:SqlGenerationContext[1], isSubSelect:Boolean[1]):String[1]
{
   $s->processSelectSQLQueryForAthena($sgc.dbConfig, $sgc.format, $sgc.config, $isSubSelect, $sgc.extensions);
}

function <<access.private>> meta::relational::functions::sqlQueryToString::athena::processSelectSQLQueryForAthena(s:SelectSQLQuery[1], dbConfig : DbConfig[1], format:Format[1], config:Config[1], isSubSelect : Boolean[1], extensions:Extension[*]):String[1]
{
  let opStr = if($s.filteringOperation->isEmpty(), |'', |$s.filteringOperation->map(s|$s->processOperation($dbConfig, $format->indent(), ^$config(callingFromFilter = true), $extensions))->filter(s|$s != '')->joinStrings(' <||> '));
  let havingStr = if($s.havingOperation->isEmpty(), |'', |$s.havingOperation->map(s|$s->processOperation($dbConfig, $format->indent(), $config, $extensions))->filter(s|$s != '')->joinStrings(' <||> '));

  assert($s.fromRow == [] && $s.toRow == [], '[unsupported-api] slice/limit operations are not implemented');
  assert($s.groupBy == [] && $havingStr == '', '[unsupported-api] groupBy/having operations are not implemented');

  $format.separator + 'select ' + if($s.distinct == true,|'distinct ',|'') +
  processSelectColumns($s.columns, $dbConfig, $format->indent(), true, $extensions) +
  if($s.data == [],|'',| ' ' + $format.separator + 'from ' + $s.data->toOne()->processJoinTreeNode([], $dbConfig, $format->indent(), [], $extensions)) +
  if (eq($opStr, ''), |'', | ' ' + $format.separator + 'where ' + $opStr) +
  if ($s.orderBy->isEmpty(),|'',| ' ' + $format.separator + 'order by '+ $s.orderBy->processOrderBy($dbConfig, $format->indent(), $config, $extensions)->makeString(','));
}
