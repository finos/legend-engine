import meta::pure::runtime::*;
import meta::relational::runtime::*;
import meta::external::function::activator::snowflakeApp::generation::*;
import meta::relational::mapping::*;
import meta::pure::executionPlan::*;

Class meta::external::function::activator::snowflakeApp::SnowFlakeAppExecutionContext extends FunctionActivatorExecutionContext 
{
}

function meta::external::function::activator::snowflakeApp::generation::generateArtifact(s: meta::external::function::activator::snowflakeApp::SnowflakeApp[1]):String[1]
{
  let extensions = meta::external::format::shared::externalFormatExtension()->concatenate(meta::relational::extension::relationalExtensions());
  meta::external::function::activator::snowflakeApp::generation::generateArtifact($s, $extensions);
}

function  meta::external::function::activator::snowflakeApp::generation::generateArtifact(s: meta::external::function::activator::snowflakeApp::SnowflakeApp[1], extensions:meta::pure::extension::Extension[*]):String[1]
{
  let inputParamsStub = generateInputParamsStub($s.function);

  let context = ^meta::external::function::activator::snowflakeApp::SnowFlakeAppExecutionContext(postProcessor = meta::relational::postProcessor::ReplaceFreeMarkerWithTableFunctionParamHolder_SQLQuery_1__PostProcessorResult_1_);
  let contextWithEnumPushDown = $context->meta::pure::executionPlan::featureFlag::addFlagToContext(meta::pure::executionPlan::features::Feature.PUSH_DOWN_ENUM_TRANSFORM);  

  let plan =  meta::pure::executionPlan::executionPlan($s.function->cast(@ConcreteFunctionDefinition<Any>), $contextWithEnumPushDown, $extensions, noDebug());
  let resultStub = generateResultTypeStub($plan.rootExecutionNode.resultType->cast(@TDSResultType), $extensions);
  let generatedQuery = $plan.rootExecutionNode->allNodes($extensions)->filter(n|$n->instanceOf(SQLExecutionNode))->last()->cast(@SQLExecutionNode).sqlQuery->toOne('candidate query not found');
  
  $s.applicationName->toUpper() + $inputParamsStub + ' RETURNS TABLE ('+ $resultStub+ ') LANGUAGE SQL AS $$ '+ $generatedQuery +' $$;';
}

function  meta::external::function::activator::snowflakeApp::generation::generateResultTypeStub(r: TDSResultType[1], extensions:meta::pure::extension::Extension[*]):String[1]
{
  let tdsTypeToRelationalTypeMap = PureTypeToSnowflakeTypeMap();
  $r.tdsColumns->map(c|'"'+ $c.name->toUpper() + '" '+ 
                        if($c.sourceDataType->isNotEmpty(),
                          | $c.sourceDataType->toOne()->cast(@meta::relational::metamodel:;datatype::DataType)->meta:;relational::metamodel::datatype::dataTypeToSqlText(),
                          | $tdsTypeToRelationalTypeMap->get($c.type->toOne('Column type missing for column: '+$c.name))->toOne('Relational type missing for type: '+ $c.type->toOne()->toString())
                          )
                      ) ->joinStrings(',');
}

function  <<access.private>> meta::external::function::activator::snowflakeApp::generation::generateInputParamsStub(f:Function<Any>[1]):String[1]
{  
  let params = $f->functionType().parameters->evaluateAndDeactivate();   
  assertEmpty($params->filter(p | $p.multiplicity != PureOne), 'not implemented, only multiplicity PureOne for snowflake app params is supported: ' + $params->filter(p | $p.multiplicity != PureOne)->map(p|$p.name)->joinStrings(', '));
  let typeMap = PureTypeToSnowflakeTypeMap();
  
  '(' + $params->map(p|'"' + $p.name + '" ' + if($p.genericType.rawType->toOne()->instanceOf(Enumeration), | 'VARCHAR', | $typeMap->get($p.genericType.rawType->toOne())->toOne('Relational type missing for type: ' + $p.genericType.rawType->toOne()->toString()))
)->joinStrings(',') + ')';    
}

function <<access.private>>  meta::external::function::activator::snowflakeApp::generation::PureTypeToSnowflakeTypeMap():Map<PrimitiveType, String>[1]
{
  [
  pair(Integer, 'number'),
  pair(Float, 'FLOAT'),
  pair(Number, 'number'),
  pair(String, 'VARCHAR'),
  pair(Date, 'date'),
  pair(DateTime, 'timestamp'),
  pair(StrictDate, 'date'),
  pair(Boolean, 'boolean')
  ]->newMap()
}
