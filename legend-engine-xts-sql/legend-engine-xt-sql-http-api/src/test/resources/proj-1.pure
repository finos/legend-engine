// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Relational
Database demo::H2DemoDataBase
(
  Table FirmTable
  (
    id INTEGER PRIMARY KEY,
    legal_name VARCHAR(200)
  )
  Table EmployeeTable
  (
    id INTEGER PRIMARY KEY,
    ratings DECIMAL(10,2),
    firm_id INTEGER,
    name VARCHAR(200),
    country_id INTEGER,
    type VARCHAR(25),
    start_date DATE
  )
  Table CountryTable
  (
    id INTEGER PRIMARY KEY,
    country_name VARCHAR(200)
  )

  Join FirmEmployee(EmployeeTable.firm_id = FirmTable.id)
  Join EmployeeCountry(EmployeeTable.country_id = CountryTable.id)
)


###Service
Service demo::H2DemoService
{
  pattern: '/testService';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(
      [
        x|$x.id,
        x|$x.name,
        x|$x.type
      ],
      [
        'Id',
        'Name',
        'Employee Type'
      ]
    );
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

###Service
Service demo::H2DemoServiceRelation
{
  pattern: '/testServiceRelation';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(~[
        Id: x | $x.id,
        Name: x | $x.name,
        'Employee Type': x | $x.type
    ]);
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonService
{
  pattern: '/personService';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(
      [
        x|$x.id,
        x|$x.name,
        x|$x.type,
        x|$x.name + ' Smith',
        x|$x.derivedName
      ],
      [
        'Id',
        'Name',
        'Employee Type',
        'Full Name',
        'Derived Name'
      ]
    );
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonServiceRelation
{
  pattern: '/personServiceRelation';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(~[
        Id: x | $x.id,
        Name: x | $x.name,
        'Employee Type': x | $x.type,
        'Full Name': x | $x.name + ' Smith',
        'Derived Name': x | $x.derivedName
    ]);
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonRatings
{
  pattern: '/personRatings';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(
      [
        x|$x.id,
        x|$x.name,
        x|$x.ratings
      ],
      [
        'Id',
        'Name',
        'Ratings'
      ]
    );
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}


Service demo::H2PersonRatingsRelation
{
  pattern: '/personRatingsRelation';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: |demo::employee.all()->project(~[
        Id: x | $x.id,
        Name: x | $x.name,
        Ratings: x | $x.ratings
    ]);
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonServiceParameterized
{
  pattern: '/personServiceForNames';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: {names:String[*]|demo::employee.all()->filter(p | $names->isEmpty() || $p.name->in($names))->project(
      [
        x|$x.id,
        x|$x.name,
        x|$x.type
      ],
      [
        'Id',
        'Name',
        'Employee Type'
      ]
    )};
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonServiceParameterizedRelation
{
  pattern: '/personServiceForNamesRelation';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: {names:String[*]|demo::employee.all()->filter(p | $names->isEmpty() || $p.name->in($names))->project(
      ~[
        Id: x | $x.id,
        Name: x | $x.name,
        'Employee Type': x | $x.type
      ]
    )};
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonServiceDateParameterized
{
  pattern: '/personServiceForStartDate/{date}';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: {date:Date[1], type:demo::employeeType[0..1] |demo::employee.all()->filter(p | $p.startDate == $date && ($type->isEmpty() || $p.type == $type))->project(
      [
        x|$x.id,
        x|$x.name,
        x|$x.type
      ],
      [
        'Id',
        'Name',
        'Employee Type'
      ]
    )};
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}

Service demo::H2PersonServiceDateParameterizedRelation
{
  pattern: '/personServiceForStartDateRelation/{date}';
  owners:
  [
    'anonymous1',
    'anonymous2'
  ];
  documentation: '';
  autoActivateUpdates: true;
  execution: Single
  {
    query: {date:Date[1], type:demo::employeeType[0..1] |demo::employee.all()->filter(p | $p.startDate == $date && ($type->isEmpty() || $p.type == $type))->project(
      ~[
        Id: x | $x.id,
        Name: x | $x.name,
        'Employee Type': x | $x.type
      ]
    )};
    mapping: demo::DemoRelationalMapping;
    runtime: demo::H2DemoRuntime;
  }
}


###Pure
Enum demo::employeeType
{
  Type1,
  Type2
}

Class demo::employee
{
  id: Integer[1];
  ratings: Decimal[1];
  name: String[1];
  type: demo::employeeType[1];
  startDate: Date[1];
  derivedName() {$this.name+'_derived'} : String[1];
}


###Mapping
Mapping demo::DemoRelationalMapping
(
  *demo::employee: Relational
  {
    ~primaryKey
    (
      [demo::H2DemoDataBase]EmployeeTable.id
    )
    ~mainTable [demo::H2DemoDataBase]EmployeeTable
    id: [demo::H2DemoDataBase]EmployeeTable.id,
    ratings: [demo::H2DemoDataBase]EmployeeTable.ratings,
    name: [demo::H2DemoDataBase]EmployeeTable.name,
    type: EnumerationMapping EmployeeType: [demo::H2DemoDataBase]EmployeeTable.type,
    startDate: [demo::H2DemoDataBase]EmployeeTable.start_date
  }

   demo::employeeType : EnumerationMapping EmployeeType
   {
       Type1: 'Type1',
       Type2: 'Type2'
   }
)


###Connection
RelationalDatabaseConnection demo::H2DemoConnection
{
  store: demo::H2DemoDataBase;
  type: H2;
  specification: LocalH2
  {
    testDataSetupSqls: [
      'Drop table if exists EmployeeTable;\nCreate Table EmployeeTable(id INTEGER PRIMARY KEY,ratings DECIMAL,firm_id INTEGER,name VARCHAR(200),country_id INTEGER, type VARCHAR(25), start_date DATE);\nInsert into EmployeeTable (id, ratings, firm_id, name, country_id, type, start_date) values (101,9.1,202, \'Alice\', 303, \'Type1\', \'2023-08-24\');\nInsert into EmployeeTable (id, ratings, firm_id, name, country_id, type, start_date) values (102,9.2,203, \'Bob\', 304, \'Type2\', \'2022-08-24\');\nInsert into EmployeeTable (id, ratings, firm_id, name, country_id, type, start_date) values (103,9.3,204, \'Curtis\', 305, \'Type2\', \'2022-07-24\');\nInsert into EmployeeTable (id, ratings, firm_id, name, country_id, type, start_date) values (104,9.4,205, \'Danielle\', 306, \'Type1\', \'2022-07-23\');\n'
      ];
  };
  auth: DefaultH2;
}


###Runtime
Runtime demo::H2DemoRuntime
{
  mappings:
  [
    demo::DemoRelationalMapping
  ];
  connections:
  [
    demo::H2DemoDataBase:
    [
      connection_1: demo::H2DemoConnection
    ]
  ];
}
