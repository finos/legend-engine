// Copyright 2024 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::core::runtime::*;
import meta::pure::runtime::*;
import meta::pure::mapping::*;
import meta::external::query::sql::transformation::queryToPure::*;
import meta::relational::metamodel::*;
import meta::pure::extension::*;
import meta::external::query::sql::transformation::utils::*;
import meta::external::query::sql::transformation::compile::utils::*;
import meta::pure::metamodel::serialization::grammar::*;

function meta::external::query::sql::transformation::compile::utils::nullOrSfe(func: meta::pure::metamodel::function::Function<Any>[1], pvs:ValueSpecification[*]):ValueSpecification[1]
{
  nullOrSfe($func, [], $pvs);
}

function meta::external::query::sql::transformation::compile::utils::nullOrSfe(func: meta::pure::metamodel::function::Function<Any>[1], resolvedTypeParameters:GenericType[*], pvs:ValueSpecification[*]):ValueSpecification[1]
{
  nullOrSfe($func, $func->functionReturnType(), $resolvedTypeParameters, $pvs);
}

function meta::external::query::sql::transformation::compile::utils::nullOrSfe(func: meta::pure::metamodel::function::Function<Any>[1], genericType:GenericType[1], resolvedTypeParameters:GenericType[*], pvs:ValueSpecification[*]):ValueSpecification[1]
{
  nullOrSfe($func, $genericType, $resolvedTypeParameters, $func->functionReturnMultiplicity(), $pvs)
}

function meta::external::query::sql::transformation::compile::utils::nullOrSfe(func: meta::pure::metamodel::function::Function<Any>[1], genericType:GenericType[1], resolvedTypeParameters:GenericType[*], multiplicity: Multiplicity[1], pvs:ValueSpecification[*]):ValueSpecification[1]
{
  let zipped = $func->getParameters()->evaluateAndDeactivate()->zip($pvs);
  let null = $zipped->exists(p | $p.first.multiplicity->hasLowerBound() && $p.second->isNull());

  if ($null,
  | let i = iv([], $genericType),
  | sfe($func, $genericType, $resolvedTypeParameters, $multiplicity, $pvs));
}


function meta::external::query::sql::transformation::compile::utils::isNull(v:ValueSpecification[1]):Boolean[1]
{
  $v->evaluateAndDeactivate().multiplicity == PureZero;
}

function meta::external::query::sql::transformation::compile::utils::isZeroOne(v:ValueSpecification[1]):Boolean[1]
{
  $v->evaluateAndDeactivate().multiplicity == ZeroOne;
}

function meta::external::query::sql::transformation::compile::utils::isZeroMany(v:ValueSpecification[1]):Boolean[1]
{
  $v->evaluateAndDeactivate().multiplicity == ZeroMany;
}

function meta::external::query::sql::transformation::compile::utils::sfe(func: meta::pure::metamodel::function::Function<Any>[1]):SimpleFunctionExpression[1]
{
  sfe($func, [], []);
}

function meta::external::query::sql::transformation::compile::utils::sfe(func: meta::pure::metamodel::function::Function<Any>[1], pvs:ValueSpecification[*]):SimpleFunctionExpression[1]
{
  sfe($func, [], $pvs);
}

function meta::external::query::sql::transformation::compile::utils::sfe(func: meta::pure::metamodel::function::Function<Any>[1], resolvedTypeParameters:GenericType[*], pvs:ValueSpecification[*]):SimpleFunctionExpression[1]
{
  sfe($func, $func->functionReturnType(), $resolvedTypeParameters, $pvs);
}

function meta::external::query::sql::transformation::compile::utils::sfe(func: meta::pure::metamodel::function::Function<Any>[1], genericType:GenericType[1], resolvedTypeParameters:GenericType[*], pvs:ValueSpecification[*]):SimpleFunctionExpression[1]
{
  sfe($func, $genericType, $resolvedTypeParameters, $func->functionReturnMultiplicity(), $pvs)
}

function meta::external::query::sql::transformation::compile::utils::sfe(func: meta::pure::metamodel::function::Function<Any>[1], genericType:GenericType[1], resolvedTypeParameters:GenericType[*], multiplicity: Multiplicity[1], pvs:ValueSpecification[*]):SimpleFunctionExpression[1]
{

  let zipped = $func->getParameters()->evaluateAndDeactivate()->zip($pvs);

  let parameters = $zipped->map(p |

    [
      pair($p.first.multiplicity->hasLowerBound() && $p.second->isZeroOne(), | sfe(toOne_T_MANY__T_1_, $p.second->evaluateAndDeactivate().genericType, [], PureOne, $p.second->evaluateAndDeactivate())),
      pair($p.first.multiplicity->hasLowerBound() && $p.second->isZeroMany(), | sfe(toOneMany_T_MANY__T_$1_MANY$_, $p.second->evaluateAndDeactivate().genericType, [], OneMany, $p.second->evaluateAndDeactivate()))
    ]->getValue(true, | $p.second->evaluateAndDeactivate())->eval();
  );

  ^SimpleFunctionExpression(
    func = $func,
    genericType = $genericType,
    resolvedTypeParameters = $resolvedTypeParameters,
    importGroup = system::imports::coreImport,
    multiplicity = $multiplicity,
    parametersValues = $parameters
  );
}

function meta::external::query::sql::transformation::compile::utils::var(name:String[1]):VariableExpression[1]
{
  var($name, [], [])->evaluateAndDeactivate();
}

function meta::external::query::sql::transformation::compile::utils::var(name:String[1], multiplicity:Multiplicity[0..1], genericType:GenericType[0..1]):VariableExpression[1]
{
  ^VariableExpression(genericType = $genericType->orElse(^InferredGenericType()), multiplicity = $multiplicity->orElse(ZeroOne), name=  $name)->evaluateAndDeactivate();
}

function meta::external::query::sql::transformation::compile::utils::createTypedFunction(pairs:Pair<Type, Function<Any>>[*], default:Function<Any>[1], type:Type[0..1], null:Boolean[1], args:ValueSpecification[*]):ValueSpecification[1]
{
  let func = if ($type->isEmpty(), | $default, | $pairs->getValue($type->toOne(), $default));
  if ($null, | nullOrSfe($func, $args), | sfe($func, $args));
}

function meta::external::query::sql::transformation::compile::utils::functionType(name:String[1], type:Type[1], multiplicity:Multiplicity[1], returnType:Type[1], returnMultiplicity:Multiplicity[1]):FunctionType[1]
{
  functionType($name, ^GenericType(rawType = $type), $multiplicity, ^GenericType(rawType = $returnType), $returnMultiplicity);
}

function meta::external::query::sql::transformation::compile::utils::functionType(name:String[1], type:GenericType[1], multiplicity:Multiplicity[1], returnType:GenericType[1], returnMultiplicity:Multiplicity[1]):FunctionType[1]
{
  ^FunctionType(parameters = ^VariableExpression(genericType = $type, name = $name, multiplicity = $multiplicity), returnMultiplicity = $returnMultiplicity, returnType = $returnType);
}

function meta::external::query::sql::transformation::compile::utils::functionType(parameters:VariableExpression[*], returnType:GenericType[1], returnMultiplicity:Multiplicity[1]):FunctionType[1]
{
  ^FunctionType(parameters = $parameters, returnMultiplicity = $returnMultiplicity, returnType = $returnType);
}

function meta::external::query::sql::transformation::compile::utils::lambda(functionType:FunctionType[1], expressionSequence:ValueSpecification[*]):LambdaFunction<Any>[1]
{
  let lambda = newLambdaFunction($functionType);
  ^$lambda(expressionSequence = $expressionSequence->toOneMany());
}

function meta::external::query::sql::transformation::compile::utils::ivIfNotEmpty(a:Any[0..1]):ValueSpecification[0..1]
{
  if ($a->isNotEmpty(), | $a->iv(), | []);
}

function meta::external::query::sql::transformation::compile::utils::iv(value: Any[*]): ValueSpecification[1]
{
  $value->match([
    v:ValueSpecification[1] | $v,
    a:Any[0] | iv([], ^GenericType(rawType=Nil)),
    a:Any[*] | iv($a, $a->genericType());
  ]);
}

function meta::external::query::sql::transformation::compile::utils::iv(value: Any[*], genericType:GenericType[1]): ValueSpecification[1]
{
  let multiplicity = $value->match([
    a:Any[0] | PureZero,
    a:Any[1] | PureOne,
    a:Any[*] |
      let mv = ^MultiplicityValue(value = $a->size());
      ^Multiplicity(lowerBound = $mv, upperBound = $mv);
  ]);



  ^InstanceValue(multiplicity = $multiplicity, genericType = $genericType, values = $value);
}

function meta::external::query::sql::transformation::compile::utils::gt(type:Type[1]): GenericType[1]
{
  ^GenericType(rawType = $type);
}

function meta::external::query::sql::transformation::compile::utils::createLet(name: String[1], value: ValueSpecification[1]):FunctionExpression[1]
{
  sfe(letFunction_String_1__T_m__T_m_, $value->genericType(), [], PureOne, [iv($name), $value]);
}

function meta::external::query::sql::transformation::compile::utils::createCast(expression:ValueSpecification[1], type:Type[1]):ValueSpecification[1]
{
  createCast($expression, ^GenericType(rawType = $type));
}

function meta::external::query::sql::transformation::compile::utils::createCast(expression:ValueSpecification[1], type:GenericType[1]):ValueSpecification[1]
{
  sfe(cast_Any_m__T_1__T_m_, $type, [], $expression->evaluateAndDeactivate().multiplicity, [$expression, ^InstanceValue(genericType = $type, multiplicity = PureOne)])
}


function meta::external::query::sql::transformation::compile::utils::createExtractEnumValue(enum:Enum[1]):FunctionExpression[1]
{
  createExtractEnumValue($enum->type()->cast(@Enumeration<Enum>), $enum->toString());
}


function meta::external::query::sql::transformation::compile::utils::createExtractEnumValue(enumeration:Enumeration<Enum>[1], value:String[1]):FunctionExpression[1]
{
  createExtractEnumValue($enumeration, $value->iv())
}

function meta::external::query::sql::transformation::compile::utils::createExtractEnumValue(enumeration:Enumeration<Enum>[1], value:ValueSpecification[1]):FunctionExpression[1]
{
  sfe(extractEnumValue_Enumeration_1__String_1__T_1_, ^GenericType(rawType = $enumeration), [], [iv($enumeration), $value]);
}

function meta::external::query::sql::transformation::compile::utils::processExtractEnumValue(enum:Enum[1]):FunctionExpression[1]
{
  processExtractEnumValue($enum->type()->cast(@Enumeration<Enum>), $enum->toString());
}


function meta::external::query::sql::transformation::compile::utils::processExtractEnumValue(enumeration:Enumeration<Enum>[1], value:String[1]):FunctionExpression[1]
{
  processExtractEnumValue($enumeration, $value->iv())
}

function meta::external::query::sql::transformation::compile::utils::processExtractEnumValue(enumeration:Enumeration<Enum>[1], value:ValueSpecification[1]):FunctionExpression[1]
{
  sfe(extractEnumValue_Enumeration_1__String_1__T_1_, ^GenericType(rawType = $enumeration), [], [iv($enumeration), $value]);
}

function meta::external::query::sql::transformation::compile::utils::compileViaGrammar(lambda:LambdaFunction<Any>[1]):LambdaFunction<Any>[1]
{
  compileViaGrammar($lambda, false);
}

function meta::external::query::sql::transformation::compile::utils::compileViaGrammar(lambda:LambdaFunction<Any>[1], removeFrom:Boolean[1]):LambdaFunction<Any>[1]
{
  let config = meta::external::query::sql::transformation::queryToPure::dynamicGrammarConfiguration();
  let functionStr = if ($removeFrom, | $lambda->removeFrom(), | $lambda)->printFunctionDefinition($config, ^GContext(space=''));
  let dependenciesAsStr = $lambda
                          ->meta::external::query::sql::transformation::compile::utils::_extractDependencies()
                          ->scanDependencies([], meta::pure::extension::runtime::getExtensions().moduleExtensions->filter(x|$x->instanceOf(PureToSQLSourceModule))->cast(@PureToSQLSourceModule).extraScanDependenciesType)
                          ->removeDuplicates({a,b|$a->elementToPath() == $b->elementToPath()})
                          ->map(x|$x->printPackageableElements($config))->joinStrings('\n');

  let code =  $dependenciesAsStr+
              '\n###Pure\n'+
              'function test::container():Any[*]{\n'+
              '  ' + $functionStr + '\n'+
              '}';

  meta::legend::compile($code)->filter(x|$x.name == 'container__Any_MANY_')->cast(@FunctionDefinition<Any>).expressionSequence->cast(@InstanceValue)->evaluateAndDeactivate().values->toOne()->cast(@LambdaFunction<Any>);
}

function meta::external::query::sql::transformation::compile::utils::removeFrom<T>(lambda:LambdaFunction<T>[1]):LambdaFunction<T>[1]
{
  ^$lambda(expressionSequence=$lambda.expressionSequence->evaluateAndDeactivate()->map(e | $e->removeFrom()));
}

function meta::external::query::sql::transformation::compile::utils::removeFrom(x:ValueSpecification[1]):ValueSpecification[1]
{
  $x->match(
    [
      v:FunctionExpression[1]| if($v.func == meta::pure::mapping::from_T_m__Mapping_1__Runtime_1__T_m_,
                                    |$v.parametersValues->at(0)->evaluateAndDeactivate()->removeFrom(),
                                    |^$v(parametersValues=$v.parametersValues->evaluateAndDeactivate()->map(pv|$pv->removeFrom()))
                               ),
      x:ValueSpecification[1]|$x
    ]
  )
}


// function meta::external::query::sql::transformation::compile::utils::compileViaProtocol(l:LambdaFunction<Any>[1], removeFunctions:String[*], extensions:Extension[*]):LambdaFunction<Any>[1]
// {
//   meta::protocols::pure::vX_X_X::transformation::fromPureGraph::transformLambda($l, $extensions)->removeLambdaTDSFunction($removeFunctions)->compileViaProtocol($extensions);
// }

// function meta::external::query::sql::transformation::compile::utils::compileViaProtocol(l:meta::protocols::pure::vX_X_X::metamodel::m3::function::LambdaFunction[1], extensions:Extension[*]):LambdaFunction<Any>[1]
// {
//   $l->meta::alloy::metadataServer::alloyToJSON()
//     ->meta::external::query::sql::legendCompileVSProtocol([])
//     ->match([
//       l:LambdaFunction<Any>[1] | $l,
//       i:InstanceValue[1] | $i.values->at(0)->cast(@LambdaFunction<Any>)
//     ]);
// }

function <<access.private>> meta::external::query::sql::transformation::compile::utils::removeLambdaTDSFunction(l:meta::protocols::pure::vX_X_X::metamodel::m3::function::LambdaFunction[1], functions:String[*]):meta::protocols::pure::vX_X_X::metamodel::m3::function::LambdaFunction[1]
{
  $l->removeTDSFunction($functions)->cast(@meta::protocols::pure::vX_X_X::metamodel::m3::function::LambdaFunction);
}

function <<access.private>> meta::external::query::sql::transformation::compile::utils::removeTDSFunction(a:meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::ValueSpecification[1], functions:String[*]):meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::ValueSpecification[1]
{
  $a->match([
    l:meta::protocols::pure::vX_X_X::metamodel::m3::function::LambdaFunction[1] | ^$l(body = $l.body->map(b | $b->removeTDSFunction($functions))),
    c:meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::Collection[1] | ^$c(values = $c.values->map(v | $v->removeTDSFunction($functions))),
    f:meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::AppliedFunction[1] |
      if ($f.function->in($functions),
        | $f.parameters->at(0)->removeTDSFunction($functions),
        | ^$f(parameters = $f.parameters->map(p | $p->removeTDSFunction($functions))));,
    v:meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::ValueSpecification[1] | $v;
  ]);
}


function meta::external::query::sql::transformation::compile::utils::_extractDependencies(v:Any[1]):PackageableElement[*]
{
  $v->match([
    z:FunctionDefinition<Any>[1] |if($z->instanceOf(PackageableElement) && $z->elementToPath()->contains('::tests::'),|$z->cast(@PackageableElement),|[])->concatenate($z.expressionSequence->evaluateAndDeactivate()->map(x|$x->_extractDependencies()));,
    x:SimpleFunctionExpression[1]|let res = $x.parametersValues->evaluateAndDeactivate()->map(p|$p->_extractDependencies());
                                  $res->concatenate($x.func->_extractDependencies());,
    i: InstanceValue[1] | $i.values->evaluateAndDeactivate()->map(p|$p->_extractDependencies()),
    x: meta::pure::metamodel::relation::RelationElementAccessor<Any>[1] | $x.sourceElementContainer,
    s: meta::external::query::sql::expression::SQLExpression<Any>[1]|$s.pureFunction->_extractDependencies(),
    a:Any[1] | []
  ]);
}

function meta::external::query::sql::transformation::compile::utils::scanDependencies(v:PackageableElement[*], alreadyFound:PackageableElement[*], extraScanDependenciesType: Function<{Nil[0..1]->PackageableElement[*]}>[*]):PackageableElement[*]
{
  let found = $v->fold(
                        {p,a|$a->concatenate(
                                $p->match(
                                      $extraScanDependenciesType->concatenate(
                                        [
                                        x:Database[1]|$x.includes,
                                        x:Any[1]|[]
                                        ]
                                      )->toOneMany()
                                )
                            )
                        },
                        []
                      )->removeDuplicates();
  let newOnes = $found->removeAll($alreadyFound);
  $v->concatenate($found->concatenate(if($newOnes->isEmpty(),|[],|$newOnes->scanDependencies($alreadyFound->concatenate($newOnes), $extraScanDependenciesType))))->removeDuplicates();
}

function <<access.private>> meta::external::query::sql::transformation::queryToPure::wrapWithFro<T>(item:T[1], mapping:Mapping[0..1], runtime:Runtime[0..1], executionContext:ExecutionContext[0..1], relation:Boolean[1]):T[1]
{
  $item->match([
    l:LambdaFunction<Any>[1] | wrapWithLambdaFrom($l, $mapping, $runtime)->cast(@T),
    v:ValueSpecification[1] | wrapWithFrom($v, $mapping, $runtime, $executionContext, $relation)->cast(@T)
  ])
}

function meta::external::query::sql::transformation::compile::utils::wrapWithLambdaFrom(lambda:LambdaFunction<Any>[1], mapping:Mapping[0..1], runtime:Any[0..1]):LambdaFunction<Any>[1]
{
  if ($mapping->isEmpty() && $runtime->isEmpty(),
    | $lambda,
    | let params = $lambda->iv()->concatenate(if ($mapping->isNotEmpty(), | $mapping->iv(), | []))->concatenate(if ($runtime->isNotEmpty(), | $runtime->iv(), | []));

      let func = [
        pair($mapping->isEmpty() && $runtime->toOne()->instanceOf(PackageableRuntime), from_FunctionDefinition_1__PackageableRuntime_1__T_m_->cast(@FunctionDefinition<Any>)),
        pair($mapping->isEmpty() && $runtime->toOne()->instanceOf(Runtime), from_FunctionDefinition_1__Runtime_1__T_m_->cast(@FunctionDefinition<Any>)),
        pair($mapping->isNotEmpty() && $runtime->toOne()->instanceOf(PackageableRuntime), from_FunctionDefinition_1__Mapping_1__PackageableRuntime_1__T_m_->cast(@FunctionDefinition<Any>)),
        pair($mapping->isNotEmpty() && $runtime->toOne()->instanceOf(Runtime), from_FunctionDefinition_1__Mapping_1__Runtime_1__T_m_->cast(@FunctionDefinition<Any>))
      ]->getValue(true);

      let sfe = sfe($func, $lambda->functionReturnType(), [], PureOne, $params);

      ^$lambda(expressionSequence = $sfe);
  );
}

function meta::external::query::sql::transformation::compile::utils::wrapWithFrom(expression:ValueSpecification[1], mapping:Mapping[0..1], runtime:Runtime[0..1], executionContext:ExecutionContext[0..1], relation:Boolean[1]):ValueSpecification[1]
{
  let mappingOne = if ($mapping->isEmpty(), | emptyMapping(), | $mapping->toOne());

  if ($relation,
      | [
            pair($mapping->isEmpty() && $runtime->isNotEmpty(), |
                sfe(meta::pure::mapping::from_T_m__Runtime_1__T_m_,[$expression, iv($runtime->toOne())])),
            pair($mapping->isNotEmpty() && $runtime->isNotEmpty(), |
                sfe(meta::pure::mapping::from_T_m__Mapping_1__Runtime_1__T_m_, [$expression, iv($mappingOne), iv($runtime->toOne())]))
        ]->getValue(true, | $expression)->eval(),
      | [
            pair($runtime->isNotEmpty() && $executionContext->isNotEmpty(), |
                sfe(meta::pure::mapping::from_TabularDataSet_1__Mapping_1__Runtime_1__ExecutionContext_1__TabularDataSet_1_, [$expression, iv($mappingOne), iv($runtime->toOne()), iv($executionContext)])),
          pair($runtime->isNotEmpty() && $executionContext->isEmpty(), |
                sfe(meta::pure::mapping::from_TabularDataSet_1__Mapping_1__Runtime_1__TabularDataSet_1_, [$expression, iv($mappingOne), iv($runtime->toOne())]))
        ]->getValue(true, | $expression)->eval()
  );
}

function meta::external::query::sql::transformation::compile::utils::emptyMapping():Mapping[1]
{
  ^Mapping(package = meta::external::query::sql::transformation::compile::utils, name = 'emptymapping')
}

function meta::external::query::sql::transformation::compile::utils::fromFunctions():Function<Any>[*]
{
  [
    meta::pure::mapping::from_FunctionDefinition_1__Mapping_1__PackageableRuntime_1__T_m_,
    meta::pure::mapping::from_FunctionDefinition_1__Mapping_1__Runtime_1__T_m_,
    meta::pure::mapping::from_FunctionDefinition_1__PackageableRuntime_1__T_m_,
    meta::pure::mapping::from_FunctionDefinition_1__Runtime_1__T_m_,
    // meta::pure::mapping::from_T_m__DataSpaceExecutionContext_1__T_m_,
    // meta::pure::mapping::from_T_m__DataSpace_1__T_m_,
    meta::pure::mapping::from_T_m__Mapping_1__PackageableRuntime_1__T_m_,
    meta::pure::mapping::from_T_m__Mapping_1__Runtime_1__T_m_,
    meta::pure::mapping::from_T_m__PackageableRuntime_1__T_m_,
    meta::pure::mapping::from_T_m__Runtime_1__T_m_,
    meta::pure::mapping::from_TabularDataSet_1__Mapping_1__PackageableRuntime_1__ExecutionContext_1__TabularDataSet_1_,
    meta::pure::mapping::from_TabularDataSet_1__Mapping_1__PackageableRuntime_1__TabularDataSet_1_,
    meta::pure::mapping::from_TabularDataSet_1__Mapping_1__Runtime_1__ExecutionContext_1__TabularDataSet_1_,
    meta::pure::mapping::from_TabularDataSet_1__Mapping_1__Runtime_1__TabularDataSet_1_
  ]
}