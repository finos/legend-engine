// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Relational
Database meta::external::query::sql::transformation::queryToPure::tests::dynamic::db
(
  Table tb(n Varchar(200), o INT)
)

###Pure
import meta::external::query::sql::metamodel::*;
import meta::external::query::sql::transformation::queryToPure::*;
import meta::pure::metamodel::serialization::grammar::*;
import meta::external::query::sql::transformation::queryToPure::tests::dynamic::*;

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicCSV():Boolean[1]
{
  assertEquals('|#TDS{\na:Integer[0..1],b:Integer[0..1]\n1,2\n2,3\n}#->meta::pure::functions::relation::select(~[a])',
                transform('select a from csv(\'a,b\n1,2\n2,3\')',[]));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicWithNonDataTypeColumn():Boolean[1]
{
  let source = ^DynamicSQLSource(
    type = 'variant',
    func = x:String[1]|1,
    generationFunction = {x:TableFunction[1], s:SqlTransformContext[1]|
      #TDS
        id, payload:meta::pure::metamodel::variant::Variant
        1, "[1,2,3]"
        2, "[4,5,6]"
      #
    }
  );

  let lambda = meta::external::query::sql::transformation::queryToPure::sqlToPure('select id from variant(\'a\')', $source, [], ^Map<String, ValueSpecification>())->cast(@FunctionDefinition<Any>);

  assertEquals('|#TDS{\nid:Integer[0..1],payload:meta::pure::metamodel::variant::Variant[0..1]\n1,"[1,2,3]"\n2,"[4,5,6]"\n}#->meta::pure::functions::relation::select(~[id])',
              $lambda->printFunctionDefinition(meta::external::query::sql::transformation::queryToPure::dynamicGrammarConfiguration(), ^GContext(space=''))
  );
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicVar():Boolean[1]
{
  let rel = #TDS
              a,b
              1,2
              2,3
            #;

  let value = 2;

  assertEquals('|$rel->meta::pure::functions::relation::filter(x: (a:Integer, b:Integer)[1]|$x.b == $value)',
                transform('select * from var(\'rel\') where b = var(\'value\')',[], newMap([
                  pair('rel', $rel->deactivate()),
                  pair('value', $value->deactivate())
                ])));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicTable():Boolean[1]
{
  assertEquals('|#>{meta::external::query::sql::transformation::queryToPure::tests::dynamic::db.tb}#->meta::pure::functions::relation::select(~[n])',
              transform('select n from tb(\'meta::external::query::sql::transformation::queryToPure::tests::dynamic::db.tb\')',[]));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicTableUsingCTE():Boolean[1]
{
  assertEquals('|let tab = #>{meta::external::query::sql::transformation::queryToPure::tests::dynamic::db.tb}#->meta::pure::functions::relation::select(~[n]);\n'+
               ' $tab->meta::pure::functions::relation::filter(x: (n:meta::pure::precisePrimitives::Varchar(200))[1]|$x.n == \'a\');\n',
              transform('with tab as (select n from tb(\'meta::external::query::sql::transformation::queryToPure::tests::dynamic::db.tb\')) select n from tab where n = \'a\'',[]));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicTableInjectedSpec():Boolean[1]
{
  assertEquals('|#>{legend::db.myTb}#->meta::pure::functions::relation::select(~[a])',
                transform('select a from tb(\'legend::db.myTb\')', meta::legend::compile(readFile('/core_external_query_sql/binding/fromPure/tests/dynamic/db.legend')->toOne())));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testDynamicTableInjectedSpecSchemaForm():Boolean[1]
{
  assertEquals('|#>{legend::db.myTb}#->meta::pure::functions::relation::select(~[a])',
                transform('select a from tb__legend_db.myTb', meta::legend::compile(readFile('/core_external_query_sql/binding/fromPure/tests/dynamic/db.legend')->toOne())));

  assertEquals('|#>{legend::db.myTb}#->meta::pure::functions::relation::select(~[a])',
                transform('select a from "tb__legend_db"."myTb"', meta::legend::compile(readFile('/core_external_query_sql/binding/fromPure/tests/dynamic/db.legend')->toOne())));
}

function <<test.Test>> meta::external::query::sql::transformation::queryToPure::tests::dynamic::testStarOnDynamicTable():Boolean[1]
{
  assertEquals('|#>{legend::db.myTb}#',
                transform('select * from tb(\'legend::db.myTb\')', meta::legend::compile(readFile('/core_external_query_sql/binding/fromPure/tests/dynamic/db.legend')->toOne())));
}

function meta::external::query::sql::transformation::queryToPure::tests::dynamic::transform(sql:String[1], extraPacks:PackageableElement[*]):String[1]
{
  transform($sql, $extraPacks, ^Map<String, ValueSpecification>());
}

function meta::external::query::sql::transformation::queryToPure::tests::dynamic::transform(sql:String[1], extraPacks:PackageableElement[*], scopeVariables:Map<String, ValueSpecification>[1]):String[1]
{
  meta::external::query::sql::transformation::queryToPure::sqlToPure($sql, $extraPacks, $scopeVariables)->cast(@FunctionDefinition<Any>)->printFunctionDefinition(meta::external::query::sql::transformation::queryToPure::dynamicGrammarConfiguration(), ^GContext(space=''))
}
