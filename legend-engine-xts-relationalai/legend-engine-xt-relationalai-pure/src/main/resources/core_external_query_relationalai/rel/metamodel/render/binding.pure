import meta::pure::functions::collection::*;
import meta::external::store::rel::metamodel::*;
import meta::external::store::rel::metamodel::render::buffer::*;
import meta::external::store::rel::metamodel::render::binding::*;
import meta::external::store::rel::metamodel::render::expr::*;
import meta::external::store::rel::utils::*;


////////////////////////
// Rendering bindings //
////////////////////////

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], var: Variable[1]): RelQueryBuffer[1] {
  $buffer->append($var.identifier)
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], vars: Variable[*]): RelQueryBuffer[1] {
  $buffer->append($vars->stringify(v: Variable[1] | $v.identifier, ', '))
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], vars: Variables[1]): RelQueryBuffer[1] {
  $buffer->renderBinding($vars.inner)
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], binding: InBinding[1]): RelQueryBuffer[1] {
  $buffer->renderBinding($binding.variable)->append(' in ')->render($binding.in)
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], binding: WhereBinding[1]): RelQueryBuffer[1] {
  $buffer->renderBinding($binding.parents)->append(' where ')->render($binding.where)
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], binding: Binding[1]): RelQueryBuffer[1] {
  $binding->match([
    b: InBinding[1]    | $buffer->renderBinding($b),
    v: Variable[1]     | $buffer->renderBinding($v),
    vs: Variables[1]   | $buffer->renderBinding($vs),
    b: WhereBinding[1] | $buffer->renderBinding($b)
  ])
}

function meta::external::store::rel::metamodel::render::binding::renderBinding(buffer: RelQueryBuffer[1], bindings: Binding[*]): RelQueryBuffer[1] {
  $buffer->append($bindings, {accum: RelQueryBuffer[1], b: Binding[1] | $accum->renderBinding($b)}, ', ')
}
