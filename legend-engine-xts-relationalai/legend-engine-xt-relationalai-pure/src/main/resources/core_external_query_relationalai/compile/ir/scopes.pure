import meta::external::store::rel::compile::ir::typeinfo::*;
import meta::external::store::rel::compile::ir::utils::*;
import meta::pure::router::metamodel::*;
import meta::pure::router::store::metamodel::*;
import meta::external::store::rel::compile::ir::*;
import meta::external::store::rel::mapping::*;
import meta::external::store::rel::trace::*;
import meta::external::store::rel::utils::*;

/////////////////////////////////////////////////////////////////////////////
// Shared implementation for creation of typed and untyped variable scopes //
/////////////////////////////////////////////////////////////////////////////

function <<access.private>> meta::external::store::rel::compile::ir::handleCaseValueSpecToScopes(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  requireType: Boolean[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScope|0..1>[*] {
  let newTrace =
    $trace
      ->stage('handleCaseValueSpecToScopes(ValueSpecification[1],Boolean[1],PExpr[1],Trace[1])')
      ->detail('requireType', $requireType)
      ->detailLazy('parent', $parent->describeLazy());
  $spec->match([
    spec: InstanceValue[1] |
      $spec.values
        ->map(v |
          $v->map(f: ValueSpecification[1] |
            $f->handleCaseValueSpecToScoped($typeInfo, $requireType, $parent, $newTrace);
          )
        ),
    spec: ExtendedRoutedValueSpecification[1] | $spec.value->handleCaseValueSpecToScopes($typeInfo, $requireType, $parent, $newTrace),
    other: ValueSpecification[1] | $other->handleCaseValueSpecToScoped($typeInfo, $requireType, $parent, $newTrace)
  ]);
}

function <<access.private>> meta::external::store::rel::compile::ir::handleCaseValueSpecToScoped(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  requireType: Boolean[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScope|0..1>[1] {
  let newTrace =
    $trace
      ->stage('handleCaseValueSpecToScoped(ValueSpecification[1],Boolean[1],PExpr[0..1],Trace[1])')
      ->detail('requireType', $requireType)
      ->detailLazy('parent', $parent->describeLazy());
  $spec->match([
    {
      spec: InstanceValue[1] |
        $spec.values
          ->first()
          ->okOr(| errorTrace('Expected one value in `InstanceValue`.', $newTrace))
          ->tryCast(@FunctionRoutedValueSpecification, $newTrace)
          ->andThen(func: FunctionRoutedValueSpecification[1] |
            $func->handleCaseValueSpecToScoped($typeInfo, $requireType, $parent, $newTrace)
          )
    },
    {
      spec: FunctionRoutedValueSpecification[1] |
        let funExpr = $spec.originalFunction.expressionSequence;
        $spec.value
          ->tryCast(@InstanceValue, $newTrace)
          ->andThen(value: InstanceValue[1] |
            $value.values
              ->first()
              ->okOr(| errorTrace('Expected one value in `InstanceValue`.', $newTrace))
              ->tryCast(@FunctionDefinition<Any>, $newTrace)
              ->andThen(func: FunctionDefinition<Any>[1] |
                $func->handleCaseFunctionDefToScoped($typeInfo, $requireType, $parent, $newTrace);
              );
          );
    },
    {
      spec: RoutedValueSpecification[1] |
        $spec.value
          ->tryCast(@FunctionRoutedValueSpecification, $newTrace)
          ->andThen(inner: FunctionRoutedValueSpecification[1] |
            $inner->handleCaseValueSpecToScoped($typeInfo, $requireType, $parent, $newTrace)
          )
    }
  ]);
}

/**
  * Case handling actual creation of variable scopes, which can either be typed or untyped.
  */
function <<access.private>> meta::external::store::rel::compile::ir::handleCaseFunctionDefToScoped(
  func: FunctionDefinition<Any>[1],
  typeInfo: TranspileTypeInfo[1],
  requireType: Boolean[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScope|0..1>[1] {
  let newTrace =
    $trace
      ->stage('handleCaseFunctionDefToScoped(FunctionDefinition<Any>[1],Boolean[1],PExpr[0..1],Trace[1])')
      ->detail('requireType', $requireType)
      ->detailLazy('parent', $parent->describeLazy());
  let functionParams = $func->functionType().parameters;

  $func.expressionSequence
    ->evaluateAndDeactivate()
    ->okOr(| errorTrace('Encountered empty function body while evaluating expression.', $newTrace))
    ->andThem(exprs: ValueSpecification[*] |
      if ($exprs->size() != 1,
        | error('More than one expression in function body not supported yet.', $newTrace),
        | $exprs->toOne()->ok()
      )
    )
    ->andThen(spec: ValueSpecification[1] |
      /*
       * If `spec` is a StoreMappingRoutedValueSpecification, then annotate the
       * variable scope introduced with the type given by the RelSetImplementation
       * of the value specification. Otherwise, introduce the variable scope
       * without type annotation.
       *
       * FIXME: In the typed case we only have one variable, whereas in the typed
       * case we can have more than one variable (e.g., in the case of joins).
       */

      $spec
        ->valueSpecToPExpr($typeInfo, $newTrace)
        ->andThen(inner: PExpr[1] |
          $functionParams
            ->evaluateAndDeactivate()
            ->tryCastm(@VariableExpression, $newTrace)
            ->andThem(vars: VariableExpression[*] |
              if (!$requireType,
                {| $inner->scopeUntyped($vars.name, $parent)->ok()},
                {|
                  $spec->match([
                    {
                      spec: StoreMappingRoutedValueSpecification[1] |
                        $spec
                          ->storeMappingRoutedValueSpecToRelSet($newTrace)
                          ->andThen(set: RelSetImplementation[0..1] |
                            if ($set->isEmpty() || $vars->size() != 1,
                              | error('Expected exactly one set.', $newTrace),
                              | $inner->scopeTyped($vars->toOne().name, exprTypeClass($set->toOne()), $parent)->ok()
                            );
                          )
                    },
                    {
                      spec: SimpleFunctionExpression[1] |
                        if ($vars->size() != 1,
                          {|
                            error('Only a single variable is supported.', $newTrace)
                          },
                          {|
                            let var = $vars->toOne();
                            let type = $var.genericType.rawType->toOne();
                            $typeInfo
                              ->exprType($type)
                              ->okOr(| errorTrace(format('Could not determine type for `%s`.', $type->toString()), $newTrace) )
                              ->then(exprType: PExprType[1] |
                                $inner->scopeTyped($var.name, $exprType, $parent);
                              );
                          })
                    },
                    {
                      spec: ValueSpecification[1] |
                        error('Missing required type information; unsupported value specification type. Available type info: ' + $typeInfo->describe(), $newTrace);
                    }
                  ])
                }
              )
            )
        )
    );
}

//////////////////////////////////////////////////
// Handling creation of untyped variable scopes //
//////////////////////////////////////////////////

function meta::external::store::rel::compile::ir::valueSpecToUntypedScopes(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScope|0..1>[*] {
  let newTrace =
    $trace
      ->stage('valueSpecToUntypedScopes(ValueSpecification[1],Trace[1])')
      ->detailLazy('parent', $parent->describeLazy());
  handleCaseValueSpecToScopes($spec, $typeInfo, false, $parent, $trace);
}

function meta::external::store::rel::compile::ir::valueSpecToUntypedScoped(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  trace: Trace[1]
): Result<PExprScope|0..1>[1] {
  let newTrace =
    $trace->stage('valueSpecToUntypedScoped(ValueSpecification[1],Trace[1])');
  handleCaseValueSpecToScoped($spec, $typeInfo, false, [], $trace);
}

function meta::external::store::rel::compile::ir::valueSpecToUntypedScoped(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScope|0..1>[1] {
  let newTrace =
    $trace
      ->stage('valueSpecToUntypedScoped(ValueSpecification[1],Trace[1])')
      ->detailLazy('parent', $parent->describeLazy());
  handleCaseValueSpecToScoped($spec, $typeInfo, false, $parent, $trace);
}

function meta::external::store::rel::compile::ir::functionDefToUntypedScoped(
  func: FunctionDefinition<Any>[1],
  typeInfo: TranspileTypeInfo[1],
  parent: PExpr[1],
  trace: Trace[1]
): Result<PExprScope|0..1>[1] {
  let newTrace =
    $trace
      ->stage('functionDefToUntypedScoped(ValueSpecification[1],Trace[1])')
      ->detailLazy('parent', $parent->describeLazy());
  handleCaseFunctionDefToScoped($func, $typeInfo, false, $parent, $trace);
}

////////////////////////////////////////////////
// Handling creation of typed variable scopes //
////////////////////////////////////////////////

function meta::external::store::rel::compile::ir::valueSpecToTypedScopes(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  trace: Trace[1]
): Result<PExprScopeTyped|0..1>[*] {
  let newTrace =
    $trace->stage('valueSpecToTypedScopes(ValueSpecification[1],Trace[1])');
  $spec->valueSpecToTypedScopes($typeInfo, [], $newTrace);
}

function meta::external::store::rel::compile::ir::valueSpecToTypedScopes(
  spec: ValueSpecification[1],
  typeInfo: TranspileTypeInfo[1],
  parent: PExpr[0..1],
  trace: Trace[1]
): Result<PExprScopeTyped|0..1>[*] {
  /*
   * To try to avoid repetition, instead of unpacking the value specification in a
   * similar way in order to obtain a typed variable, we instead attempt to cast at
   * the end. We prefer to construct typed variables where possible, and this cast
   * will succeed if a typed variable has been constructed.
   */
  let newTrace =
    $trace
      ->stage('valueSpecToTypedScopes(ValueSpecification[1],PExpr[1],Trace[1])')
      ->detailLazy('parent', $parent->describeLazy());
  handleCaseValueSpecToScopes($spec, $typeInfo, true, $parent, $newTrace)
    ->map(result: Result<PExprScope|0..1>[1] |
      $result->tryCast(@PExprScopeTyped, $newTrace);
    );
}
