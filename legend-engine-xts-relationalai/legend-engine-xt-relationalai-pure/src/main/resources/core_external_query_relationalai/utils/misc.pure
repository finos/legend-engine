import meta::external::store::rel::utils::*;
import meta::external::store::rel::utils::collection::*;
import meta::external::store::rel::utils::lang::*;

function meta::external::store::rel::utils::stringify<T>(
  list: T[*],
  elem: FunctionDefinition<{T[1]->String[1]}>[1],
  separator: String[1]
): String[1] {
  if ($list->size() == 0, | '', |
    $list->init()->fold({e, accum |
      $accum + $elem->eval($e) + $separator
    }, '') + $elem->eval($list->last()->toOne())
  );
}

function meta::external::store::rel::utils::type(spec: ValueSpecification[1]): Type[1] {
  $spec.genericType.rawType->toOne();
}

function meta::external::store::rel::utils::inspect<T|m>(any: T[m]): T[m] {
  println($any);
  $any;
}

function meta::external::store::rel::utils::inspect<T|m>(any: T[m], label: String[1]): T[m] {
  print($label + ': ');
  println($any);
  $any;
}

function meta::external::store::rel::utils::inspect<T|m>(any: T[m], fun: FunctionDefinition<{T[m]->Any[*]}>[1]): T[m] {
  println($fun->eval($any));
  $any;
}

function meta::external::store::rel::utils::inspect<T|m>(any: T[m], fun: FunctionDefinition<{T[m]->Any[*]}>[1], label: String[1]): T[m] {
  print($label + ': ');
  println($fun->eval($any));
  $any;
}

function meta::external::store::rel::utils::debugInspect<T|m>(any: T[m], ctx: DebugContext[1]): T[m] {
  $ctx->debug($any, 'DEBUG: ');
  $any;
}

function meta::external::store::rel::utils::debug(ctx: DebugContext[1], message: Any[*], label: String[1]): Any[*] {
  $ctx->debug(| $message, $label);
}

function meta::external::store::rel::utils::debug(ctx: DebugContext[1], message: Any[*]): Any[*] {
  $ctx->debug($message, '');
}

function meta::external::store::rel::utils::debug(ctx: DebugContext[1], message: FunctionDefinition<{->Any[*]}>[1]): Any[*] {
  $ctx->debug($message, '');
}

function meta::external::store::rel::utils::debug(ctx: DebugContext[1], message: FunctionDefinition<{->Any[*]}>[1], label: String[1]): Any[*] {
  if($ctx.debug, | print($label); $message->eval()->println(); $message;, | '');
}

function meta::external::store::rel::utils::quote(str: String[1]): String[1] {
  '`' + $str + '`';
}

function meta::external::store::rel::utils::escapeString(string: String[1], character: String[1]): String[1] {
  $string->replace($character, format('\\%s', $character));
}

function meta::external::store::rel::utils::indent(amount: Integer[1]): String[1] {
  range($amount)->map(_n | '  ')->joinStrings('');
}

/**
  * Concatenate the given messages, separating by newlines, and indenting all nested newlines by two spaces.
  */
function meta::external::store::rel::utils::concatenateMessages(messages: String[*]): String[1] {
  let indent = indent(1);
  $messages
    ->map(s: String[1] | $s->replace('\n', format('\n%s', $indent)))
    ->makeString('', '\n', '');
}

/**
  * Concatenate the given messages, vertically formatting as a collection, and indenting all nested newlines by two spaces.
  */
function meta::external::store::rel::utils::concatenateCollection(collection: String[*], suppressNewlines: Boolean[1]): String[1] {
  let indent = indent(1);
  let indentInner =
    $collection
      ->map(s: String[1] | $s->replace('\n', format('\n%s', $indent)));
  if ( $suppressNewlines,
     | $indentInner->makeString('[\n', ',', '\n]'),
     | $indentInner->makeString(format('[\n%s', $indent), format(',\n%s', $indent), '\n]')
     );
}

function meta::external::store::rel::utils::concatenateCollection(collection: String[*]): String[1] {
  concatenateCollection($collection, false);
}