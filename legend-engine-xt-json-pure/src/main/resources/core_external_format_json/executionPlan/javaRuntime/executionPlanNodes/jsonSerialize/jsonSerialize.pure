// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.


import meta::pure::executionPlan::*;
import meta::pure::executionPlan::engine::*;
import meta::pure::executionPlan::engine::java::*;
import meta::pure::executionPlan::engine::java::naming::*;
import meta::pure::executionPlan::engine::java::platform::*;
import meta::pure::executionPlan::engine::java::serialize::*;
import meta::pure::executionPlan::engine::java::typeInfo::*;

import meta::pure::graphFetch::*;
import meta::pure::mapping::modelToModel::*;
import meta::pure::router::extension::*;

import meta::external::language::java::factory::*;
import meta::external::language::java::factory::project::*;
import meta::external::language::java::transform::*;

import meta::external::shared::format::binding::*;
import meta::external::shared::format::binding::validation::*;
import meta::external::shared::format::executionPlan::*;

import meta::external::format::json::binding::validation::*;
import meta::external::format::json::executionPlan::*;
import meta::external::format::json::executionPlan::engine::java::jsonSerialize::helperFunctions::*;

// ========================================================================================================================================================
// prepare
// ========================================================================================================================================================

function meta::external::format::json::executionPlan::engine::java::jsonSerialize::prepareForJsonSerialize(node:JsonSerializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GenerationContext[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') prepare JsonSerializeExecutionNode\n', |''));
   
   let bindingDetail = bindDetails($node.binding->cast(@Binding));
   assert($bindingDetail->instanceOf(JsonSchemaBindingDetail), | 'Detailed binding failed: ' + $bindingDetail->cast(@FailedBindingDetail).errorMessages->joinStrings('\n'));
   
   let classIn      = $node.executionNodes.resultType->match([
                         c:ClassResultType[1] | $c.type->cast(@Class<Any>)
                      ]);
   
   let tree         = generateRootTreeFromBindingDetail($classIn, $bindingDetail->cast(@JsonSchemaBindingDetail));
   let newTypeInfos = $context.typeInfos->addForGraphFetchTree($tree);
   let childInfo    = $context->childNodeInfosForPath($path);
   
   let data = ^SerializeData(
      tree           = $tree,
      auxiliaryTrees = $childInfo.graphFetchTrees
   );
   
   let nodeInfo = ^NodeInfo(
      path            = $path, 
      returnType      = ^SimpleJavaType(pureType=String, javaType=javaString()),
      data            = $data,
      graphFetchTrees = $data.tree->concatenate($data.auxiliaryTrees)
   );
   
   $context
      ->setTypeInfos($newTypeInfos)
      ->addNodeInfo($nodeInfo);
}


// ========================================================================================================================================================
// generate
// ========================================================================================================================================================

function meta::external::format::json::executionPlan::engine::java::jsonSerialize::generateForJsonSerialize(node:JsonSerializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GeneratedCode[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') generate JsonSerializeExecutionNode\n', |''));

   let serializeClass = $context.conventions->planNodeClass('public', $path, 'Serialize');

   let project = mergeProjects([
      generateInterfacesAndEnums($context, $debug->indent()),
      createSerializeProject($path, $context, $debug)
   ]);
   generatedCode($project, $serializeClass);
}

// ========================================================================================================================================================
// helper functions
// ========================================================================================================================================================

function meta::external::format::json::executionPlan::engine::java::jsonSerialize::helperFunctions::generateRootTreeFromBindingDetail(classIn:Class<Any>[1], detail:JsonSchemaBindingDetail[1]): RootGraphFetchTree<Any>[1]
{
   let propertiesInScope = $detail.mappedPropertiesForClass($classIn);
   let subTrees          = $propertiesInScope->map(p | $p->generatePropertyTreeForProperty($detail));
   
   ^RootGraphFetchTree<Any>(class=$classIn, subTrees = $subTrees);
}

function <<access.private>> meta::external::format::json::executionPlan::engine::java::jsonSerialize::helperFunctions::generatePropertyTreeForProperty(property:AbstractProperty<Any>[1], detail:JsonSchemaBindingDetail[1]): PropertyGraphFetchTree[1]
{
   let returnType           = $property->functionReturnType().rawType->toOne();
   let isPropertyPrimitive  = $returnType->instanceOf(PrimitiveType) || $returnType->instanceOf(Enumeration) ;
   
   let subPropertiesInScope = if($isPropertyPrimitive, | [], | $detail.mappedPropertiesForClass($returnType->cast(@Class<Any>)));
   let subTrees             = $subPropertiesInScope->map(p | $p->generatePropertyTreeForProperty($detail));

   ^PropertyGraphFetchTree(property=$property, subTrees = $subTrees);
}