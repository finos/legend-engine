###Relational
Database egress::alloy::memsqlTest::stores::DemoDb
(
  Schema LEGEND_GOVERNANCE
  (
    Table BUSINESS_OBJECTS
    (
      BUSINESS_OBJECT_UDTF_NAME VARCHAR(16777216),
      APP_NAME VARCHAR(16777216) PRIMARY KEY,
      ALLOY_SERVICE_NAME VARCHAR(16777216),
      VERSION_NUMBER VARCHAR(16777216),
      OWNER VARCHAR(16777216),
      CREATE_DATETIME TIMESTAMP,
      MODIFIED_DATETIME TIMESTAMP,
      DESCRIPTION VARCHAR(16777216),
      TO_BE_UPDATED VARCHAR(16777216),
      SQL_FRAGMENT VARCHAR(16777216)
    )
  )
)

###MemSql
MemSqlFunction egress::alloy::memsqlTest::activators::DemoMemSqlFunction
{
  functionName: 'demoFunction1';
  function: egress::alloy::memsqlTest::functions::NativeApp_QueryFunction():TabularDataSet[1];
  ownership : Deployment { identifier: 'id1' };
  activationConfiguration: egress::alloy::memsqlTest::connection::MemsqlConnection;
}

MemSqlFunction egress::alloy::memsqlTest::activators::DemoMemSqlFunctionWithParam
{
  functionName: 'demoFunction2';
  function: egress::alloy::memsqlTest::functions::NativeApp_QueryFunction(Integer[1],String[1]):TabularDataSet[1];
  ownership : Deployment { identifier: 'id1' };
  activationConfiguration: egress::alloy::memsqlTest::connection::MemsqlConnection;
}

###Pure
Class egress::alloy::memsqlTest::models::NativeApp
{
  AppName: String[1];
  Doc: String[1];
  Query: String[1];
  Owner: String[1];
  Version: String[1];
}

function egress::alloy::memsqlTest::functions::NativeApp_QueryFunction(): meta::pure::tds::TabularDataSet[1]
{
  egress::alloy::memsqlTest::models::NativeApp.all()->project(
    [
      x|$x.AppName,
      x|$x.Query,
      x|$x.Owner,
      x|$x.Version,
      x|$x.Doc
    ],
    [
      'App Name',
      'Query',
      'Owner',
      'Version',
      'Doc'
    ]
  )->from(
    egress::alloy::memsqlTest::mapping::DemoMapping,
    egress::alloy::memsqlTest::runtime::memsqlRunTime
  )
}

function egress::alloy::memsqlTest::functions::NativeApp_QueryFunction(nameLength: Integer[1], nameStart: String[1]): meta::pure::tds::TabularDataSet[1]
{
  egress::alloy::memsqlTest::models::NativeApp.all()->filter(
    x|($x.AppName->length() >
      $nameLength) &&
      $x.AppName->startsWith(
      $nameStart
    )
  )->project(
    [
      x|$x.AppName,
      x|$x.Query,
      x|$x.Owner,
      x|$x.Version,
      x|$x.Doc
    ],
    [
      'App Name',
      'Query',
      'Owner',
      'Version',
      'Doc'
    ]
  )->from(
    egress::alloy::memsqlTest::mapping::DemoMapping,
    egress::alloy::memsqlTest::runtime::memsqlRunTime
  )
}

###Mapping
Mapping egress::alloy::memsqlTest::mapping::DemoMapping
(
  *egress::alloy::memsqlTest::models::NativeApp: Relational
  {
    ~primaryKey
    (
      [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.APP_NAME
    )
    ~mainTable [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS
    AppName: [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.APP_NAME,
    Doc: [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.DESCRIPTION,
    Query: [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.SQL_FRAGMENT,
    Owner: [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.OWNER,
    Version: [egress::alloy::memsqlTest::stores::DemoDb]LEGEND_GOVERNANCE.BUSINESS_OBJECTS.VERSION_NUMBER
  }
)

###Connection
RelationalDatabaseConnection egress::alloy::memsqlTest::connection::MemsqlConnection
{
  store: egress::alloy::memsqlTest::stores::DemoDb;
  type: MemSQL;
  specification: Static
  {
    name: 'pwm_quantum_uat';
    host: 'dev.memquantumd005a.mag.memsql.url.gs.com';
    port: 3306;
  };
  auth: MiddleTierUserNamePassword
  {
    vaultReference: '81d02fef888a4407a987c339a603b90b';
  };
}

###Runtime
Runtime egress::alloy::memsqlTest::runtime::memsqlRunTime
{
  mappings:
  [
    egress::alloy::memsqlTest::mapping::DemoMapping
  ];
  connections:
  [
    egress::alloy::memsqlTest::stores::DemoDb:
    [
      connection_1: egress::alloy::memsqlTest::connection::MemsqlConnection
    ]
  ];
}