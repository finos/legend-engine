// Copyright 2022 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::analytics::mapping::modelCoverage::*;
import meta::pure::mapping::*;

function meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(mapping: Mapping[1]): MappedEntity[*]
{
  meta::analytics::mapping::modelCoverage::doModelCoverageAnalytics($mapping);
}

// Relational
function <<meta::pure::profiles::test.Test>> meta::analytics::mapping::modelCoverage::test::testSimpleRelationalMappingCoverage():Boolean[1]
{
  let modelCoverageAnalytics = meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(meta::analytics::mapping::modelCoverage::test::sampleRelationalMapping);
  let mappedEntityForFirm = $modelCoverageAnalytics->filter(mp|$mp.path == 'meta::analytics::mapping::modelCoverage::test::Firm');
  assert($mappedEntityForFirm.properties->size() == 3);
  // qualified property
  assertContains($mappedEntityForFirm.properties.name, 'employeeSize');
  // embedded relational mapping
  assertContains($mappedEntityForFirm.properties.name, 'employees');
  assertContains($modelCoverageAnalytics.path, $mappedEntityForFirm.properties->filter(p|$p.name == 'employees')->toOne()->cast(@EntityMappedProperty).entityPath);
}

function <<meta::pure::profiles::test.Test>> meta::analytics::mapping::modelCoverage::test::testSimpleRelationalInheritanceMappingCoverage():Boolean[1]
{
  let modelCoverageAnalytics = meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(meta::analytics::mapping::modelCoverage::test::sampleRelationalMapping);
  let mappedEntityForLegalEntity = $modelCoverageAnalytics->filter(mp|$mp.path == 'meta::analytics::mapping::modelCoverage::test::LegalEntity');
  assert($mappedEntityForLegalEntity.properties->size() == 3);
  assertContains($mappedEntityForLegalEntity.properties->filter(p|$p->instanceOf(EntityMappedProperty))->cast(@EntityMappedProperty).entityPath, '@meta::analytics::mapping::modelCoverage::test::Firm');
  assertContains($mappedEntityForLegalEntity.properties->filter(p|$p->instanceOf(EntityMappedProperty))->cast(@EntityMappedProperty).entityPath, 'meta_analytics_mapping_modelCoverage_test_Firm_employees');
}

function <<meta::pure::profiles::test.Test>> meta::analytics::mapping::modelCoverage::test::testSimpleRelationalUnionMappingCoverage():Boolean[1]
{
  let modelCoverageAnalytics = meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(meta::analytics::mapping::modelCoverage::test::sampleRelationalMapping);
  let mappedEntityForStreet = $modelCoverageAnalytics->filter(mp|$mp.path == 'meta::analytics::mapping::modelCoverage::test::Street');
  assert($mappedEntityForStreet.properties->size() == 1);
  assertContains($mappedEntityForStreet.properties.name, 'zipcode');
}

// M2M
function <<meta::pure::profiles::test.Test>> meta::analytics::mapping::modelCoverage::test::testSimpleM2MMappingCoverage():Boolean[1]
{
  let modelCoverageAnalytics = meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(meta::analytics::mapping::modelCoverage::test::sampleModelToModelMapping);
  let mappedEntityForTargetFirm = $modelCoverageAnalytics->filter(mp|$mp.path == 'meta::analytics::mapping::modelCoverage::test::_Firm');
  assert($mappedEntityForTargetFirm.properties->size() == 1);
  assertContains($mappedEntityForTargetFirm.properties.name, 'employees');
}

function <<meta::pure::profiles::test.Test>> meta::analytics::mapping::modelCoverage::test::testSimpleUnionM2MMappingCoverage():Boolean[1]
{
  let modelCoverageAnalytics = meta::analytics::mapping::modelCoverage::test::generateModelCoverageAnalytics(meta::analytics::mapping::modelCoverage::test::sampleModelToModelMapping);
  let mappedEntityForTargetStreet = $modelCoverageAnalytics->filter(mp|$mp.path == 'meta::analytics::mapping::modelCoverage::test::_Street');
  assert($mappedEntityForTargetStreet.properties->size() == 1);
  assertContains($mappedEntityForTargetStreet.properties.name, 'streetName');
}

###Relational
Database meta::analytics::mapping::modelCoverage::test::sampleDB
(
  Table FirmTable
  (
    id INTEGER PRIMARY KEY,
    legalName VARCHAR(200)
  )
  Table PersonTable
  (
    id INTEGER PRIMARY KEY,
    firmID INTEGER,
    firstName VARCHAR(200),
    lastName VARCHAR(200)
  )
  Table AddressTable
  (
    id INTEGER PRIMARY KEY,
    firmID INTEGER,
    name VARCHAR(200)
  )

  Join firm_person(PersonTable.firmID = FirmTable.id)
  Join firm_address(AddressTable.firmID = FirmTable.id)
)


###Pure
Enum meta::analytics::mapping::modelCoverage::test::IncType
{
  Corp,
  LLC
}

Class meta::analytics::mapping::modelCoverage::test::LegalEntity
{
  legalName: String[1];
  address: meta::analytics::mapping::modelCoverage::test::Address[1];
}

Class meta::analytics::mapping::modelCoverage::test::Firm extends meta::analytics::mapping::modelCoverage::test::LegalEntity
{
  employees: meta::analytics::mapping::modelCoverage::test::Person[1..*];
  incType: meta::analytics::mapping::modelCoverage::test::IncType[1];
  employeeSize() {$this.employees->count()}: Number[1];
}

Class meta::analytics::mapping::modelCoverage::test::Person
{
  firstName: String[1];
  lastName: String[1];
}

Class meta::analytics::mapping::modelCoverage::test::Address
{
  zipcode: Integer[1];
}

Class meta::analytics::mapping::modelCoverage::test::Street extends meta::analytics::mapping::modelCoverage::test::Address
{
  streetName: String[1];
}

Class meta::analytics::mapping::modelCoverage::test::_Firm
{
  employees: meta::analytics::mapping::modelCoverage::test::_Person[1..*];
  type: meta::analytics::mapping::modelCoverage::test::IncType[1];
}

Class meta::analytics::mapping::modelCoverage::test::_Person
{
  fullName: String[1];
}

Class meta::analytics::mapping::modelCoverage::test::_Street
{
  streetName: String[1];
}



###Mapping
Mapping meta::analytics::mapping::modelCoverage::test::sampleRelationalMapping
(
  *meta::analytics::mapping::modelCoverage::test::Firm: Relational
  {
    ~primaryKey
    (
      [meta::analytics::mapping::modelCoverage::test::sampleDB]FirmTable.id
    )
    ~mainTable [meta::analytics::mapping::modelCoverage::test::sampleDB]FirmTable
    employees
    (
      firstName: [meta::analytics::mapping::modelCoverage::test::sampleDB]PersonTable.firstName
    ),
    address: [meta::analytics::mapping::modelCoverage::test::sampleDB]@firm_address
  }
  *meta::analytics::mapping::modelCoverage::test::Person: Relational
  {
    ~primaryKey
    (
      [meta::analytics::mapping::modelCoverage::test::sampleDB]PersonTable.id
    )
    ~mainTable [meta::analytics::mapping::modelCoverage::test::sampleDB]PersonTable
    firstName: [meta::analytics::mapping::modelCoverage::test::sampleDB]PersonTable.firstName,
    lastName: [meta::analytics::mapping::modelCoverage::test::sampleDB]PersonTable.lastName
  }
  *meta::analytics::mapping::modelCoverage::test::LegalEntity: Operation
  {
    meta::pure::router::operations::inheritance_OperationSetImplementation_1__SetImplementation_MANY_()
  }
  *meta::analytics::mapping::modelCoverage::test::Address: Operation
  {
    meta::pure::router::operations::inheritance_OperationSetImplementation_1__SetImplementation_MANY_()
  }
  *meta::analytics::mapping::modelCoverage::test::Street: Operation
  {
    meta::pure::router::operations::union_OperationSetImplementation_1__SetImplementation_MANY_(s1,s2)
  }
  meta::analytics::mapping::modelCoverage::test::Street[s2]: Relational
  {
    ~primaryKey
    (
      [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable.id
    )
    ~mainTable [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable
    zipcode: [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable.id
  }
  meta::analytics::mapping::modelCoverage::test::Street[s1]: Relational
  {
    ~primaryKey
    (
      [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable.id
    )
    ~mainTable [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable
    zipcode: [meta::analytics::mapping::modelCoverage::test::sampleDB]AddressTable.id
  }
)

###Mapping
Mapping meta::analytics::mapping::modelCoverage::test::sampleModelToModelMapping
(
  meta::analytics::mapping::modelCoverage::test::_Person: Pure
  {
    ~src meta::analytics::mapping::modelCoverage::test::Person
    fullName: $src.firstName + ' ' + $src.lastName
  }
  meta::analytics::mapping::modelCoverage::test::_Firm: Pure
  {
    ~src meta::analytics::mapping::modelCoverage::test::Firm
    employees: $src.employees
  }
  *meta::analytics::mapping::modelCoverage::test::_Street: Operation
  {
    meta::pure::router::operations::union_OperationSetImplementation_1__SetImplementation_MANY_(s1,s2)
  }
  meta::analytics::mapping::modelCoverage::test::_Street[s2]: Pure
  {
    ~src meta::analytics::mapping::modelCoverage::test::Street
    streetName: $src.streetName + '2'
  }
  meta::analytics::mapping::modelCoverage::test::_Street[s1]: Pure
  {
    ~src meta::analytics::mapping::modelCoverage::test::Street
    streetName: $src.streetName + '1'
  }
)
