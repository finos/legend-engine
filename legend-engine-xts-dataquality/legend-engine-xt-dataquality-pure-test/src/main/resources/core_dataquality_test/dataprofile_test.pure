// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::metamodel::relation::*;
import meta::pure::runtime::*;
import meta::pure::mapping::*;
import meta::external::dataquality::dataprofile::*;
import meta::external::dataquality::tests::*;
import meta::external::dataquality::dataprofile::tests::*;
import meta::pure::router::preeval::*;

function <<test.Test>> meta::external::dataquality::tests::testDataProfileLambdaGeneration():Boolean[1]
{
  let runtime = loadModel([])->filter(p | $p->elementToPath() == 'meta::external::dataquality::tests::domain::DataQualityRuntime')->toOne()->cast(@PackageableRuntime);

  let lambda = {|
      let cte = #>{meta::external::dataquality::tests::domain::db.personTable}#->select(~[DOB, AGE, FIRSTNAME]);

      $cte->aggregate(~[
          column_name: x | 'DOB' : y | 'DOB',
          count: x | $x.DOB : y | $y->count(),
          count_distinct: x | $x.DOB : y | $y->distinct()->count(),
          count_null: x | if ($x.DOB->isEmpty(), | 1, | []) : y | $y->count(),
          'number/max' : x | []->cast(@Number) : y | $y->max(),
          'number/min' : x | []->cast(@Number) : y | $y->min(),
          'date/max' : x | $x.DOB->cast(@Date) : y | $y->max(),
          'date/min' : x | $x.DOB->cast(@Date) : y | $y->min(),
          'string/max_length': x | []->cast(@Integer) : y | $y->max(),
          'string/min_length': x | []->cast(@Integer) : y | $y->min()
        ])->concatenate(
          $cte->aggregate(~[
                column_name: x | 'AGE' : y | 'AGE',
                count: x | $x.AGE : y | $y->count(),
                count_distinct: x | $x.AGE : y | $y->distinct()->count(),
                count_null: x | if ($x.AGE->isEmpty(), | 1, | []) : y | $y->count(),
                'number/max' : x | $x.AGE->cast(@Number) : y | $y->max(),
                'number/min' : x | $x.AGE->cast(@Number) : y | $y->min(),
                'date/max' : x | []->cast(@Date) : y | $y->max(),
                'date/min' : x | []->cast(@Date) : y | $y->min(),
                'string/max_length': x | []->cast(@Integer) : y | $y->max(),
                'string/min_length': x | []->cast(@Integer) : y | $y->min()
          ])
        )->concatenate(
                $cte->aggregate(~[
                      column_name: x | 'FIRSTNAME' : y | 'FIRSTNAME',
                      count: x | $x.FIRSTNAME : y | $y->count(),
                      count_distinct: x | $x.FIRSTNAME : y | $y->distinct()->count(),
                      count_null: x | if ($x.FIRSTNAME->isEmpty(), | 1, | []) : y | $y->count(),
                      'number/max' : x | []->cast(@Number) : y | $y->max(),
                      'number/min' : x | []->cast(@Number) : y | $y->min(),
                      'date/max' : x | []->cast(@Date) : y | $y->max(),
                      'date/min' : x | []->cast(@Date) : y | $y->min(),
                      'string/max_length': x | $x.FIRSTNAME->toOne()->length() : y | $y->max(),
                      'string/min_length': x | $x.FIRSTNAME->toOne()->length() : y | $y->min()
                      ])
              );
  };

  let validation = '###DataQualityValidation' +
    '      DataQualityRelationValidation meta::external::dataquality::tests::domain::RelationValidation' +
    '      {' +
    '        query: #>{meta::external::dataquality::tests::domain::db.personTable}#->select(~[DOB, AGE, FIRSTNAME])->from(meta::external::dataquality::tests::domain::DataQualityRuntime);' +
    '        validations: [' +
    '          {' +
    '             name: \'validFirstName\';' +
    '             description: \'first name should not be empty\';' +
    '             assertion: row2|$row2.AGE->isNotEmpty();' +
    '             type: ROW_LEVEL;' +
    '          }' +
    '         ];' +
    '      }';

  doTest(
    $validation,
    {|from($lambda->eval(), $runtime)},
    true
  );

  doTest(
    $validation,
    $lambda,
    false
  );
}

function meta::external::dataquality::tests::doTest(dq:String[1], expected:FunctionDefinition<Any>[1], wrapWithFrom: Boolean[1]):Boolean[1]
{
  let dataqualityRelationValidation = $dq->loadDataQualityRelationValidation();

  let actual = $dataqualityRelationValidation->getProfilingLambda($wrapWithFrom);

  let expectedPrevaled = if ($wrapWithFrom, | $expected->preval([]), | $expected);
  let actualPrevaled = if ($wrapWithFrom, | $actual->preval([]), | $actual);

  assertLambdaEquals($expectedPrevaled, $actualPrevaled->cast(@LambdaFunction<Any>));
}