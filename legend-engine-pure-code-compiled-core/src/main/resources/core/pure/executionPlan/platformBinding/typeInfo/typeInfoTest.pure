// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::executionPlan::platformBinding::typeInfo::*;
import meta::pure::executionPlan::platformBinding::typeInfo::tests::*;
import meta::pure::lineage::scanProperties::propertyTree::*;

Enum meta::pure::executionPlan::platformBinding::typeInfo::tests::Colour
{
   RED, GREEN, BLUE
}

Enum meta::pure::executionPlan::platformBinding::typeInfo::tests::Measure
{
   b, Kb, Mb, Gb, Tb
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::Memory
{
   amount  : Integer[1];
   measure : meta::pure::executionPlan::platformBinding::typeInfo::tests::Measure[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::A
{
   s   : String[1];
   f   : Float[1];
   sd  : StrictDate[1];
   c   : Colour[0..1];
   ram : Memory[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::B extends A
{
   i    : Integer[1];
   d    : Decimal[1];
   disk : Memory[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::C
{
   s      : String[1];
   someAs : A[*];
   moreAs : A[*];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::E
{
   s: String[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::F
{
   s   : String[1];
   ssd : Memory[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::G
{
   s: String[1];
}

Association meta::pure::executionPlan::platformBinding::typeInfo::tests::E_F
{
   e : E[1];
   fs: F[*];
}

Association meta::pure::executionPlan::platformBinding::typeInfo::tests::E_G
{
   e : E[1];
   gs: G[*];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::H extends A
{
   i: Integer[1];
   b: Boolean[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::I extends B, H
{
   s: String[1];
}

Class meta::pure::executionPlan::platformBinding::typeInfo::tests::Trade
[
   if($this.side->isEmpty() || $this.side == Side.BUY, |$this.quantity > 0, |$this.quantity < 0)
]
{
   quantity: Integer[1];
   side: Side[0..1];
}

Enum meta::pure::executionPlan::platformBinding::typeInfo::tests::Side
{
   BUY, SELL
}

Class meta::alloy::test::runtime::java::typeInfo::Car
[
   wheelsOn: $this.wheels->forAll(w| $w.attached),
   canRoll: $this.allRound,
   always: true
]
{
   wheels: Wheel[3..*];

   allRound()
   {
      $this.wheels->forAll(w| $w.round)
   }: Boolean[1];
}

Class meta::alloy::test::runtime::java::typeInfo::Wheel
{
   attached: Boolean[1];
   round: Boolean[1];
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testGraphAddsInheritesOnlyOnce(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{I {s, f, sd, c, i, d, b}}#);
   assertEquals(5, $typeInfos.typeInfos->size());
   
   assertTypeInfo(['s', 'f', 'sd', 'c'], [], $typeInfos->forClass(A));
   assertTypeInfo(['i', 'd'], [A], $typeInfos->forClass(B));
   assertTypeInfo(['i', 'b'], [A], $typeInfos->forClass(H));
   assertTypeInfo(['s'], [A, B, H], $typeInfos->forClass(I));
   assertEquals(Colour, $typeInfos->forEnumeration(Colour).enumeration());

   assertProperties(['s', 'f', 'sd', 'c', 'i', 'd', 'b'], $typeInfos->allProperties(I), I);
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testGraphIncludesOnlySpecifiedClassProperties(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{C {s, someAs {s, f, sd, c}}}#);
   assertEquals(3, $typeInfos.typeInfos->size());
   
   assertTypeInfo(['s', 'f', 'sd', 'c'], [], $typeInfos->forClass(A));
   assertTypeInfo(['s', 'someAs'], [], $typeInfos->forClass(C));
   assertEquals(Colour, $typeInfos->forEnumeration(Colour).enumeration());
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testGraphMergesForTrees(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{C {s, someAs {s, f}}}#)->addForGraphFetchTree(#{C {moreAs {sd, c}}}#);
   assertEquals(3, $typeInfos.typeInfos->size());
   
   assertTypeInfo(['s', 'f', 'sd', 'c'], [], $typeInfos->forClass(A));
   assertTypeInfo(['s', 'someAs', 'moreAs'], [], $typeInfos->forClass(C));
   assertEquals(Colour, $typeInfos->forEnumeration(Colour).enumeration());
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testGraphIncludesOnlySpecifiedAssociations(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{E {s, fs {s}}}#);
   assertEquals(2, $typeInfos.typeInfos->size());
   
   assertTypeInfo(['s', 'fs'], [], $typeInfos->forClass(E));
   assertTypeInfo(['s', 'e'], [], $typeInfos->forClass(F));
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testClassDependenciesViaProperties(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{E {fs {ssd}}}#);
   assertClasses([F, Memory], $typeInfos->classDependenciesViaPropertiesWithSubTypes(E), E, 'classDependenciesViaProperties');
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testEnumDependenciesViaProperties(): Boolean[1]
{
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree(#{E {fs {ssd {amount,measure}}}}#);
   assert($typeInfos->enumDependenciesViaProperties(E)->containsAll(meta::pure::executionPlan::platformBinding::typeInfo::tests::Measure), |'Unexpected enums: ' + $typeInfos->enumDependenciesViaProperties(E)->map(e| $e->enumName())->joinStrings(','));
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testAddingConstraintsAddsEnum(): Boolean[1]
{
   let tree      = #{Trade {quantity}}#;
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree($tree)->addConstraintsForGraphFetchTree($tree);
   assertEquals(Side, $typeInfos->forEnumeration(Side).enumeration());
   assertEquals('0', $typeInfos->forClass(Trade).constraints.id);
   assertEquals('root\n' +
                ' c_Trade\n' +
                '  p_Trade.quantity\n' +
                '  p_Trade.side\n',
                $typeInfos->forClass(Trade).constraints.propertyTree->toOne()->printTree(''));
}

function <<test.Test>> meta::pure::executionPlan::platformBinding::typeInfo::tests::testAddingConstraintsProperties(): Boolean[1]
{
   let tree      = #{Car {wheels{attached}}}#;
   let typeInfos = newTypeInfoSet()->addForGraphFetchTree($tree)->addConstraintsForGraphFetchTree($tree);

   let c0 = $typeInfos->forClass(Car).constraints->at(0);
   assertEquals('wheelsOn', $c0.id);
   assertEquals('root\n' +
                ' c_Car\n' +
                '  p_Car.wheels\n' +
                '   p_Wheel.attached\n',
                $c0.propertyTree->printTree(''));

   let c1 = $typeInfos->forClass(Car).constraints->at(1);
   assertEquals('canRoll', $c1.id);
   assertEquals('root\n' +
                ' c_Car\n' +
                '  p_Car.wheels\n' +
                '   p_Wheel.round\n',
                $c1.propertyTree->printTree(''));

   let c2 = $typeInfos->forClass(Car).constraints->at(2);
   assertEquals('always', $c2.id);
   assertEquals('root\n', $c2.propertyTree->printTree(''));
}

function <<access.private>> meta::pure::executionPlan::platformBinding::typeInfo::tests::assertTypeInfo(expectedProperties:String[*], expectedSupertypes:Type[*], actual:ClassTypeInfo[1]): Boolean[1]
{
   assertProperties($expectedProperties, $actual.properties, $actual.type);
   assertClasses($expectedSupertypes, $actual.supertypes, $actual.type, 'supertypes');
}

function <<access.private>> meta::pure::executionPlan::platformBinding::typeInfo::tests::assertProperties(expectedProperties:String[*], actual:AbstractProperty<Any>[*], type:Type[1]): Boolean[1]
{
   assertEquals($expectedProperties->size(), $actual->size(), 'For type %s, properties: expected [%s], actual: [%s]', [$type.name->toOne(), $expectedProperties->joinStrings(','), $actual.name->joinStrings(',')]);
   assert($actual.name->containsAll($expectedProperties),     'For type %s, properties: expected [%s], actual: [%s]', [$type.name->toOne(), $expectedProperties->joinStrings(','), $actual.name->joinStrings(',')]);
}

function <<access.private>> meta::pure::executionPlan::platformBinding::typeInfo::tests::assertClasses(expectedClasses:Type[*], actual:Class<Any>[*], type:Type[1], purpose:String[1]): Boolean[1]
{
   assertEquals($expectedClasses->size(), $actual->size(), 'For type %s, %s: expected [%s], actual: [%s]', [$type.name->toOne(), $purpose, $expectedClasses.name->joinStrings(','), $actual.name->joinStrings(',')]);
   assert($actual->containsAll($expectedClasses),          'For type %s, %s: expected [%s], actual: [%s]', [$type.name->toOne(), $purpose, $expectedClasses.name->joinStrings(','), $actual.name->joinStrings(',')]);
}

