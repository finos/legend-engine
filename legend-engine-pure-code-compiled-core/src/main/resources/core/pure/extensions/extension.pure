// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::java::metamodel::*;
import meta::pure::graphFetch::executionPlan::*;
import meta::pure::graphFetch::routing::*;
import meta::pure::graphFetch::*;
import meta::pure::tds::schema::*;
import meta::pure::executionPlan::*;
import meta::pure::router::routing::*;
import meta::pure::router::clustering::*;
import meta::pure::mapping::*;
import meta::pure::runtime::*;
import meta::pure::store::*;
import meta::pure::extension::*;
import meta::external::shared::format::*;

Class meta::pure::extension::Extension
{
  type : String[1];

  // Store Extension --------------------------------------------------------------------------
  availableStores : StoreContract[*];
  // -------------------------------------------------------------------------- Store Extension

  // External Format Extension ----------------------------------------------------------------
  // TODO: We should change this package to better align with our strategy to have binding as core feature
  availableExternalFormats : meta::external::shared::format::ExternalFormatContract<meta::external::shared::format::metamodel::SchemaDetail>[*];
  // ---------------------------------------------------------------- External Format Extension

  // Feature Extension ------------------------------------------------------------------------
  availableFeatures : FeatureExtension[*];
  // ------------------------------------------------------------------------ Feature Extension

  routerExtensions() 
  {
    $this.availableStores->concatenate($this.availableFeatures)->cast(@meta::pure::router::extension::RouterExtension)
  } : meta::pure::router::extension::RouterExtension[*];

  executionPlanExtensions()
  {
    $this.availableStores->concatenate($this.availableFeatures)->cast(@meta::pure::executionPlan::extension::ExecutionPlanExtension)
  } : meta::pure::executionPlan::extension::ExecutionPlanExtension[*];

  // Platform binding -------------------------------------------------------------------------
  availablePlatformBindings : meta::pure::executionPlan::platformBinding::PlatformBinding[*];
  // ------------------------------------------------------------------------- Platform binding

  // ExecutePlan
  executionPlan_execution_processNode : Function<{Mapping[1], Runtime[1], Pair<VariableExpression, Any>[*], meta::pure::extension::Extension[*] -> Function<{Nil[1] -> Result<Any|*>[1]}>[*]}>[0..1];
  executionPlan_execution_allNodes : Function<{meta::pure::extension::Extension[*] -> Function<{Nil[1] ->  ExecutionNode[*]}>[*]}>[0..1];
  executionPlan_execution_buildVariableTemplateString : Function<{String[1], Any[1], meta::pure::metamodel::type::Type[1] -> Function<{Nil[1] ->  String[1]}>[*]}>[0..1];

  //Build Context
  //TODO: Move to its apt place, with the new extension framework 
  routeContextBuilderFunctions : Function<{FunctionExpression[1]->ExecutionContext[1]}>[0..1];

  // M2M concern that leaks to CSV and Flatdata. Remove when CSV and Flatdata are absorbed by ExternalFormat ------------------------------
  graphExtension_calculateSourceTree : Function<{RootGraphFetchTree<Any>[1] -> Function<{Nil[1]->RootGraphFetchTree<Any>[1]}>[1]}>[0..1];
  // --------------------------------------------------------------------------------------------------------------------------------------

  //---------------------------------------------------------------------------------------------------------------------------------------
  // meta::pure::tds::join is WRONGLY in Relational because it uses JoinType
  // meta::pure::tds::extensions::columnValueDifference is WRONGLY in Relational because it uses Join
  // meta::pure::tds::project(tds:meta::relational::mapping::TableTDS[1], columnFunctions:ColumnSpecification<TDSRow>[*]) is in relational because of TableTDS but it should be deleted as TableTDS is a TabularDataSet
  tdsSchema_resolveSchemaImpl : Function<{FunctionExpression[1], Map<String, List<Any>>[1], Extension[*] -> Pair<Function<Any>, Function<{->SchemaState[1]}>>[*]}>[0..1];
  //---------------------------------------------------------------------------------------------------------------------------------------

  // testedBy navigates the Database structure. Should probably delete --------------------------------------------------------------------
  testExtension_testedBy : Function<{ReferenceUsage[1], Extension[*] -> Function<{TestedByResult[1]->TestedByResult[1]}>[*]}>[0..1];
  //---------------------------------------------------------------------------------------------------------------------------------------

  // logActivities implementation is in Relationl because it casts Activity to RelationalActivity -----------------------------------------
  logActivities : Function<{meta::pure::mapping::Activity[*] -> Nil[0]}>[0..1];
  //---------------------------------------------------------------------------------------------------------------------------------------

  // Used by the graph serializer to ensure we push serialize Test models stored in the system area ---------------------------------------
  validTestPackages : String[*];
  //---------------------------------------------------------------------------------------------------------------------------------------

  // Serializer Extension ---------------------------------------------------------------------
  serializerExtension : Function<{String[1]->String[1]}>[0..1];
  serializerExtensionByProtocolVersion : Pair<String, SerializerExtension>[*];

  serializerExtension(version : String[1])
  {
    let ext = $this;
    $this.serializerExtensionByProtocolVersion->filter(p|$p.first == $version)->match([
      { found : Pair<String, SerializerExtension> [1] | $found.second; },
      { missing : Pair<String, SerializerExtension> [0] |
          let serializer = $this.fetchSerializerExtension($version);
          if($serializer->isEmpty(),
            | $serializer,
            | $ext->mutateAdd('serializerExtensionByProtocolVersion', pair($version, $serializer->toOne()));
                $serializer->toOne();
          );
      }]);
  } : SerializerExtension[0..1];

  fetchSerializerExtension(version : String[1])
  {
    if ($this.serializerExtension->isEmpty(),
      |[],
      |let funcString = $this.serializerExtension->toOne()->eval($version);
       let func = $funcString->forgivingPathToElement()->cast(@Function<{String[1]->SerializerExtension[1]}>);
       if($func->isEmpty(),
          |[],
          |$func->toOne()->eval($this.type)
       );
    )
  } : SerializerExtension[0..1];
  // --------------------------------------------------------------------- Serializer Extension

  // Module Extension ---------------------------------------------------------------------
  moduleExtensions : ModuleExtension[*];

  moduleExtension(module:String[1])
  {
    $this.moduleExtensions->filter(f|$f.module == $module)->first()
  } : ModuleExtension[0..1];
  // --------------------------------------------------------------------- Module Extension
}

Class meta::pure::extension::SerializerExtension
{
}

Class meta::pure::extension::FeatureExtension extends meta::pure::router::extension::RouterExtension, meta::pure::executionPlan::extension::ExecutionPlanExtension
{
  id : String[1];
}

Class meta::pure::extension::ModuleExtension
{
    module : String[1];
}
