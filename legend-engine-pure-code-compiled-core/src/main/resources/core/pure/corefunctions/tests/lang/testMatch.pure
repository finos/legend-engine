// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::profiles::*;
import meta::pure::functions::tests::model::*;

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchOneWithZeroOne():Boolean[1]
{
   assertEquals(1, [1]->match([
         i:Integer[0..1] | [1],
         s:String[1]     | 'address:'
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchOneWith():Boolean[1]
{
   assertEquals('address:Digby', 'Digby'->match([
         i:Integer[1..4] | [1,2,3],
         s:String[1]     | 'address:' + $s
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchOneWithMany():Boolean[1]
{
   assertEquals('address:Digby', 'Digby'->match([
         i:Integer[1..4] | [1,2,3],
         s:String[1..2]  | 'address:Digby'
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchZeroWithZero():Boolean[1]
{
   assertEquals('address', []->cast(@String)->match([
         i:Integer[1..4] | [1,2,3],
         s:String[0]     | 'address'
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchZeroWithMany():Boolean[1]
{
   assertEquals('address', []->cast(@String)->match([
         i:Integer[1..4] | [1,2,3],
         s:String[*]     | 'address'
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchManyWithMany():Boolean[1]
{
   assertEquals(['address:','w','w','w'], ['w','w','w']->match([
         i:Integer[1..4] | [1,2,3],
         s:String[*]     | 'address:'->concatenate($s)
   ]))
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatch():Boolean[1]
{
    assertEq('address', ^Address(name='Hoboken, NJ', type=GeographicEntityType.CITY)->match([a:Address[1] | 'address', l:Location[1] | 'location', a:Any[1] | 'Any1']));
    assertEq('location', ^Location(place='Hoboken, NJ', type=GeographicEntityType.CITY)->match([a:Address[1] | 'address', l:Location[1] | 'location', a:Any[1] | 'Any1']));
    assertEq('Any1', ^Person(firstName='Pierre', lastName='Doe')->match([a:Address[1] | 'address', l:Location[1] | 'location', a:Any[1] | 'Any1']));
    assertEq('Any*', ^Person(firstName='Pierre', lastName='Doe')->match([a:Address[1] | 'address', l:Location[1] | 'location', a:Any[*] | 'Any*']));
    assertEq('Any*', [^Person(firstName='Pierre', lastName='Doe'), ^Person(firstName='Fabrice', lastName='Smith')]->match([a:Address[1] | 'address', l:Location[1] | 'location', a:Any[*] | 'Any*']));
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithMixedReturnType():Boolean[1]
{
    let f = ^Location(place='Hoboken, NJ', type=GeographicEntityType.CITY)->match([a:Address[1] | 'address', l:Location[1] | 1, a:Any[1] | 'Any1'])->deactivate();
    assertIs(Any, $f.genericType.rawType->toOne());
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithExtraParam():Boolean[1]
{
   assertEquals('good_other', [1]->match([{i:Integer[1], b:String[1]|'good_'+$b},
                                          {s:String[1], b:String[1]|'bad'}
                                         ],'other')
   );
   
   assertEquals('good_otheryo', [1]->match([{i:Integer[1], b:String[*]|'good_'+$b->joinStrings();},
                                            {s:String[1], b:String[*]|'bad'}
                                         ],['other', 'yo'])
   );
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithExtraParamsAndFunctionsAsParam():Boolean[1]
{
    let lambdas = [{a:String[1],b:String[1]|'1'+$b},{a:Integer[1],b:String[1]|$b},{a:Date[1],b:String[1]|'5'+$b}];
    assertEquals('11', '1'->match($lambdas, '1'));
    assertEquals('2', 1->match($lambdas, '2'));
    assertEquals('53', %2005->match($lambdas, '3'));
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithFunctionsAsParam():Boolean[1]
{
    let lambdas = [{a:Integer[1]|1}, {a:String[1]|[1,2]}, {a:Date[1]|[4,5,6]}];
    assertEquals([1], 1->match($lambdas));
    assertEquals([1,2], '1'->match($lambdas));
    assertEquals([4,5,6], %2005->match($lambdas));
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithFunctionsAsParamManyMatch():Boolean[1]
{
    let lambdas = [{a:Integer[1]|1}, {a:String[1]|[6,7,1,2]}, {a:String[*]|$a}, {a:Date[1]|[4,5,6]}];
    assertEquals([1], 1->match($lambdas));
    assertEquals(['1','2'], ['1','2']->match($lambdas));
    assertEquals([6,7,1,2], '1'->match($lambdas));
    assertEquals([4,5,6], %2005->match($lambdas));

    let lambdas2 = [{a:Integer[1]|1}, {a:String[*]|$a}, {a:String[1]|[6,7,1,2]}, {a:Date[1]|[4,5,6]}];
    assertEquals([1], 1->match($lambdas2));
    assertEquals(['1','2'], ['1','2']->match($lambdas2));
    assertEquals('1', '1'->match($lambdas2));
    assertEquals([4,5,6], %2005->match($lambdas2));
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithFunctions():Boolean[1]
{
    assertEquals([1], 1->match([{a:Integer[1]|1}, {a:String[1]|[1,2]}, {a:Date[1]|[4,5,6]}]));
    assertEquals([1,2], '1'->match([{a:Integer[1]|1}, {a:String[1]|[1,2]}, {a:Date[1]|[4,5,6]}]));
    assertEquals([4,5,6], %2005->match([{a:Integer[1]|1}, {a:String[1]|[1,2]}, {a:Date[1]|[4,5,6]}]));
}

function <<test.Test>> meta::pure::functions::lang::tests::match::testMatchWithFunctionsManyMatch():Boolean[1]
{
 
    assertEquals([1], 1->match([{a:Integer[1]|1}, {a:String[1]|[6,7,1,2]}, {a:String[*]|$a}, {a:Date[1]|[4,5,6]}]));
    assertEquals(['1','2'], ['1','2']->match([{a:Integer[1]|1}, {a:String[1]|[6,7,1,2]}, {a:String[*]|$a}, {a:Date[1]|[4,5,6]}]));
    assertEquals([6,7,1,2], '1'->match([{a:Integer[1]|1}, {a:String[1]|[6,7,1,2]}, {a:String[*]|$a}, {a:Date[1]|[4,5,6]}]));
    assertEquals([4,5,6], %2005->match([{a:Integer[1]|1}, {a:String[1]|[6,7,1,2]}, {a:String[*]|$a}, {a:Date[1]|[4,5,6]}]));

  
    assertEquals([1], 1->match([{a:Integer[1]|1}, {a:String[*]|$a}, {a:String[1]|[6,7,1,2]}, {a:Date[1]|[4,5,6]}]));
    assertEquals(['1','2'], ['1','2']->match([{a:Integer[1]|1}, {a:String[*]|$a}, {a:String[1]|[6,7,1,2]}, {a:Date[1]|[4,5,6]}]));
    assertEquals('1', '1'->match([{a:Integer[1]|1}, {a:String[*]|$a}, {a:String[1]|[6,7,1,2]}, {a:Date[1]|[4,5,6]}]));
    assertEquals([4,5,6], %2005->match([{a:Integer[1]|1}, {a:String[*]|$a}, {a:String[1]|[6,7,1,2]}, {a:Date[1]|[4,5,6]}]));
}