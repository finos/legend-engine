// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Pure
Class join::model::Person
{
  firstName: String[1];
  lastName: String[1];
  firm: join::model::Firm[1];
}

Class join::model::Firm
{
  ID: Integer[1];
  legalName: String[1];
  otherNames: String[1];
}

###Relational
Database join::store::H2DB
(
  Schema PERSON_SCHEMA
  (
    Table PERSON_TABLE
    (
      ID INTEGER PRIMARY KEY,
      FIRSTNAME VARCHAR(100),
      LASTNAME VARCHAR(100),
      FIRM SEMISTRUCTURED
    )
  )
  Schema FIRM_SCHEMA
  (
    Table FIRM_TABLE
    (
      FIRM_DETAILS SEMISTRUCTURED PRIMARY KEY
    )
  )
  Join FirmPerson(extractFromSemiStructured(PERSON_SCHEMA.PERSON_TABLE.FIRM, 'ID', 'INTEGER') = extractFromSemiStructured(FIRM_SCHEMA.FIRM_TABLE.FIRM_DETAILS, 'ID', 'INTEGER'))
)

###ExternalFormat
Binding join::store::FirmBinding
{
  contentType: 'application/json';
  modelIncludes: [
    join::model::Firm
  ];
}

###Mapping
Mapping join::mapping::H2Mapping
(
  *join::model::Person: Relational
  {
    ~primaryKey
    (
      [join::store::H2DB]PERSON_SCHEMA.PERSON_TABLE.ID
    )
    ~mainTable [join::store::H2DB]PERSON_SCHEMA.PERSON_TABLE
    firstName: [join::store::H2DB]PERSON_SCHEMA.PERSON_TABLE.FIRSTNAME,
    lastName: [join::store::H2DB]PERSON_SCHEMA.PERSON_TABLE.LASTNAME,
    firm: Binding join::store::FirmBinding : [join::store::H2DB]@FirmPerson | [join::store::H2DB]FIRM_SCHEMA.FIRM_TABLE.FIRM_DETAILS

  }
)

###Pure
function join::testJoinOnSemiStructuredProperty():TabularDataSet[1]
{
    join::model::Person.all()->project(
      [
        x|$x.firstName,
        x|$x.lastName,
        x|$x.firm.legalName
      ],
      [
        'First Name',
        'Last Name',
        'Firm/Legal Name'
      ]
    );
}