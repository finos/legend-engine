// Copyright 2026 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::executionPlan::m2m2r::tests::*;

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planProjectWithDerivedProperty() : Boolean[1]
{
  let query = {|Person.all()->project([x|$x.fullName], ['fullName'])};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 'Relational\n(\n  type = TDS[(fullName, String, VARCHAR(8192), "")]\n  resultColumns = [("fullName", "")]\n  sql = select concat("root".firstName, \' \', "root".lastName) as "fullName" from Person as "root"\n  connection = TestDatabaseConnection(type = "H2")\n)\n';
  assertEquals($expected, $plan);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planProjectWithDerivedProperty1() : Boolean[1]
{
  let query = {|Person.all()->project([x|$x.isName('Peter Smith')], ['isPeterSmith'])};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 'Relational\n(\n  type = TDS[(isPeterSmith, Boolean, BIT, "")]\n  resultColumns = [("isPeterSmith", "")]\n  sql = select concat("root".firstName, \' \', "root".lastName) = \'Peter Smith\' as "isPeterSmith" from Person as "root"\n  connection = TestDatabaseConnection(type = "H2")\n)\n';
  assertEquals($expected, $plan);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planGraphFetchWithDerivedProperty() : Boolean[1]
{
  let query = {|PersonPeterSmith.all()->meta::pure::graphFetch::execution::graphFetch(#{PersonPeterSmith{isPeterSmith}}#)};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 
    'StoreMappingGlobalGraphFetch\n' +
    '(\n' +
    '  type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith | ModelToModelMapping.meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith)], propertiesWithParameters = [isPeterSmith()]]\n' +
    '  resultSizeRange = *\n' +
    '  store = MODEL\n' +
    '  localGraphFetchExecutionNode = \n' +
    '     InMemoryRootGraphFetch\n' +
    '     (\n' +
    '       type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith | ModelToModelMapping.meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith)], propertiesWithParameters = [isPeterSmith()]]\n' +
    '       graphFetchTree = [meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith/meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith]{[/isPeterSmith()]}\n' +
    '       nodeIndex = 0\n' +
    '       batchSize = 1\n' +
    '       checked = false\n' +
    '       (\n' +
    '         StoreMappingGlobalGraphFetch\n' +
    '         (\n' +
    '           type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::_Person | RelationalMapping.meta_pure_executionPlan_m2m2r_tests__Person)], propertiesWithParameters = [firstName, lastName]]\n' +
    '           resultSizeRange = *\n' +
    '           store = meta::pure::executionPlan::m2m2r::tests::DB\n' +
    '           localGraphFetchExecutionNode = \n' +
    '              RelationalGraphFetch\n' +
    '              (\n' +
    '                type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::_Person | RelationalMapping.meta_pure_executionPlan_m2m2r_tests__Person)], propertiesWithParameters = [firstName, lastName]]\n' +
    '                nodeIndex = 0\n' +
    '                relationalNode = \n' +
    '                   SQL\n' +
    '                   (\n' +
    '                     type = meta::pure::metamodel::type::Any\n' +
    '                     resultColumns = [("pk_0", INT), ("firstName", VARCHAR(200)), ("lastName", VARCHAR(200))]\n' +
    '                     sql = select "root".id as "pk_0", "root".firstName as "firstName", "root".lastName as "lastName" from Person as "root"\n' +
    '                     connection = TestDatabaseConnection(type = "H2")\n' +
    '                   )\n' +
    '                children = [\n' +
    '                   \n' +
    '                ]\n' +
    '              )\n' +
    '           children = [\n' +
    '              \n' +
    '           ]\n' +
    '           localTreeIndices = [0, 1, 2]\n' +
    '           dependencyIndices = []\n' +
    '         )\n' +
    '       )\n' +
    '       children = [\n' +
    '          \n' +
    '       ]\n' +
    '     )\n' +
    '  children = [\n' +
    '     \n' +
    '  ]\n' +
    '  localTreeIndices = [0, 1]\n' +
    '  dependencyIndices = []\n' +
    ')\n';
  assertEquals($expected, $plan);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planProjectWithNestedProperty() : Boolean[1]
{
  let query = {|PersonPeterSmith.all()->project([x|$x.details.firstName], ['firstName'])};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 'Relational\n(\n  type = TDS[(firstName, String, VARCHAR(8192), "")]\n  resultColumns = [("firstName", VARCHAR(200))]\n  sql = select "root".firstName as "firstName" from Person as "root"\n  connection = TestDatabaseConnection(type = "H2")\n)\n';
  assertEquals($expected, $plan);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planProjectWithNestedDerivedProperty() : Boolean[1]
{
  let query = {|PersonPeterSmith.all()->project([x|$x.isPeterSmith], ['isPeterSmith'])};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 'Relational\n(\n  type = TDS[(isPeterSmith, Boolean, BIT, "")]\n  resultColumns = [("isPeterSmith", "")]\n  sql = select concat("root".firstName, \' \', "root".lastName) = \'Peter Smith\' as "isPeterSmith" from Person as "root"\n  connection = TestDatabaseConnection(type = "H2")\n)\n';
  assertEquals($expected, $plan);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::executeProjectWithNestedDerivedProperty() : Boolean[1]
{
  let query = {|PersonPeterSmith.all()->project(
    [
      x|$x.details.firstName,
      x|$x.details.lastName,
      x|$x.isPeterSmith
    ], 
    [
      'firstName',
      'lastName',
      'isPeterSmith'
    ])};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let setupData = [
    'DROP TABLE IF EXISTS Person;',
    'CREATE TABLE Person(id INT, firstName VARCHAR(200), lastName VARCHAR(200));',
    'INSERT INTO Person(id, firstName, lastName) values (1, \'Grace\', \'Davis\');',
    'INSERT INTO Person(id, firstName, lastName) values (2, \'Alice\', \'Johnson\');',
    'INSERT INTO Person(id, firstName, lastName) values (3, \'Bob\', \'Williams\');',
    'INSERT INTO Person(id, firstName, lastName) values (4, \'Charlie\', \'Brown\');',
    'INSERT INTO Person(id, firstName, lastName) values (5, \'Diana\', \'Jones\');',
    'INSERT INTO Person(id, firstName, lastName) values (6, \'Eve\', \'Garcia\');',
    'INSERT INTO Person(id, firstName, lastName) values (7, \'Frank\', \'Miller\');',
    'INSERT INTO Person(id, firstName, lastName) values (8, \'Peter\', \'Smith\');',
    'INSERT INTO Person(id, firstName, lastName) values (9, \'Henry\', \'Rodriguez\');',
    'INSERT INTO Person(id, firstName, lastName) values (10, \'Ivy\', \'Martinez\');',
    'INSERT INTO Person(id, firstName, lastName) values (11, \'Jack\', \'Hernandez\');'
  ];
  let response = generateAndExecutePlan($query, $mapping, $runtime, $setupData);
  let expected =  '['+
                  '{"firstName":"Grace","lastName":"Davis","isPeterSmith":false},'+
                  '{"firstName":"Alice","lastName":"Johnson","isPeterSmith":false},'+
                  '{"firstName":"Bob","lastName":"Williams","isPeterSmith":false},'+
                  '{"firstName":"Charlie","lastName":"Brown","isPeterSmith":false},'+
                  '{"firstName":"Diana","lastName":"Jones","isPeterSmith":false},'+
                  '{"firstName":"Eve","lastName":"Garcia","isPeterSmith":false},'+
                  '{"firstName":"Frank","lastName":"Miller","isPeterSmith":false},'+
                  '{"firstName":"Peter","lastName":"Smith","isPeterSmith":true},'+
                  '{"firstName":"Henry","lastName":"Rodriguez","isPeterSmith":false},'+
                  '{"firstName":"Ivy","lastName":"Martinez","isPeterSmith":false},'+
                  '{"firstName":"Jack","lastName":"Hernandez","isPeterSmith":false}'+
                  ']';
  assertEquals($expected, $response);
}

function <<meta::pure::profiles::test.Test>> meta::pure::executionPlan::m2m2r::tests::planGraphFetchWithNestedDerivedProperty() : Boolean[1]
{
  let query = {|PersonPeterSmith.all()->meta::pure::graphFetch::execution::graphFetch(#{PersonPeterSmith{isPeterSmith}}#)};
  let runtime = getM2M2RRuntime();
  let mapping = ModelToModelMapping;
  let plan = planToString($query, $mapping, $runtime);
  let expected = 
    'StoreMappingGlobalGraphFetch\n' +
    '(\n' +
    '  type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith | ModelToModelMapping.meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith)], propertiesWithParameters = [isPeterSmith()]]\n' +
    '  resultSizeRange = *\n' +
    '  store = MODEL\n' +
    '  localGraphFetchExecutionNode = \n' +
    '     InMemoryRootGraphFetch\n' +
    '     (\n' +
    '       type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith | ModelToModelMapping.meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith)], propertiesWithParameters = [isPeterSmith()]]\n' +
    '       graphFetchTree = [meta_pure_executionPlan_m2m2r_tests_PersonPeterSmith/meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith]{[/isPeterSmith()]}\n' +
    '       nodeIndex = 0\n' +
    '       batchSize = 1\n' +
    '       checked = false\n' +
    '       (\n' +
    '         StoreMappingGlobalGraphFetch\n' +
    '         (\n' +
    '           type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::_Person | RelationalMapping.meta_pure_executionPlan_m2m2r_tests__Person)], propertiesWithParameters = [firstName, lastName]]\n' +
    '           resultSizeRange = *\n' +
    '           store = meta::pure::executionPlan::m2m2r::tests::DB\n' +
    '           localGraphFetchExecutionNode = \n' +
    '              RelationalGraphFetch\n' +
    '              (\n' +
    '                type = PartialClass[impls=[(meta::pure::executionPlan::m2m2r::tests::_Person | RelationalMapping.meta_pure_executionPlan_m2m2r_tests__Person)], propertiesWithParameters = [firstName, lastName]]\n' +
    '                nodeIndex = 0\n' +
    '                relationalNode = \n' +
    '                   SQL\n' +
    '                   (\n' +
    '                     type = meta::pure::metamodel::type::Any\n' +
    '                     resultColumns = [("pk_0", INT), ("firstName", VARCHAR(200)), ("lastName", VARCHAR(200))]\n' +
    '                     sql = select "root".id as "pk_0", "root".firstName as "firstName", "root".lastName as "lastName" from Person as "root"\n' +
    '                     connection = TestDatabaseConnection(type = "H2")\n' +
    '                   )\n' +
    '                children = [\n' +
    '                   \n' +
    '                ]\n' +
    '              )\n' +
    '           children = [\n' +
    '              \n' +
    '           ]\n' +
    '           localTreeIndices = [0, 1, 2]\n' +
    '           dependencyIndices = []\n' +
    '         )\n' +
    '       )\n' +
    '       children = [\n' +
    '          \n' +
    '       ]\n' +
    '     )\n' +
    '  children = [\n' +
    '     \n' +
    '  ]\n' +
    '  localTreeIndices = [0, 1]\n' +
    '  dependencyIndices = []\n' +
    ')\n';
  assertEquals($expected, $plan);
}

function  meta::pure::executionPlan::m2m2r::tests::planToString(query: FunctionDefinition<Any>[1], mapping: meta::pure::mapping::Mapping[1], runtime: meta::core::runtime::Runtime[1]) : String[1]
{
  meta::pure::executionPlan::executionPlan($query, $mapping, $runtime, meta::relational::extension::relationalExtensions())
  ->meta::pure::executionPlan::toString::planToString(meta::relational::extension::relationalExtensions())
}

function  meta::pure::executionPlan::m2m2r::tests::generateAndExecutePlan(query: FunctionDefinition<Any>[1], mapping: meta::pure::mapping::Mapping[1], runtime: meta::core::runtime::Runtime[1], dbSetupSql: String[*]) : String[1]
{
  let connection = $runtime.connectionStores->at(1);
  $dbSetupSql->map(sqlString | $sqlString->meta::relational::metamodel::execute::executeInDb($connection));
  let plan = meta::pure::executionPlan::executionPlan($query, $mapping, $runtime, meta::relational::extension::relationalExtensions());
  $plan->meta::pure::executionPlan::execute([], meta::relational::extension::relationalExtensions()).values->cast(@TabularDataSet)->toOne()->meta::json::tdsToJSONKeyValueObjectString()->makeString();
}


Class meta::pure::executionPlan::m2m2r::tests::PersonPeterSmith
{
  details : Person[1];
  isPeterSmith()
  {
    $this.details.isName('Peter Smith')
  } : Boolean[1];
}

Class meta::pure::executionPlan::m2m2r::tests::Person
{
  firstName : String[1];
  lastName  : String[1];
  fullName()
  {
    $this.firstName + ' ' + $this.lastName
  } : String[1];
  isName(name : String[1])
  {
    $this.fullName == $name
  } : Boolean[1];
}

Class meta::pure::executionPlan::m2m2r::tests::_Person
{
  firstName : String[1];
  lastName  : String[1];
}

###Relational
Database meta::pure::executionPlan::m2m2r::tests::DB
(
    Table Person
    (
      id INTEGER PRIMARY KEY,
      firstName VARCHAR(200),
      lastName VARCHAR(200)
    )
  )
)

###Mapping 
import meta::pure::executionPlan::m2m2r::tests::*;

Mapping meta::pure::executionPlan::m2m2r::tests::ModelToModelMapping
(
  Person : Pure
  {
    ~src _Person
    firstName : $src.firstName,
    lastName : $src.lastName
  }

  PersonPeterSmith : Pure
  {
    ~src _Person
    details : $src
  }
)

Mapping meta::pure::executionPlan::m2m2r::tests::RelationalMapping
(
  _Person : Relational
  {
    firstName : [DB] Person.firstName,
    lastName : [DB] Person.lastName
  }
)

###Pure
//-------------
// Connections
//-------------
function meta::pure::executionPlan::m2m2r::tests::getH2Connection(): meta::external::store::relational::runtime::DatabaseConnection[1]
{
  ^meta::external::store::relational::runtime::TestDatabaseConnection(
      type=meta::relational::runtime::DatabaseType.H2
   )
}

function meta::pure::executionPlan::m2m2r::tests::getModelChainConnection(): meta::external::store::model::ModelChainConnection[1]
{
  ^meta::external::store::model::ModelChainConnection(
    mappings = [meta::pure::executionPlan::m2m2r::tests::RelationalMapping]
  )
}

//-------------
// ConnectionStores
//-------------
function meta::pure::executionPlan::m2m2r::tests::getRelationalConnectionStore(): meta::core::runtime::ConnectionStore[1]
{
  ^meta::core::runtime::ConnectionStore(
    element = meta::pure::executionPlan::m2m2r::tests::DB,
    connection= meta::pure::executionPlan::m2m2r::tests::getH2Connection()
  )
}

function meta::pure::executionPlan::m2m2r::tests::getM2M2RConnectionStore(): meta::core::runtime::ConnectionStore[1]
{
  ^meta::core::runtime::ConnectionStore(
    element = ^meta::external::store::model::ModelStore(),
    connection= meta::pure::executionPlan::m2m2r::tests::getModelChainConnection()
  )
}

//-------------
// Runtimes
//-------------
function meta::pure::executionPlan::m2m2r::tests::getRelationalRuntime(): meta::core::runtime::Runtime[1]
{
  ^meta::core::runtime::Runtime(
      connectionStores = [meta::pure::executionPlan::m2m2r::tests::getRelationalConnectionStore()]
  )
}

function meta::pure::executionPlan::m2m2r::tests::getM2M2RRuntime(): meta::core::runtime::Runtime[1]
{
  ^meta::core::runtime::Runtime(
      connectionStores = [
        meta::pure::executionPlan::m2m2r::tests::getM2M2RConnectionStore(),
        meta::pure::executionPlan::m2m2r::tests::getRelationalConnectionStore()
      ]
  )
}