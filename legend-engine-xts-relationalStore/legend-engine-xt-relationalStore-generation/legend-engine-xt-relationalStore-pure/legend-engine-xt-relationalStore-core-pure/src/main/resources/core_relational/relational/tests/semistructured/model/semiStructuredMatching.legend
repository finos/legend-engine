// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Pure
Class match::model::Order
{
  orderId: Integer[1];
  orderName: String[1];
  customer: match::model::Customer[1];
}

Class match::model::Customer
{
  customerId: String[1];
  customerAddress: match::model::CustomerAddress[1];
  transactionDetails: match::model::TransactionDetails[1];
}

Class match::model::CustomerAddress
{
  name: String[1];
  defaultAddress: String[0..1];
}

Class match::model::BillingAddress extends match::model::CustomerAddress
{
  billAddress: String[1];
}

Class match::model::ShippingAddress extends match::model::CustomerAddress
{
  shipAddress: String[1];
}

Class match::model::TransactionDetails
{
  payment: match::model::Payment[1];
}

Class match::model::Payment
{
  paymentId: String[1];
}

Class match::model::CashOnDeliveryPayment extends match::model::Payment
{
  amountToBePaid: Integer[1];
}

Class match::model::PrepaidPayment extends match::model::Payment
{
  amountPaid: Integer[1];
}

Class match::model::WalletPrepaidPayment extends match::model::PrepaidPayment
{
  walletTransactionAmount: Integer[1];
}

Class match::model::CardPrepaidPayment extends match::model::PrepaidPayment
{
  cardTransactionAmount: Integer[1];
}

###Relational
Database match::store::H2DB
(
  Schema ORDER_SCHEMA
  (
    Table ORDER_TABLE
    (
      ORDERID INTEGER PRIMARY KEY,
      ORDERNAME VARCHAR(100),
      CUSTOMER SEMISTRUCTURED
    )
  )
)

###ExternalFormat
Binding match::store::OrderBinding
{
  contentType: 'application/json';
  modelIncludes: [
    match::model::Order,
    match::model::Customer,
    match::model::CustomerAddress,
    match::model::BillingAddress,
    match::model::ShippingAddress,
    match::model::TransactionDetails,
    match::model::Payment,
    match::model::CashOnDeliveryPayment,
    match::model::PrepaidPayment,
    match::model::WalletPrepaidPayment,
    match::model::CardPrepaidPayment
  ];
}

###Mapping
Mapping match::mapping::H2Mapping
(
  match::model::Order: Relational
  {
    ~primaryKey
     (
       [match::store::H2DB]ORDER_SCHEMA.ORDER_TABLE.ORDERID
     )
     ~mainTable [match::store::H2DB]ORDER_SCHEMA.ORDER_TABLE
     orderId: [match::store::H2DB]ORDER_SCHEMA.ORDER_TABLE.ORDERID,
     orderName: [match::store::H2DB]ORDER_SCHEMA.ORDER_TABLE.ORDERNAME,
     customer: Binding match::store::OrderBinding : [match::store::H2DB]ORDER_SCHEMA.ORDER_TABLE.CUSTOMER
  }
)

###Pure
function match::semiStructuredMatchComplexProperty(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.customerAddress->match(
                [
                    s: match::model::BillingAddress[1] | $s.billAddress,
                    s: match::model::ShippingAddress[1] | $s.shipAddress,
                    s: Any[1] | 'Default Address'
                ]
            )
        ],
        [
            'Customer Address'
        ]
    );
}

function match::semiStructuredMatchWithDifferentMultiplicities(): TabularDataSet[1]
{
  match::model::Order.all()->project(
    [
      x|$x.customer.customerAddress->match(
      [
        s: match::model::BillingAddress[1]|$s.billAddress,
        s: match::model::ShippingAddress[1]|$s.shipAddress,
        s: match::model::CustomerAddress[1]|$s.defaultAddress,
        s: Any[1]|[]
      ]
    )
    ],
    ['Customer Address']
  )
}

function match::semiStructuredMatchWithMultipleProject(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.customerAddress->match(
                [
                    s: match::model::BillingAddress[1] | $s.billAddress,
                    s: match::model::ShippingAddress[1] | $s.shipAddress
                ]
            ),
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
                    s:match::model::PrepaidPayment[1] | $s.amountPaid
                ]
            )
        ],
        [
            'Customer Address',
            'Order Price'
        ]
    );
}

function match::semiStructuredMatchWithComplexFilter(): TabularDataSet[1]
{
    match::model::Order.all()
        ->filter(
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
                    s:match::model::PrepaidPayment[1] | $s.amountPaid
                ]
            )<200
        )
        ->project(
            [
                x | $x.customer.customerAddress->match(
                    [
                        s: match::model::BillingAddress[1] | $s.billAddress,
                        s: match::model::ShippingAddress[1] | $s.shipAddress
                    ]
                )
            ],
            [
                'Customer Address'
            ]
        );
}

function match::semiStructuredMatchWithVariableAccess(): TabularDataSet[1]
{
    let maxAmount = 200;
    match::model::Order.all()->project(
        [
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] |
                        if($s.amountToBePaid < $maxAmount,
                           |true,
                           |false
                          ),
                    s:match::model::PrepaidPayment[1]  |
                        if($s.amountPaid  < $maxAmount,
                            |true,
                            |false
                          )
                ]
            )
        ],
        [
            'Max Amount Flag'
        ]
    );

}

function match::semiStructuredMatchMultilevel(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.transactionDetails.payment->match(
                [
                    s: match::model::PrepaidPayment[1] | $s->match(
                        [
                            k:match::model::WalletPrepaidPayment[1] | $k.walletTransactionAmount ,
                            k:match::model::CardPrepaidPayment[1]  | $k.cardTransactionAmount,
                            k:match::model::PrepaidPayment[1] | $k.amountPaid
                        ]
                    ),
                    s: match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid
                ]
            )
        ],
        [
            'Amount'
        ]
    );
}

function match::semiStructuredMatchWithMultipleProjectUsingCol(): TabularDataSet[1]
{
    match::model::Order.all()->project([
        col(x | $x.customer.customerAddress->match(
            [
                s: match::model::BillingAddress[1] | $s.billAddress,
                s: match::model::ShippingAddress[1] | $s.shipAddress
            ]),'Customer Address'),
        col(x | $x.customer.transactionDetails.payment->match(
            [
               s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
               s:match::model::PrepaidPayment[1] | $s.amountPaid
            ]),'Order Price')
    ]);
}
