// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the 'License');
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an 'AS IS' BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::external::store::relational::sqlDialectTranslation::tests::utils::*;
import meta::external::query::sql::metamodel::*;
import meta::external::store::relational::sqlDialectTranslation::*;
import meta::external::store::relational::sqlDialectTranslation::functionRegistry::*;
import meta::external::store::relational::sqlDialectTranslation::sqlTyping::sqlTypes::*;
import meta::external::store::relational::sqlDialectTranslation::utils::*;
import meta::external::store::relational::sqlDialectTranslation::functionRegistry::tests::*;

Class {sqlFunctionInfo.initializer = 'averageRank'} meta::external::store::relational::sqlDialectTranslation::functionRegistry::extensionFunctions::window::AverageRank extends ExtensionSqlFunction
[
  $this.name == 'average_rank'
]
{}

function meta::external::store::relational::sqlDialectTranslation::functionRegistry::extensionFunctions::window::averageRank(): SqlFunction[1]
{
  ^meta::external::store::relational::sqlDialectTranslation::functionRegistry::extensionFunctions::window::AverageRank
  (
    name = 'average_rank',
    documentation = 'Returns the average rank of the current row, assigning mean rank for tied rows.',
    variations = [
      ^SqlFunctionVariation
      (
        parameterTypes = [],
        returnType = AbstractNumericSqlType
      )
    ],
    tests = [
      sqlFunctionTest(
        testSetupDataForWindowFunctions(),
        // SELECT *, average_rank() OVER (PARTITION BY dept ORDER BY score DESC NULLS LAST) AS result FROM testTable ORDER BY result ASC NULLS LAST, student ASC NULLS LAST
        windowFunctionCall(
          ^FunctionCall(
            name = ^QualifiedName(parts = 'average_rank'), 
            arguments = [],
            window = ^meta::external::query::sql::metamodel::Window(
              orderBy = [
                ^SortItem(
                  sortKey = ^QualifiedNameReference(name = ^QualifiedName(parts = 'score')),
                  ordering = SortItemOrdering.DESCENDING, 
                  nullOrdering = SortItemNullOrdering.LAST
                )
              ],
              partitions = [^QualifiedNameReference(name = ^QualifiedName(parts = 'dept'))]
            )
          ),
          'testTable',
          'student'
        ),        
        ^TestExpectedResult(
          columns = ['student', 'dept', 'score', 'result'], 
          data = [
            list(['Alice', 'Science', 90, 1]), 
            list(['Eve', 'Arts', 88, 1]),
            list(['Bob', 'Science', 88, 2]),
            list(['Frank', 'Arts', 85, 2]),
            list(['Charlie', 'Science', 85, 3]),
            list(['Grace', 'Arts', 84, 3]),
            list(['David', 'Science', 80, 4]),
            list(['Hannah', 'Arts', 80, 4])
          ]
        )
      )
    ]
  )
}
