// Copyright 2024 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::functions::collection::*;
import meta::external::query::sql::metamodel::*;
import meta::external::store::relational::sqlDialectTranslation::*;
import meta::external::store::relational::sqlDialectTranslation::utils::*;
import meta::external::store::relational::sqlDialectTranslation::functionRegistry::*;
import meta::external::store::relational::sqlDialectTranslation::sqlTyping::sqlTypes::*;
import meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::*;
import meta::external::query::sql::metamodel::extension::*;

//-------------------------------------------------------------------------------------------------------------------------------------------------

Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedSqlExpression extends meta::external::query::sql::metamodel::Expression
{
  sqlType: Class<SqlType>[1];
}

Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedComparisonExpression extends ComparisonExpression, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedLogicalBinaryExpression extends LogicalBinaryExpression, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedNotExpression extends NotExpression, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedIsNullPredicate extends IsNullPredicate, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedIsNotNullPredicate extends IsNotNullPredicate, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedNegativeExpression extends NegativeExpression, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedCastExpression extends Cast, TypedSqlExpression {}

Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedIntegerLiteral extends IntegerLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedLongLiteral extends LongLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedDoubleLiteral extends DoubleLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedDecimalLiteral extends DecimalLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedBooleanLiteral extends BooleanLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedStringLiteral extends StringLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedDateLiteral extends DateLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedTimestampLiteral extends TimestampLiteral, TypedSqlExpression {}
Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedNullLiteral extends NullLiteral, TypedSqlExpression {}

Class meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::TypedFunctionCall extends FunctionCall, TypedSqlExpression
{
  sqlFunction: SqlFunction[1];
}

//-------------------------------------------------------------------------------------------------------------------------------------------------

function meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::inferTypes(query: Query[1], functionRegistry: Map<Class<SqlFunction>, SqlFunction>[1]): Query[1]
{
  let castedFunctionRegistry = $functionRegistry->values()->map(sqlFunc | pair($sqlFunc.name->toOne()->toLower(), $sqlFunc))->newMap();
  $query->inferTypes(typeInferenceTransformFunction($castedFunctionRegistry))->cast(@Query);
}

function meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::inferTypes(expr: meta::external::query::sql::metamodel::Expression[1], functionRegistry: Map<Class<SqlFunction>, SqlFunction>[1]): meta::external::query::sql::metamodel::Expression[1]
{
  let castedFunctionRegistry = $functionRegistry->values()->map(sqlFunc | pair($sqlFunc.name->toOne(), $sqlFunc))->newMap();
  $expr->inferTypes(typeInferenceTransformFunction($castedFunctionRegistry))->cast(@meta::external::query::sql::metamodel::Expression);
}

function <<access.private>> meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::inferTypes(node: Node[1], transformFunction: Function<{Node[1]->Node[1]}>[1]): Node[1]
{
  postTraversalClassTransform(
    $node,
    {node:Any[1], keyValues:KeyValue[*]|
      let nodeType = $node->type()->cast(@Class<Any>);
      let newNode = $nodeType->dynamicNew($keyValues);
      $newNode->match([n: Node[1] | $transformFunction->eval($newNode), a: Any[1] | $a]);
    }
  )->cast(@Node)->toOne()
}

function meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::typeInferenceTransformFunction(functionRegistry: Map<String, SqlFunction>[1]): Function<{Node[1]->Node[1]}>[1]
{
  {node: Node[1] |
    $node->match([
      e: ComparisonExpression[1] | $e->typedExpression(TypedComparisonExpression, BooleanSqlType),
      e: LogicalBinaryExpression[1] | $e->typedExpression(TypedLogicalBinaryExpression, BooleanSqlType),
      e: NotExpression[1] | $e->typedExpression(TypedNotExpression, BooleanSqlType),
      e: IsNullPredicate[1] | $e->typedExpression(TypedIsNullPredicate, BooleanSqlType),
      e: IsNotNullPredicate[1] | $e->typedExpression(TypedIsNotNullPredicate, BooleanSqlType),
      e: NegativeExpression[1] | $e->typedExpression(TypedNegativeExpression, $e.value->match([t :TypedSqlExpression[1] | $t.sqlType, a: Any[1] | AbstractNumericSqlType])),
      e: Cast[1] | $e->typedCast(),
      l: IntegerLiteral[1] | $l->typedExpression(TypedIntegerLiteral, IntegerSqlType),
      l: LongLiteral[1] | $l->typedExpression(TypedLongLiteral, BigIntSqlType),
      l: DoubleLiteral[1] | $l->typedExpression(TypedDoubleLiteral, DoubleSqlType),
      l: BooleanLiteral[1] | $l->typedExpression(TypedBooleanLiteral, BooleanSqlType),
      l: StringLiteral[1] | $l->typedExpression(TypedStringLiteral, VarcharSqlType),
      l: NullLiteral[1] | $l->typedExpression(TypedNullLiteral, SqlType),
      l: DateLiteral[1] | $l->typedExpression(TypedDateLiteral, DateSqlType),
      l: DecimalLiteral[1] | $l->typedExpression(TypedDecimalLiteral, NumericSqlType),
      l: TimestampLiteral[1] | $l->typedExpression(TypedTimestampLiteral, TimestampSqlType),
      f: FunctionCall[1] | $f->typedFunctionCall($functionRegistry),
      n: Node[1] | $n 
    ])
  }
}

function <<access.private>> meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::typedCast(c: Cast[1]): TypedSqlExpression[1]
{
  let targetType = $c.type.name;
  let sqlTypeMap = [
    
    // Integer types
    pair('int2', SmallIntSqlType),
    pair('smallint', SmallIntSqlType),

    pair('int', IntegerSqlType),
    pair('int4', IntegerSqlType),
    pair('integer', IntegerSqlType),

    pair('int8', BigIntSqlType),
    pair('bigint', BigIntSqlType),

    // Decimal types
    pair('numeric', NumericSqlType),
    pair('decimal', DecimalSqlType),
    pair('float', FloatSqlType),
    pair('float4', FloatSqlType),
    pair('real', RealSqlType),
    pair('double precision', DoubleSqlType),
    pair('float8', DoubleSqlType),

    // Boolean types
    pair('boolean', BooleanSqlType),
    pair('bool', BooleanSqlType),

    // String types
    pair('varchar', VarcharSqlType),
    pair('character varying', VarcharSqlType),

    pair('char', CharSqlType),
    pair('character', CharSqlType),

    // Temporal types
    pair('date', DateSqlType),
    pair('timestamp', TimestampSqlType)

    // TODO: More types
  ]->newMap();
  let targetTypeWithoutArgs = $targetType->toLower()->split('(')->at(0);
  $c->typedExpression(TypedCastExpression, $sqlTypeMap->get($targetTypeWithoutArgs)->toOne('Failed to infer Postgres type while processing Cast expression to target type \'' + $targetType + '\'.'));
}

function <<access.private>> meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::typedFunctionCall(f: FunctionCall[1], functionRegistry: Map<String, SqlFunction>[1]): TypedSqlExpression[1]
{
  let funcName = $f.name.parts->joinStrings('.')->toLower();
  let sqlFunction = $functionRegistry->get($funcName)->toOne('Function registry does not contain function - ' + $funcName);
  let funcArgs = $f.arguments;
  if ($funcArgs->forAll(x | $x->instanceOf(TypedSqlExpression)),
    | // All args are typed. So, infer function based on types
      let funcArgsSqlTypes = $funcArgs->map(a | $a->cast(@TypedSqlExpression).sqlType);
      let firstMatchedVariation = $sqlFunction.variations->fold({v, res |
        if ($res->isEmpty(),
          | let variationParameterTypes = if($v->instanceOf(VariadicSqlFunctionVariation),
              | let argCountDiff = $funcArgsSqlTypes->size() - $v.parameterTypes->size();
                if($argCountDiff >= 0, |$v.parameterTypes->init()->concatenate($v.parameterTypes->last()->toOne()->repeat($argCountDiff + 1)), |$v.parameterTypes);,
              | $v.parameterTypes
            );
            if (($variationParameterTypes->size() == $funcArgsSqlTypes->size()) && $funcArgsSqlTypes->zip($variationParameterTypes)->forAll(x | $x.first->_subTypeOf($x.second) || $x.first == SqlType),
              | $v,
              | $res
            );,
          | $res
        )}, 
        []
      );
      assert(
        $firstMatchedVariation->isNotEmpty(),
        | 'No matched function in function registry for - ' + $funcName + '(' + $funcArgsSqlTypes->map(x | $x.name)->joinStrings(', ') + ').\n' +
          'Available variations for that function - ' + $sqlFunction.variations->map(v | '  ' + $funcName + $v.identifier)->joinStrings('[\n', ',\n', '\n]')
      );
      $f->typedExpression(TypedFunctionCall, $firstMatchedVariation->toOne().returnType, $sqlFunction);,
      
    | // Not all args are typed. So, try to find out common return type. Assumes last function variation is most generic.
      let allReturnTypes = $sqlFunction.variations->filter(v|($v->instanceOf(VariadicSqlFunctionVariation) && ($v.parameterTypes->size() <= $funcArgs->size())) || $v.parameterTypes->size() == $funcArgs->size()).returnType;
      assert(
        $allReturnTypes->isNotEmpty(),
        | 'No matched function in function registry for - ' + $funcName + '(' + '<UNKNOWN_TYPE>'->repeat($funcArgs->size())->joinStrings(', ') + ').\n' +
          'Available variations for that function - ' + $sqlFunction.variations->map(v | '  ' + $funcName + $v.identifier)->joinStrings('[\n', ',\n', '\n]')
      );
      let lastReturnType = $allReturnTypes->last()->toOne();
      assert($allReturnTypes->forAll(r | $r->_subTypeOf($lastReturnType)), | 'No common return type for variation of function - ' + $funcName);
      $f->typedExpression(TypedFunctionCall, $lastReturnType, $sqlFunction);
  );
}

function <<access.private>> meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::typedExpression(expr: meta::external::query::sql::metamodel::Expression[1], cls: Class<TypedSqlExpression>[1], inferredType: Class<SqlType>[1]): TypedSqlExpression[1]
{
  $expr->typedExpression($cls, $inferredType, [])
}

function <<access.private>> meta::external::store::relational::sqlDialectTranslation::sqlTyping::typeInference::typedExpression(expr: meta::external::query::sql::metamodel::Expression[1], cls: Class<TypedSqlExpression>[1], inferredType: Class<SqlType>[1], sqlFunction: SqlFunction[0..1]): TypedSqlExpression[1]
{
  let exprCls = $expr->type()->cast(@Class<Any>);
  assert($cls->_subTypeOf($exprCls), | 'TypedExpression class ' + $cls->elementToPath() + ' is not a subtype of ' + $exprCls->elementToPath());
  let allProperties = $exprCls->hierarchicalAllProperties()->filter(x | $x->instanceOf(Property))->cast(@Property<Nil,Any|*>);
  let propKeyValues = $allProperties->map(p | ^KeyValue(key = $p.name->toOne(), value = $p->evaluate(list($expr))));
  let typeKeyValue = ^KeyValue(key = 'sqlType', value = $inferredType);
  let sqlFunctionKeyValue = if ($cls == TypedFunctionCall, | ^KeyValue(key = 'sqlFunction', value = $sqlFunction->toOne()), | []);
  $cls->dynamicNew($propKeyValues->concatenate($typeKeyValue)->concatenate($sqlFunctionKeyValue))->cast(@TypedSqlExpression)->toOne();
}