// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::relational::metamodel::*;
import meta::external::query::sql::metamodel::*;
import meta::external::store::relational::sqlDialectTranslation::*;
import meta::external::store::relational::sqlDialectTranslation::functionRegistry::*;
import meta::external::store::relational::sqlDialectTranslation::sqlTyping::sqlTypes::*;
import meta::external::store::relational::sqlDialectTranslation::utils::*;
import meta::external::store::relational::sqlDialectTranslation::tests::utils::*;

Class meta::external::store::relational::sqlDialectTranslation::functionRegistry::postgresNativeFunctions::string::aggregate::StringAgg extends PostgresNativeSqlFunction
[
  $this.name == 'string_agg'
]
{}

function meta::external::store::relational::sqlDialectTranslation::functionRegistry::postgresNativeFunctions::string::aggregate::stringAgg(): SqlFunction[1]
{
  ^meta::external::store::relational::sqlDialectTranslation::functionRegistry::postgresNativeFunctions::string::aggregate::StringAgg
  (
    name = 'string_agg',
    documentation = 'Aggregate function that concatenates the non-null input values into a single string.',
    variations = [
      ^SqlFunctionVariation
      (
        parameterTypes = [AbstractStringSqlType, AbstractStringSqlType],
        returnType = AbstractStringSqlType
      )
    ],
    tests = [
      sqlFunctionTest(
        ^TestSetupData(
          tableName = 'testTable', 
          columns = [pair('age', 'integer'), pair('name', 'varchar')], 
          data = [
            dataRow([18, 'Anthony']),
            dataRow([18, 'John']), 
            dataRow([20, 'Fabrice']),
            dataRow([30, ^NullLiteral()]), 
            dataRow([^NullLiteral(), 'Oliver'])
          ]
        ),
        // SELECT age, string_agg(name, ',') AS result FROM testTable GROUP BY age ORDER BY result ASC NULLS LAST
        simpleAggFunctionCall(
          ^FunctionCall(
            name = ^QualifiedName(parts = 'string_agg'), 
            arguments = [^QualifiedNameReference(name = ^QualifiedName(parts = 'name')), ^StringLiteral(value = ',')]
          ),
          'testTable',
          'age'
        ),
        ^TestExpectedResult(
          columns = ['age', 'result'], 
          data = [
            list([18, 'Anthony,John']),
            list([20, 'Fabrice']),
            list([null(), 'Oliver']),
            list([30, null()])
          ]
        )
      )
    ]
  )
}