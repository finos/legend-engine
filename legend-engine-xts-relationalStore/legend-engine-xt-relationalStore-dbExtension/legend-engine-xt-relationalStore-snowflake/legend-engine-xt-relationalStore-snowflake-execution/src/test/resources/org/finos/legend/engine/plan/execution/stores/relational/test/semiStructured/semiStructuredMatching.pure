// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Pure
Class match::model::Order
{
  orderId: Integer[1];
  orderName: String[1];
  customer: match::model::Customer[1];
}

Class match::model::Customer
{
  customerId: String[1];
  customerAddress: match::model::CustomerAddress[1];
  transactionDetails: match::model::TransactionDetails[1];
}

Class match::model::CustomerAddress
{
  name: String[1];
}

Class match::model::BillingAddress extends match::model::CustomerAddress
{
  billAddress: String[1];
}

Class match::model::ShippingAddress extends match::model::CustomerAddress
{
  shipAddress: String[1];
}

Class match::model::TransactionDetails
{
  payment: match::model::Payment[1];
}

Class match::model::Payment
{
  paymentId: String[1];
}

Class match::model::CashOnDeliveryPayment extends match::model::Payment
{
  amountToBePaid: Integer[1];
}

Class match::model::PrepaidPayment extends match::model::Payment
{
  amountPaid: Integer[1];
}

Class match::model::WalletPrepaidPayment extends match::model::PrepaidPayment
{
  walletTransactionAmount: Integer[1];
}

Class match::model::CardPrepaidPayment extends match::model::PrepaidPayment
{
  cardTransactionAmount: Integer[1];
}

###Relational
Database match::store::SnowflakeDB
(
  Schema ORDER_SCHEMA
  (
    Table ORDER_TABLE
    (
      ORDERID INTEGER PRIMARY KEY,
      ORDERNAME VARCHAR(100),
      CUSTOMER SEMISTRUCTURED
    )
  )
)

###ExternalFormat
Binding match::store::CustomerBinding
{
  contentType: 'application/json';
  modelIncludes: [
    match::model::Customer,
    match::model::CustomerAddress,
    match::model::BillingAddress,
    match::model::ShippingAddress,
    match::model::TransactionDetails,
    match::model::Payment,
    match::model::CashOnDeliveryPayment,
    match::model::PrepaidPayment,
    match::model::WalletPrepaidPayment,
    match::model::CardPrepaidPayment
  ];
}

SchemaSet match::schema::Customer_v07
{
  format: JSON;
  schemas: [
    {
      id: Customer;
      location: 'Customer.json';
      content: '{\n  "$schema": "http://json-schema.org/draft-07/schema#",\n  "title": "match::model::Customer",\n  "type": "object",\n  "properties": {\n    "customerId": {\n      "type": "string"\n    },\n    "customerAddress": {\n      "oneOf": [\n        {\n          "$ref": "#/definitions/match::model::BillingAddress"\n        },\n        {\n          "$ref": "#/definitions/match::model::CustomerAddress"\n        },\n        {\n          "$ref": "#/definitions/match::model::ShippingAddress"\n        }\n      ]\n    },\n    "transactionDetails": {\n      "$ref": "#/definitions/match::model::TransactionDetails"\n    }\n  },\n  "required": [\n    "customerId",\n    "customerAddress",\n    "transactionDetails"\n  ],\n  "definitions": {\n    "match::model::BillingAddress": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::BillingAddress",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::CustomerAddress"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "billAddress": {\n          "type": "string"\n        }\n      },\n      "required": [\n        "billAddress"\n      ]\n    },\n    "match::model::CardPrepaidPayment": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::CardPrepaidPayment",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::PrepaidPayment"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "cardTransactionAmount": {\n          "type": "integer"\n        }\n      },\n      "required": [\n        "cardTransactionAmount"\n      ]\n    },\n    "match::model::CashOnDeliveryPayment": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::CashOnDeliveryPayment",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::Payment"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "amountToBePaid": {\n          "type": "integer"\n        }\n      },\n      "required": [\n        "amountToBePaid"\n      ]\n    },\n    "match::model::CustomerAddress": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::CustomerAddress",\n      "type": "object",\n      "properties": {\n        "name": {\n          "type": "string"\n        }\n      },\n      "required": [\n        "name"\n      ]\n    },\n    "match::model::Payment": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::Payment",\n      "type": "object",\n      "properties": {\n        "paymentId": {\n          "type": "string"\n        }\n      },\n      "required": [\n        "paymentId"\n      ]\n    },\n    "match::model::PrepaidPayment": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::PrepaidPayment",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::Payment"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "amountPaid": {\n          "type": "integer"\n        }\n      },\n      "required": [\n        "amountPaid"\n      ]\n    },\n    "match::model::ShippingAddress": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::ShippingAddress",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::CustomerAddress"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "shipAddress": {\n          "type": "string"\n        }\n      },\n      "required": [\n        "shipAddress"\n      ]\n    },\n    "match::model::TransactionDetails": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::TransactionDetails",\n      "type": "object",\n      "properties": {\n        "payment": {\n          "oneOf": [\n            {\n              "$ref": "#/definitions/match::model::CardPrepaidPayment"\n            },\n            {\n              "$ref": "#/definitions/match::model::CashOnDeliveryPayment"\n            },\n            {\n              "$ref": "#/definitions/match::model::Payment"\n            },\n            {\n              "$ref": "#/definitions/match::model::PrepaidPayment"\n            },\n            {\n              "$ref": "#/definitions/match::model::WalletPrepaidPayment"\n            }\n          ]\n        }\n      },\n      "required": [\n        "payment"\n      ]\n    },\n    "match::model::WalletPrepaidPayment": {\n      "$schema": "http://json-schema.org/draft-07/schema#",\n      "title": "match::model::WalletPrepaidPayment",\n      "allOf": [\n        {\n          "$ref": "#/definitions/match::model::PrepaidPayment"\n        }\n      ],\n      "type": "object",\n      "properties": {\n        "walletTransactionAmount": {\n          "type": "integer"\n        }\n      },\n      "required": [\n        "walletTransactionAmount"\n      ]\n    }\n  }\n}';
    }
  ];
}

SchemaSet match::schema::Customer_v_LegendJson
{
  format: JSON;
  schemas: [
    {
      id: Customer;
      location: 'Customer.json';
      content: '{\n  "$schema": "http://legend.finos.org/json-schema/v1/schema#",\n  "schemaConfig": {\n    "typeConfig": {\n    "typeKeyName": "_type",\n    "fullyQualifiedTypePath": true,\n    "fullyQualifiedTypeNameOverride": {\n      "match::model::PrepaidPayment": "Prepaid",\n      "match::model::WalletPrepaidPayment": "Wallet"\n     }\n  }\n    },\n  "innerSchema": {\n    "$schema": "http://json-schema.org/draft-07/schema#",\n    "title": "match::model::Customer",\n    "type": "object",\n    "properties": {\n      "customerId": {\n        "type": "string"\n      },\n      "customerAddress": {\n        "oneOf": [\n          {\n            "$ref": "#/definitions/match::model::BillingAddress"\n          },\n          {\n            "$ref": "#/definitions/match::model::CustomerAddress"\n          },\n          {\n            "$ref": "#/definitions/match::model::ShippingAddress"\n          }\n        ]\n      },\n      "transactionDetails": {\n        "$ref": "#/definitions/match::model::TransactionDetails"\n      }\n    },\n    "required": [\n      "customerId",\n      "customerAddress",\n      "transactionDetails"\n    ],\n    "definitions": {\n      "match::model::BillingAddress": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::BillingAddress",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::CustomerAddress"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "billAddress": {\n            "type": "string"\n          }\n        },\n        "required": [\n          "billAddress"\n        ]\n      },\n      "match::model::CardPrepaidPayment": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::CardPrepaidPayment",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::PrepaidPayment"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "cardTransactionAmount": {\n            "type": "integer"\n          }\n        },\n        "required": [\n          "cardTransactionAmount"\n        ]\n      },\n      "match::model::CashOnDeliveryPayment": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::CashOnDeliveryPayment",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::Payment"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "amountToBePaid": {\n            "type": "integer"\n          }\n        },\n        "required": [\n          "amountToBePaid"\n        ]\n      },\n      "match::model::CustomerAddress": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::CustomerAddress",\n        "type": "object",\n        "properties": {\n          "name": {\n            "type": "string"\n          }\n        },\n        "required": [\n          "name"\n        ]\n      },\n      "match::model::Payment": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::Payment",\n        "type": "object",\n        "properties": {\n          "paymentId": {\n            "type": "string"\n          }\n        },\n        "required": [\n          "paymentId"\n        ]\n      },\n      "match::model::PrepaidPayment": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::PrepaidPayment",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::Payment"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "amountPaid": {\n            "type": "integer"\n          }\n        },\n        "required": [\n          "amountPaid"\n        ]\n      },\n      "match::model::ShippingAddress": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::ShippingAddress",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::CustomerAddress"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "shipAddress": {\n            "type": "string"\n          }\n        },\n        "required": [\n          "shipAddress"\n        ]\n      },\n      "match::model::TransactionDetails": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::TransactionDetails",\n        "type": "object",\n        "properties": {\n          "payment": {\n            "oneOf": [\n              {\n                "$ref": "#/definitions/match::model::CardPrepaidPayment"\n              },\n              {\n                "$ref": "#/definitions/match::model::CashOnDeliveryPayment"\n              },\n              {\n                "$ref": "#/definitions/match::model::Payment"\n              },\n              {\n                "$ref": "#/definitions/match::model::PrepaidPayment"\n              },\n              {\n                "$ref": "#/definitions/match::model::WalletPrepaidPayment"\n              }\n            ]\n          }\n        },\n        "required": [\n          "payment"\n        ]\n      },\n      "match::model::WalletPrepaidPayment": {\n        "$schema": "http://json-schema.org/draft-07/schema#",\n        "title": "match::model::WalletPrepaidPayment",\n        "allOf": [\n          {\n            "$ref": "#/definitions/match::model::PrepaidPayment"\n          }\n        ],\n        "type": "object",\n        "properties": {\n          "walletTransactionAmount": {\n            "type": "integer"\n          }\n        },\n        "required": [\n          "walletTransactionAmount"\n        ]\n      }\n    }\n  }\n}';
    }
  ];
}

SchemaSet match::schema::Customer_v_MinimalLegendJson
{
  format: JSON;
  schemas: [
    {
      id: Customer;
      location: 'Customer.json';
      content: '{\n  "$schema": "http://legend.finos.org/json-schema/v1/schema#",\n  "schemaConfig": {\n    "typeConfig": {\n    "typeKeyName": "_type",\n    "fullyQualifiedTypePath": false\n  }\n } }';
    }
  ];
}

Binding match::store::CustomerBindingWithLegendJsonSchema
{
  schemaSet: match::schema::Customer_v_LegendJson;
  schemaId: Customer;
  contentType: 'application/json';
  modelIncludes: [
    match::model::Customer,
    match::model::CashOnDeliveryPayment,
    match::model::TransactionDetails,
    match::model::CustomerAddress,
    match::model::CardPrepaidPayment,
    match::model::WalletPrepaidPayment,
    match::model::PrepaidPayment,
    match::model::Payment,
    match::model::ShippingAddress,
    match::model::BillingAddress
  ];
}

Binding match::store::CustomerBindingWithMinimalLegendJsonSchema
{
  schemaSet: match::schema::Customer_v_MinimalLegendJson;
  schemaId: Customer;
  contentType: 'application/json';
  modelIncludes: [
    match::model::Customer
  ];
}

###Mapping
Mapping match::mapping::SnowflakeMapping
(
  match::model::Order: Relational
  {
    ~primaryKey
    (
      [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID
    )
    ~mainTable [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE
    orderId: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID,
    orderName: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERNAME,
    customer: Binding match::store::CustomerBinding : [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.CUSTOMER
  }
)

Mapping match::mapping::SnowflakeMappingWithLegendJsonSchema
(
  match::model::Order: Relational
  {
    ~primaryKey
    (
      [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID
    )
    ~mainTable [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE
    orderId: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID,
    orderName: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERNAME,
    customer: Binding match::store::CustomerBindingWithLegendJsonSchema : [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.CUSTOMER
  }
)

Mapping match::mapping::SnowflakeMappingWithMinimalLegendJsonSchema
(
  match::model::Order: Relational
  {
    ~primaryKey
    (
      [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID
    )
    ~mainTable [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE
    orderId: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERID,
    orderName: [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.ORDERNAME,
    customer: Binding match::store::CustomerBindingWithMinimalLegendJsonSchema : [match::store::SnowflakeDB]ORDER_SCHEMA.ORDER_TABLE.CUSTOMER
  }
)

###Runtime
Runtime match::runtime::SnowflakeRuntime
{
    mappings :
    [
        match::mapping::SnowflakeMapping
    ];
    connections :
    [
        match::store::SnowflakeDB :
        [
            connection_1 : #{
                RelationalDatabaseConnection {
                    store: match::store::SnowflakeDB;
                    type: Snowflake;
                    specification: Snowflake
                    {
                        name: 'dbName';
                        account: 'account';
                        warehouse: 'warehouse';
                        region: 'region';
                    };
                    auth: Test;
                }
            }#
        ]
    ];
}

###Pure
function match::semiStructuredMatchComplexProperty(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.customerAddress->match(
                [
                    s: match::model::BillingAddress[1] | $s.billAddress,
                    s: match::model::ShippingAddress[1] | $s.shipAddress,
                    s: Any[1] | 'Default Address'
                ]
            )
        ],
        [
            'Customer Address'
        ]
    );
}

function match::semiStructuredMatchWithMultipleProject(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.customerAddress->match(
                [
                    s: match::model::BillingAddress[1] | $s.billAddress,
                    s: match::model::ShippingAddress[1] | $s.shipAddress
                ]
            ),
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
                    s:match::model::PrepaidPayment[1] | $s.amountPaid
                ]
            )
        ],
        [
            'Customer Address',
            'Order Price'
        ]
    );
}

function match::semiStructuredMatchWithComplexFilter(): TabularDataSet[1]
{
    match::model::Order.all()
        ->filter(
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
                    s:match::model::PrepaidPayment[1] | $s.amountPaid
                ]
            )<200
        )
        ->project(
            [
                x | $x.customer.customerAddress->match(
                    [
                        s: match::model::BillingAddress[1] | $s.billAddress,
                        s: match::model::ShippingAddress[1] | $s.shipAddress
                    ]
                )
            ],
            [
                'Customer Address'
            ]
        );
}

function match::semiStructuredMatchWithVariableAccess(): TabularDataSet[1]
{
    let maxAmount = 200;
    match::model::Order.all()->project(
        [
            x | $x.customer.transactionDetails.payment->match(
                [
                    s:match::model::CashOnDeliveryPayment[1] |
                        if($s.amountToBePaid < $maxAmount,
                            |true,
                            |false
                          ),
                    s:match::model::PrepaidPayment[1]  |
                        if($s.amountPaid  < $maxAmount,
                            |true,
                            |false
                          )
                ]
            )
        ],
        [
            'Max Amount Flag'
        ]
    );

}

function match::semiStructuredMatchMultilevel(): TabularDataSet[1]
{
    match::model::Order.all()->project(
        [
            x | $x.customer.transactionDetails.payment->match(
                [
                    s: match::model::PrepaidPayment[1] | $s->match(
                        [
                            k:match::model::WalletPrepaidPayment[1] | $k.walletTransactionAmount ,
                            k:match::model::CardPrepaidPayment[1]  | $k.cardTransactionAmount,
                            k:match::model::PrepaidPayment[1] | $k.amountPaid
                        ]
                    ),
                    s: match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid
                ]
            )
        ],
        [
            'Amount'
        ]
    );
}

function match::semiStructuredMatchWithMultipleProjectUsingCol(): TabularDataSet[1]
{
    match::model::Order.all()->project([
        col(x | $x.customer.customerAddress->match(
            [
                s: match::model::BillingAddress[1] | $s.billAddress,
                s: match::model::ShippingAddress[1] | $s.shipAddress
            ]),'Customer Address'),
        col(x | $x.customer.transactionDetails.payment->match(
            [
               s:match::model::CashOnDeliveryPayment[1] | $s.amountToBePaid,
               s:match::model::PrepaidPayment[1] | $s.amountPaid
            ]),'Order Price')
    ]);
}