// Copyright 2025 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::constant::datatype::primitive::*;
import meta::protocols::pure::vX_X_X::metamodel::m3::valuespecification::constant::*;
import meta::relational::functions::typeConversion::duckDB::*;

function meta::relational::functions::typeConversion::duckDB::convert(genericType:GenericType[1]):meta::relational::metamodel::datatype::DataType[1]
{
    let path = $genericType.rawType.name->toOne();
    let variables = $genericType.typeVariableValues;
    if (
        [
            pair(|$path == 'Boolean', |^meta::relational::metamodel::datatype::Boolean()),
            pair(|$path == 'Integer', |^meta::relational::metamodel::datatype::BigInt()),
            pair(|$path == 'Float', |^meta::relational::metamodel::datatype::Double()),
            pair(|$path == 'Byte', |^meta::relational::metamodel::datatype::Binary(size=10000)),
            // pair(|$path == 'StrictTime', |fail('not supported yet!')),
            pair(|$path == 'Decimal', |^meta::relational::metamodel::datatype::Decimal(precision=10, scale=10)),
            pair(|$path == 'String', |^meta::relational::metamodel::datatype::Varchar(size=1024)),
            pair(|$path == 'DateTime', |^meta::relational::metamodel::datatype::Timestamp()),
            pair(|$path == 'Number', |^meta::relational::metamodel::datatype::Decimal(precision=10, scale=10)),
            pair(|$path == 'Date', |^meta::relational::metamodel::datatype::Timestamp()),

            pair(|$path == 'BigInt', |^meta::relational::metamodel::datatype::BigInt()),
            pair(|$path == 'StrictDate', |^meta::relational::metamodel::datatype::Date()),
            pair(|$path == 'Numeric', |^meta::relational::metamodel::datatype::Decimal(precision=$variables->at(0)->cast(@InstanceValue)->evaluateAndDeactivate().values->toOne()->cast(@Integer), scale=$variables->at(1)->cast(@InstanceValue)->evaluateAndDeactivate().values->toOne()->cast(@Integer))),
            pair(|$path == 'Double', |^meta::relational::metamodel::datatype::Double()),
            pair(|$path == 'Float4', |^meta::relational::metamodel::datatype::Float()),
            pair(|$path == 'Int', |^meta::relational::metamodel::datatype::Integer()),
            pair(|$path == 'SmallInt', |^meta::relational::metamodel::datatype::SmallInt()),
            // pair(|$path == 'Time', |fail('not supported yet!')),
            pair(|$path == 'Timestamp', |^meta::relational::metamodel::datatype::Timestamp()),
            pair(|$path == 'TinyInt', |^meta::relational::metamodel::datatype::TinyInt()),
            // pair(|$path == 'UBigInt', |fail('not supported yet!')),
            // pair(|$path == 'UInt', |fail('not supported yet!')),
            // pair(|$path == 'USmallInt', |fail('not supported yet!')),
            // pair(|$path == 'UTinyInt', |fail('not supported yet!')),
            pair(|$path == 'Varchar', |^meta::relational::metamodel::datatype::Varchar(size=$variables->cast(@InstanceValue)->evaluateAndDeactivate().values->toOne()->cast(@Integer))),
            pair(|$path == 'Variant', |^meta::relational::metamodel::datatype::Json())
        ],
        | fail ($path + ' is not managed yet');
    )->cast(@meta::relational::metamodel::datatype::DataType);
}

function {doc.doc='Given the platform (ie DB agnostic) data types, convert to DB specific type'} meta::relational::functions::typeConversion::duckDB::dataTypeToSqlTextDuckDB(type: meta::relational::metamodel::datatype::DataType[1]):String[1]
{
    $type->match([
        b : meta::relational::metamodel::datatype::Boolean[1] | 'BOOL',
        i : meta::relational::metamodel::datatype::Integer[1] | 'INT',
        f : meta::relational::metamodel::datatype::Float[1] | 'FLOAT',
        v : meta::relational::metamodel::datatype::Varchar[1] | format('VARCHAR(%d)', $v.size),
        c : meta::relational::metamodel::datatype::Char[1] | format('CHAR(%d)', $c.size),
        d : meta::relational::metamodel::datatype::Decimal[1] | format('DECIMAL(%d, %d)', [$d.precision, $d.scale]),
        t : meta::relational::metamodel::datatype::Timestamp[1] | 'TIMESTAMP',
        d : meta::relational::metamodel::datatype::Date[1] | 'DATE',
        b : meta::relational::metamodel::datatype::BigInt[1] | 'BIGINT',
        s : meta::relational::metamodel::datatype::SmallInt[1] | 'SMALLINT',
        t : meta::relational::metamodel::datatype::TinyInt[1] | 'TINYINT',
        d : meta::relational::metamodel::datatype::Double[1] | 'DOUBLE',
        n : meta::relational::metamodel::datatype::Numeric[1] | format('NUMERIC(%d, %d)', [$n.precision, $n.scale]),
        a : meta::relational::metamodel::datatype::Array[1] | if($a.type->isEmpty(), |'JSON[]', |$a.type->toOne()->dataTypeToSqlTextDuckDB() + '[]'),
        o : meta::relational::metamodel::datatype::Object[1] | 'MAP(%s, %s)'->format([$o.keyType->dataTypeToSqlTextDuckDB(), $o.valueType->dataTypeToSqlTextDuckDB()]),
        s : meta::relational::metamodel::datatype::SemiStructured[1] | 'JSON',
        s : meta::relational::metamodel::datatype::Json[1] | 'JSON'
    ]);
}
