// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::relational::functions::sqlstring::*;

Class meta::relational::memsql::tests::db::A
{
  id: Integer[1];
  window : Integer[1];
}

function <<test.Test>> meta::relational::memsql::tests::db::testQuoteIdentifiers() : Boolean[1]
{
  let result = toSQLString(
    | meta::relational::memsql::tests::db::A.all()->project([s | $s.window], ['WINDOW']),
      meta::relational::memsql::tests::db::simpleRelationalMappingForMemSQLWindowKeyword,
      meta::relational::runtime::DatabaseType.MemSQL,
      true,
      meta::relational::extension::relationalExtensions());

  assertEquals('select `root`.`window` as `WINDOW` from `test table` as `root`', $result);
}

function <<test.Test>> meta::relational::memsql::tests::db::testReservedKeywords() : Boolean[1]
{
  let result = toSQLString(
    | meta::relational::memsql::tests::db::A.all()->project([s | $s.window], ['WINDOW']), 
      meta::relational::memsql::tests::db::simpleRelationalMappingForMemSQLWindowKeyword,
      meta::relational::runtime::DatabaseType.MemSQL, 
      meta::relational::extension::relationalExtensions());

  assertEquals('select `root`.`window` as `WINDOW` from `test table` as `root`', $result);
}

function <<test.Test>> meta::relational::memsql::tests::db::testWeekOfYear() : Boolean[1]
{
    let result = toSQLString(
        |meta::relational::tests::mapping::sqlFunction::model::domain::SqlFunctionDemo.all()->project([s|$s.date->weekOfYear()], ['week']),
        meta::relational::tests::mapping::sqlFunction::model::mapping::testMapping,
        meta::relational::runtime::DatabaseType.MemSQL,
        meta::relational::extension::relationalExtensions());

    assertEquals('select weekofyear(date(`root`.dateTime)) as `week` from dataTable as `root`', $result);
}

###Relational
Database meta::relational::memsql::tests::db::schema1
(
   Table "test table" (ID INT PRIMARY KEY, window INT)
)

###Mapping
Mapping meta::relational::memsql::tests::db::simpleRelationalMappingForMemSQLWindowKeyword
(
   meta::relational::memsql::tests::db::A : Relational
   {
      scope([meta::relational::memsql::tests::db::schema1])
      (
         id : "test table".ID,
         window : "test table".window

      )
   }
)
