import meta::analytics::search::transformation::*;

function meta::analytics::search::transformation::buildDocument(element: PackageableElement[1], config: meta::analytics::search::metamodel::ProjectCoordinates[1]): meta::analytics::search::metamodel::BaseRootDocument[1]
{
  let document = $element->match([
    d: meta::pure::metamodel::dataSpace::DataSpace[1]| $d->meta::analytics::search::transformation::dataspace::buildDocument(),
    s: meta::legend::service::metamodel::Service[1]| $s->meta::analytics::search::transformation::hackLegendServiceTests()->meta::analytics::search::transformation::service::buildDocument(),
    d: meta::pure::metamodel::diagram::Diagram[1]| $d->meta::analytics::search::transformation::diagram::buildDocument(),
    m: meta::pure::mapping::Mapping[1]| $m->meta::analytics::search::transformation::mapping::buildDocument(),
    c: Class<Any>[1]| $c->meta::analytics::search::transformation::class::buildDocument(),
    p: PackageableElement[1]| $p->meta::analytics::search::transformation::packageableelement::buildDocument()
  ]);

  ^$document(
    projectCoordinates = ^meta::analytics::search::metamodel::ProjectCoordinates(groupId = $config.groupId, artifactId = $config.artifactId, versionId = $config.versionId)
  );
}

function meta::analytics::search::transformation::hackLegendServiceTests(s: meta::legend::service::metamodel::Service[1]): meta::legend::service::metamodel::Service[1]
{
  ^$s(
    test = ^meta::legend::service::metamodel::SingleExecutionTest(
      data = '',
      asserts = [
        ^meta::legend::service::metamodel::TestContainer(
          parametersValues = [],
          assert = {r: meta::pure::mapping::Result<Any|1>[1]|true}
        )
      ]
    )
  );
}

// PACKAGEABLEELEMENT DOCUMENT
function meta::analytics::search::transformation::packageableelement::buildDocument(packageableElement: PackageableElement[1]): meta::analytics::search::metamodel::BaseRootDocument[1]
{
  ^meta::analytics::search::metamodel::BaseRootDocument(
    id = $packageableElement->elementToPath(),
    type = meta::analytics::search::metamodel::DocumentType.PackageableElement
  );
}


// CLASS DOCUMENT
function meta::analytics::search::transformation::class::buildDocument(class: Class<Any>[1]): meta::analytics::search::metamodel::class::RootDocument[1]
{
  ^meta::analytics::search::metamodel::class::RootDocument(
    id = $class->elementToPath(),
    package = $class.package->toOne()->elementToPath(),
    name = $class.name->toOne(),
    type = meta::analytics::search::metamodel::DocumentType.Class,
    properties = $class.properties->meta::analytics::search::transformation::class::buildPropertyList()
  );
}

function meta::analytics::search::transformation::class::buildPropertyList(properties: Property<Any,Any|*>[*]): String[*]
{
  $properties->map(p|$p.name->toOne())->removeDuplicates();
}


// DATASPACE DOCUMENT
function meta::analytics::search::transformation::dataspace::buildDocument(dataspace: meta::pure::metamodel::dataSpace::DataSpace[1]): meta::analytics::search::metamodel::dataspace::RootDocument[1]
{
  ^meta::analytics::search::metamodel::dataspace::RootDocument(
    id = $dataspace->elementToPath(),
    package = $dataspace.package->toOne()->elementToPath(),
    name = $dataspace.name->toOne(),
    description = $dataspace.description->toOne(),
    defaultExecutionContext = $dataspace.defaultExecutionContext.name,
    diagrams = $dataspace.featuredDiagrams->map(d|$d->elementToPath()),
    executionContexts = $dataspace.executionContexts->map(c|$c->meta::analytics::search::transformation::dataspace::buildExecutionContextDocument()),
    taxonomies = $dataspace.taggedValues->filter(t|$t.tag.value == 'taxonomyNodes').value,
    type = meta::analytics::search::metamodel::DocumentType.DataSpace
  );
}

function meta::analytics::search::transformation::dataspace::buildExecutionContextDocument(executionContext: meta::pure::metamodel::dataSpace::DataSpaceExecutionContext[1]): meta::analytics::search::metamodel::dataspace::ExecutionContextDocument[1]
{
  ^meta::analytics::search::metamodel::dataspace::ExecutionContextDocument(
    name = $executionContext.name,
    mappingPath = $executionContext.mapping->elementToPath(),
    runtimePath = $executionContext.defaultRuntime->elementToPath(),
    classes = $executionContext.mapping->meta::analytics::search::transformation::dataspace::buildClassDocuments()
  )
}

function meta::analytics::search::transformation::dataspace::buildClassDocuments(mapping: meta::pure::mapping::Mapping[1]): meta::analytics::search::metamodel::class::SimpleClassElement[*]
{
  let analysedMapping = meta::analytics::mapping::modelCoverage::analyze($mapping, true, true);
  $analysedMapping.mappedEntities->map(e|
    ^meta::analytics::search::metamodel::class::SimpleClassElement(
      name = $e.path->split('::')->last()->toOne(),
      package = $e.path,
      properties = $e.properties->map(p|$p.name)
    )
  );
}

// DIAGRAM DOCUMENT
function meta::analytics::search::transformation::diagram::buildDocument(diagram: meta::pure::metamodel::diagram::Diagram[1]): meta::analytics::search::metamodel::diagram::RootDocument[1]
{
  ^meta::analytics::search::metamodel::diagram::RootDocument(
    id = $diagram->elementToPath(),
    package = $diagram.package->toOne()->elementToPath(),
    name = $diagram.name->toOne(),
    type = meta::analytics::search::metamodel::DocumentType.Diagram,
    classes = $diagram->meta::analytics::search::transformation::diagram::buildClassDocuments()
  );
}

function meta::analytics::search::transformation::diagram::buildClassDocuments(diagram: meta::pure::metamodel::diagram::Diagram[1]): meta::analytics::search::metamodel::class::SimpleClassElement[*]
{
  let diagramCoverage = meta::pure::metamodel::diagram::analytics::modelCoverage::getDiagramModelCoverage($diagram);
  $diagramCoverage.classes->map(c|
    ^meta::analytics::search::metamodel::class::SimpleClassElement(
      package = $c->cast(@Class<Any>).package->toOne()->elementToPath(),
      name = $c.name->toOne(),
      properties = $c->cast(@Class<Any>).properties->meta::analytics::search::transformation::class::buildPropertyList()
    );
  );
}

// MAPPING DOCUMENT
function meta::analytics::search::transformation::mapping::buildDocument(mapping: meta::pure::mapping::Mapping[1]): meta::analytics::search::metamodel::mapping::RootDocument[1]
{
  ^meta::analytics::search::metamodel::mapping::RootDocument(
    id = $mapping->elementToPath(),
    package = $mapping.package->toOne()->elementToPath(),
    name = $mapping.name->toOne(),
    type = meta::analytics::search::metamodel::DocumentType.Mapping,
    classes = $mapping->meta::analytics::search::transformation::mapping::buildClassDocuments()
  );
}

function meta::analytics::search::transformation::mapping::buildClassDocuments(mapping: meta::pure::mapping::Mapping[1]): meta::analytics::search::metamodel::class::SimpleClassElement[*]
{
  let classMappings = $mapping.classMappings;
  $classMappings->filter(m|$m.root == true)->map(m|
    ^meta::analytics::search::metamodel::class::SimpleClassElement(
      name = $m.class.name->toOne(),
      package = $m.class.package->toOne()->elementToPath(),
      properties = $m->filter(c | $c->instanceOf(meta::pure::mapping::PropertyMappingsImplementation))->cast(@meta::pure::mapping::PropertyMappingsImplementation)->map(i|$i->meta::pure::mapping::allPropertyMappings())->meta::analytics::search::transformation::mapping::buildPropertyList()
    );
  );
}

function meta::analytics::search::transformation::mapping::buildPropertyList(propertyMappings: meta::pure::mapping::PropertyMapping[*]): String[*]
{
  $propertyMappings->filter(m|$m.property->hasStereotype('generatedmilestoningdateproperty', meta::pure::profiles::milestoning) == false && $m.property->hasStereotype('generatedmilestoningproperty', meta::pure::profiles::milestoning) == false)->map(m|
    $m.property.name->toOne()
  )->removeDuplicates();
}


// SERVICE DOCUMENT
function meta::analytics::search::transformation::service::buildDocument(service: meta::legend::service::metamodel::Service[1]): meta::analytics::search::metamodel::service::RootDocument[1]
{
  ^meta::analytics::search::metamodel::service::RootDocument(
    id = $service.pattern,
    name = $service.pattern,
    type = meta::analytics::search::metamodel::DocumentType.Service,
    owners = $service.owners,
    documentation = $service.documentation,
    executions = $service.execution->meta::analytics::search::transformation::service::buildExecutionDocuments()
  );
}

function meta::analytics::search::transformation::service::buildExecutionDocuments(execution: meta::legend::service::metamodel::Execution[1]): meta::analytics::search::metamodel::service::ExecutionDocument[*]
{
  $execution->match([
    s: meta::legend::service::metamodel::PureSingleExecution[1]|
        ^meta::analytics::search::metamodel::service::ExecutionDocument(
          mapping = $s.mapping->toOne()->elementToPath(),
          classes = $s->meta::analytics::search::transformation::service::buildClassDocuments(),
          databaseColumns = $s.func->meta::analytics::search::transformation::service::buildDatabaseColumnDocuments($s.mapping->toOne(), $s.runtime->toOne())
        ),
    m: meta::legend::service::metamodel::PureMultiExecution[1]|
        $m.executionParameters->map(e|
          ^meta::analytics::search::metamodel::service::ExecutionDocument(
            mapping = $e.mapping->elementToPath(),
            classes = $m->meta::analytics::search::transformation::service::buildClassDocuments(),
            databaseColumns = $m.func->meta::analytics::search::transformation::service::buildDatabaseColumnDocuments($e.mapping, $e.runtime)
          );
        )
  ]);
}

function meta::analytics::search::transformation::getExtensions(): meta::pure::extension::Extension[*]
{
  meta::pure::extension::defaultExtensions()->concatenate(meta::relational::extension::relationalExtensions());
}

function meta::analytics::search::transformation::service::buildDatabaseColumnDocuments(funcDefinition: FunctionDefinition<Any>[1], mapping: meta::pure::mapping::Mapping[1], runtime: meta::pure::runtime::Runtime[1]): meta::analytics::search::metamodel::mapping::DatabaseColumnDocument[*]
{
  let lineage = $funcDefinition->meta::analytics::lineage::computeLineage($mapping, $runtime, meta::analytics::search::transformation::getExtensions());

  let columnLineage = $lineage.reportLineage->meta::analytics::lineage::transformColumns();
  let tables = $columnLineage->map(l|
    $l.columns->map(c|
      ^meta::analytics::search::metamodel::mapping::DatabaseColumnDocument(
        database = $c.database,
        schema = $c.schema,
        table = $c.table,
        columns = $c.name
      );
    )
  );

  $tables->map(t|
    let cols = $tables->filter(table|$table.database == $t.database && $table.schema == $t.schema && $table.table == $t.table)->map(table|$table.columns);
    ^meta::analytics::search::metamodel::mapping::DatabaseColumnDocument(
      database = $t.database,
      schema = $t.schema,
      table = $t.table,
      columns = $cols
    );
  )->removeDuplicates({t1,t2|$t1.database == $t2.database && $t1.schema == $t2.schema && $t1.table == $t2.table});
}

function meta::analytics::search::transformation::service::buildClassDocuments(execution: meta::legend::service::metamodel::Execution[1]): meta::analytics::search::metamodel::class::SimpleClassElement[*]
{
  let pureExecution = $execution->cast(@meta::legend::service::metamodel::PureExecution);
  let valueSpec = $pureExecution.func.expressionSequence->first()->toOne()->evaluateAndDeactivate();

  $valueSpec
        ->meta::pure::lineage::scanProperties::scanProperties().result
        ->meta::pure::lineage::scanProperties::propertyTree::buildPropertyTree().children
        ->meta::analytics::search::transformation::service::buildClassDocuments();
}

function meta::analytics::search::transformation::service::buildClassDocuments(tree: meta::pure::lineage::scanProperties::propertyTree::PropertyPathTree[*]): meta::analytics::search::metamodel::class::SimpleClassElement[*]
{
  let traversedTree = $tree->meta::analytics::search::transformation::service::traversePropertyPathTree();
  $traversedTree.classPath->removeDuplicates()->map(c|
    ^meta::analytics::search::metamodel::class::SimpleClassElement(
      name = $c->split('::')->last()->toOne(),
      package = $c,
      properties = $traversedTree->filter(n|$n->cast(@meta::analytics::search::metamodel::service::LinkedPropertyElement).classPath == $c)
                                 ->removeDuplicatesBy(x|$x.name)
                                 ->map(n|$n.name)
    );
  );
}

function meta::analytics::search::transformation::service::traversePropertyPathTree(tree: meta::pure::lineage::scanProperties::propertyTree::PropertyPathTree[*]): meta::analytics::search::metamodel::service::LinkedPropertyElement[*]
{
  $tree->map(t|
    if(
      $t.children->isEmpty(),
      |
        let property = $t.value->cast(@meta::pure::lineage::scanProperties::PropertyPathNode);
        ^meta::analytics::search::metamodel::service::LinkedPropertyElement(
          name = $property.property.name->toOne(),
          classPath = $property.class->elementToPath()
        );,
      |$t.children->map(c|$c->meta::analytics::search::transformation::service::traversePropertyPathTree())
    );
  );
}