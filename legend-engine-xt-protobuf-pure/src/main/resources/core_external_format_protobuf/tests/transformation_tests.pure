// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::external::format::protobuf::generation::*;
import meta::pure::executionPlan::profiles::*;
import meta::external::format::protobuf::serialization::*;
import meta::external::format::protobuf::generation::tests::trade::*;
import meta::external::format::protobuf::generation::tests::account::*;
import meta::external::format::protobuf::metamodel::*;

Class meta::external::format::protobuf::generation::tests::trade::Trade
{
   type:String[1];
   id:Integer[1];
   npv:Float[1];
   notionals:Float[*];
   account : Account[1];
}

Enum meta::external::format::protobuf::generation::tests::trade::MyEnum
{
   a,b
}

Class meta::external::format::protobuf::generation::tests::account::Account
{
   name : String[1];
   t : Trade[1];
   enum : MyEnum[1];
   aDate:StrictDate[1];
}

Class meta::external::format::protobuf::generation::tests::Person
{
   name : String[1];
}

Class meta::external::format::protobuf::generation::tests::StandaloneThing
{
   name : String[1];
}

Class meta::external::format::protobuf::generation::tests::ClassWithMap
{
   map : Map<String,Integer>[1];
}

Class meta::external::format::protobuf::generation::tests::oneof::Shape
{
}

Class meta::external::format::protobuf::generation::tests::oneof::Circle extends meta::external::format::protobuf::generation::tests::oneof::Shape
{
  radius:Float[1];
}

Class meta::external::format::protobuf::generation::tests::oneof::Rectangle extends meta::external::format::protobuf::generation::tests::oneof::Shape
{
  height:Float[1];
  width:Float[1];
}

Class meta::external::format::protobuf::generation::tests::oneof::Drawing
{
  shapes:meta::external::format::protobuf::generation::tests::oneof::Shape[*];
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::testClassToProtoBuf():Boolean[1]
{
  let results = Trade->classToMessage();              
  assertSize($results, 2);
  let resultForTrade = $results->filter( r | $r.package == 'meta.external.format.protobuf.generation.tests.trade');
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.trade;\n' +
                '\n' +
                'import "meta_external_format_protobuf_generation_tests_account.proto";\n' +
                '\n' + 
                'enum MyEnum {\n'+
                '  a = 0;\n'+
                '  b = 1;\n'+
                '}\n'+
                '\n' + 
                'message Trade {\n'+
                '  string type = 1;\n'+
                '  int64 id = 2;\n'+
                '  double npv = 3;\n'+
                '  repeated double notionals = 4;\n'+
                '  meta.external.format.protobuf.generation.tests.account.Account account = 5;\n'+
                '}'
, $resultForTrade->toOne()->meta::external::format::protobuf::serialization::toString());
  let resultForAccount = $results->filter( r | $r.package == 'meta.external.format.protobuf.generation.tests.account');              
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.account;\n' +
                '\n' +
                'import "google/protobuf/timestamp.proto";\n' +
                'import "meta_external_format_protobuf_generation_tests_trade.proto";\n' +
                '\n' +
                'message Account {\n'+
                '  string name = 1;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.Trade t = 2;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.MyEnum enum = 3;\n'+
                '  google.protobuf.Timestamp a_date = 4;\n'+
                '}', $resultForAccount->toOne()->meta::external::format::protobuf::serialization::toString());              
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::testGenerateProtobufFromPureWithScope():Boolean[1]
{
  let results = generateProtobufFromPureWithScope(^ProtobufConfig(scopeElements=[meta::external::format::protobuf::generation::tests]));
  assertSize($results, 4);
  let resultForTrade = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests_trade.proto');
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.trade;\n' +
                '\n' +
                'import "meta_external_format_protobuf_generation_tests_account.proto";\n' +
                '\n' + 
                'enum MyEnum {\n'+
                '  a = 0;\n'+
                '  b = 1;\n'+
                '}\n'+
                '\n' + 
                'message Trade {\n'+
                '  string type = 1;\n'+
                '  int64 id = 2;\n'+
                '  double npv = 3;\n'+
                '  repeated double notionals = 4;\n'+
                '  meta.external.format.protobuf.generation.tests.account.Account account = 5;\n'+
                '}'
, $resultForTrade.content);
  let resultForAccount = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests_account.proto');              
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.account;\n' +
                '\n' +
                'import "google/protobuf/timestamp.proto";\n' +
                'import "meta_external_format_protobuf_generation_tests_trade.proto";\n' +
                '\n' +
                'message Account {\n'+
                '  string name = 1;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.Trade t = 2;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.MyEnum enum = 3;\n'+
                '  google.protobuf.Timestamp a_date = 4;\n'+
                '}', $resultForAccount.content );
  let resultForTestPkg = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests.proto');
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests;\n' +
                '\n' + 
                'message ClassWithMap {\n' +
                '  map<string,int64> map = 1;\n' +
                '}\n' +
                '\n' + 
                'message Person {\n' +
                '  string name = 1;\n' +
                '}\n' +
                '\n' +  
                'message StandaloneThing {\n' +
                '  string name = 1;\n' +
                '}', $resultForTestPkg.content );
  let resultForInheritence = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests_oneof.proto');
  assertEquals('syntax = "proto3";\n'+
                'package meta.external.format.protobuf.generation.tests.oneof;\n'+
                '\n' +
                'message Circle {\n'+
                '  double radius = 1;\n'+
                '}\n'+
                '\n' + 
                'message Drawing {\n'+
                '  repeated Shape shapes = 1;\n'+
                '}\n'+
                '\n' + 
                'message Rectangle {\n'+
                '  double height = 1;\n'+
                '  double width = 2;\n'+
                '}\n'+
                '\n' + 
                'message Shape {\n' +
                '  oneof shape {\n'+
                '    Circle circle = 1;\n'+
                '    Rectangle rectangle = 2;\n'+
                '  }\n'+
                '}', $resultForInheritence.content );                                      
}

function <<test.Test>> {serverVersion.start='v1_20_0'} meta::external::format::protobuf::generation::tests::transform_testClassToProtoBuf():Boolean[1]
{
  let results = meta::external::format::protobuf::generation::transform(^ProtobufConfig(class='meta::external::format::protobuf::generation::tests::trade::Trade'));
  assertSize($results, 2);
  let resultForTrade = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests_trade.proto');
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.trade;\n' +
                '\n' +
                'import "meta_external_format_protobuf_generation_tests_account.proto";\n' + 
                '\n' +
                'enum MyEnum {\n'+
                '  a = 0;\n'+
                '  b = 1;\n'+
                '}\n'+
                '\n' + 
                'message Trade {\n'+
                '  string type = 1;\n'+
                '  int64 id = 2;\n'+
                '  double npv = 3;\n'+
                '  repeated double notionals = 4;\n'+
                '  meta.external.format.protobuf.generation.tests.account.Account account = 5;\n'+
                '}'
                , $resultForTrade.content);
  let resultForAccount = $results->filter( r | $r.fileName == 'meta_external_format_protobuf_generation_tests_account.proto');              
  assertEquals('syntax = "proto3";\n' +
                'package meta.external.format.protobuf.generation.tests.account;\n' +
                '\n' +
                'import "google/protobuf/timestamp.proto";\n' +
                'import "meta_external_format_protobuf_generation_tests_trade.proto";\n' +
                '\n' +
                'message Account {\n'+
                '  string name = 1;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.Trade t = 2;\n'+
                '  meta.external.format.protobuf.generation.tests.trade.MyEnum enum = 3;\n'+
                '  google.protobuf.Timestamp a_date = 4;\n'+
                '}', $resultForAccount.content );
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::testOptionsToString():Boolean[1]
{
  let config = ^ProtobufConfig(javaPackage='com.gs.mymodel', 
                         javaOuterClassname='TestClass', 
                         javaMultipleFiles=true, 
                         optimizeFor=OptimizeMode.SPEED,
                         customOptions=[^ProtobufOption(name='aString', value='stuff'),
                                        ^ProtobufOption(name='aBool', value=true)]);
  let options = meta::external::format::protobuf::generation::configToOptions($config);
  assertEquals('option java_package = "com.gs.mymodel";\n'
    + 'option java_outer_classname = "TestClass";\n'
    + 'option java_multiple_files = true;\n'
    + 'option optimize_for = SPEED;\n'
    + 'option aString = "stuff";\n'
    + 'option aBool = true;\n', $options->map( o | $o->toString())->joinStrings());                       
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::testLiteralBlockOptionToString():Boolean[1]
{
  let option = ^Option(name='a', value=^BlockLiteral(values=[^BlockValue(name='get', value=^StringLiteral(value='a/b'))]));
  assertEquals('option a = { get: "a/b" };\n', $option->toString());
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::testRpcToString():Boolean[1]
{
  let rpc = ^RemoteProcedureCall(name='hello', 
                                 requestType=^MessageType(type=^MessagePtr(package='test',name='Request')),
                                 returnType=^MessageType(type=^MessagePtr(package='test',name='Response'))
                                 );
  assertEquals('  rpc hello(Request) returns (Response) {}',$rpc->toString('test'));
  let rpcWithOption = ^RemoteProcedureCall(name='hello', 
                                 requestType=^MessageType(type=^MessagePtr(package='test',name='Request')),
                                 returnType=^MessageType(type=^MessagePtr(package='test',name='Response')),
                                 options=[^Option(name='a.b', value=^BlockLiteral(values=[^BlockValue(name='get', value=^StringLiteral(value='a/b'))]))]
                                 );
  assertEquals('  rpc hello(Request) returns (Response) {\n' +
               'option (a.b) = { get: "a/b" };\n' +
               '}',$rpcWithOption->toString('test'));
}

function <<test.Test>> meta::external::format::protobuf::generation::tests::transform_testClassToProtoBufWithOptions():Boolean[1]
{
  let config = ^ProtobufConfig(class='meta::external::format::protobuf::generation::tests::Person',
                               javaPackage='com.gs.person');
   assertEquals('syntax = "proto3";\n'
      + 'package meta.external.format.protobuf.generation.tests;\n'
      + '\n'
      + 'option java_package = "com.gs.person";\n'
      + '\n'
      + 'message Person {\n'
      + '  string name = 1;\n'
      + '}'
      ,meta::external::format::protobuf::generation::transform($config).content);
}

function <<test.Test, test.ExcludeAlloy>> meta::external::format::protobuf::generation::tests::transform_testClassWithMapToProtoBuf():Boolean[1]
{
  let config = ^ProtobufConfig(class='meta::external::format::protobuf::generation::tests::ClassWithMap');
   assertEquals('syntax = "proto3";\n'
      + 'package meta.external.format.protobuf.generation.tests;\n'
      + '\n'
      + 'message ClassWithMap {\n'
      + '  map<string,int64> map = 1;\n'
      + '}'
      ,meta::external::format::protobuf::generation::transform($config).content);
}