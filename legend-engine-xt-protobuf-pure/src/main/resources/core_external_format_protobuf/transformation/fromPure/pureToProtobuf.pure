// Copyright 2022 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::external::format::protobuf::metamodel::*;
import meta::external::format::protobuf::transformation::fromPure::*;

import meta::external::shared::format::*;
import meta::external::shared::format::metamodel::*;
import meta::external::shared::format::transformation::fromPure::*;

import meta::pure::generation::metamodel::*;

import meta::pure::model::unit::*;

// ========================================================================================================================================================
// Descriptor
// ========================================================================================================================================================

function meta::external::format::protobuf::transformation::fromPure::protobufFromPureDescriptor(): ExternalFormatFromPureDescriptor<ModelToProtobufDataConfiguration>[1]
{
  ^ExternalFormatFromPureDescriptor<ModelToProtobufDataConfiguration>
  (
    configuration        = ModelToProtobufDataConfiguration,
    generator            = pureToProtobuf_ModelUnit_1__ModelToProtobufDataConfiguration_1__SchemaSet_1_,
    defaultConfig        = defaultConfig()
  )
}

// ========================================================================================================================================================
// Configuration
// ========================================================================================================================================================

Class meta::external::format::protobuf::transformation::fromPure::ModelToProtobufDataConfiguration extends ModelToSchemaConfiguration
{
  javaPackage:String[0..1];
  {doc.doc='The class name (and hence the file name) for the wrapper Java class you want to generate. If no explicit java_outer_classname is specified in the .proto file, the class name will be constructed by converting the .proto file name to camel-case (so foo_bar.proto becomes FooBar.java). If the java_multiple_files option is disabled, then all other classes/enums/etc. generated for the .proto file will be generated within this outer wrapper Java class as nested classes/enums/etc.'}
  javaOuterClassname:String[0..1];
  {doc.doc='If false, only a single .java file will be generated for this .proto file, and all the Java classes/enums/etc. generated for the top-level messages, services, and enumerations will be nested inside of an outer class (see java_outer_classname). If true, separate .java files will be generated for each of the Java classes/enums/etc. generated for the top-level messages, services, and enumerations, and the wrapper Java class generated for this .proto file won\'t contain any nested classes/enums/etc. This is a Boolean option which defaults to false'}
  javaMultipleFiles:Boolean[0..1];
  {doc.doc='Affects the C++ and Java code generators (and possibly third-party generators) changing the generated code to optimize for these characteristics.'}
  optimizeFor:OptimizeMode[0..1];
  {doc.doc='Freeform options that are not part of the specification but can be passed through to other tooling'}
  customOptions:Map<String,Any>[0..1];
}

Enum meta::external::format::protobuf::transformation::fromPure::OptimizeMode
{
  {doc.doc='(default): The protocol buffer compiler will generate code for serializing, parsing, and performing other common operations on your message types. This code is highly optimized.'}
  SPEED,
  {doc.doc='The protocol buffer compiler will generate minimal classes and will rely on shared, reflection-based code to implement serialialization, parsing, and various other operations. The generated code will thus be much smaller than with SPEED, but operations will be slower. Classes will still implement exactly the same public API as they do in SPEED mode. This mode is most useful in apps that contain a very large number .proto files and do not need all of them to be blindingly fast.'}
  CODE_SIZE,
  {doc.doc='The protocol buffer compiler will generate classes that depend only on the "lite" runtime library (libprotobuf-lite instead of libprotobuf). The lite runtime is much smaller than the full library (around an order of magnitude smaller) but omits certain features like descriptors and reflection. This is particularly useful for apps running on constrained platforms like mobile phones. The compiler will still generate fast implementations of all methods as it does in SPEED mode. Generated classes will only implement the MessageLite interface in each language, which provides only a subset of the methods of the full Message interface.'}
  LITE_RUNTIME
}

function meta::external::format::protobuf::transformation::fromPure::defaultConfig(): ModelToProtobufDataConfiguration[1]
{
  ^ModelToProtobufDataConfiguration(format = 'Protobuf', targetSchemaSet='target::package::GeneratedSchemaSet');
}

// ========================================================================================================================================================
// generation
// ========================================================================================================================================================

function meta::external::format::protobuf::transformation::fromPure::pureToProtobuf(modelUnit:ModelUnit[1], config:ModelToProtobufDataConfiguration[1]): SchemaSet[1]
{
   let sourceModels = $modelUnit->resolve().classes();
   createSchemaSet($sourceModels, $config);
}

function <<access.private>> meta::external::format::protobuf::transformation::fromPure::createSchemaSet(sourceModels:Class<Any>[*], config:ModelToProtobufDataConfiguration[1]): SchemaSet[1]
{
   let schemaSetSepPos  = $config.targetSchemaSet->lastIndexOf('::');
   let schemaSetPkgName = $config.targetSchemaSet->substring(0, $schemaSetSepPos);

   let protoSchemas     = generateProtobufFromPure($sourceModels, $config);

   ^SchemaSet(
      package = pathToPackage($schemaSetPkgName, true),
      name    = $config.targetSchemaSet->substring($schemaSetSepPos+2),
      format  = 'Protobuf',
      schemas = $protoSchemas->map(f | ^Schema(
                                         location  = $f.fileName,
                                         detail    = $f
                                        ))
   );
}

// ========================================================================================================================================================
// helper functions
// ========================================================================================================================================================

Class meta::external::format::protobuf::transformation::fromPure::ProtobufOption
{
  name:String[1];
  value:Any[1];
}

function <<Generation.Configuration>>  meta::external::format::protobuf::transformation::fromPure::describeConfiguration(): GenerationParameter[*]
{
  meta::pure::generation::describeConfiguration(ModelToProtobufDataConfiguration, meta::external::format::protobuf::transformation::fromPure::defaultConfig__ModelToProtobufDataConfiguration_1_, []);
}

function meta::external::format::protobuf::transformation::fromPure::transform(modelUnit:ModelUnit[1], input:ModelToProtobufDataConfiguration[1]):meta::external::shared::format::metamodel::data::SchemaData[*]
{
   meta::legend::test::mayExecuteLegendTest(
       {clientVersion, serverVersion, serializationKind, host, port|
         let fStr = 'meta::protocols::pure::'+$clientVersion+'::invocation::externalFormat::protobuf::legendGenerateProtobuf_ModelUnit_1__ModelToProtobufDataConfiguration_1__String_1__Integer_1__String_1__SchemaData_MANY_';
         let xf = $fStr->pathToElement()->cast(@Function<{ModelUnit[1], ModelToProtobufDataConfiguration[1], String[1], Integer[1], String[1]->meta::external::shared::format::metamodel::data::SchemaData[*]}>);
         $xf->evaluate([$modelUnit, $input, $host, $port, $serverVersion]->map(v|list($v)))->toOneMany()->cast(@meta::external::shared::format::metamodel::data::SchemaData);},
       | generateProtobufFromPure($modelUnit->resolve().classes(), $input)->map(x|
         ^meta::external::shared::format::metamodel::data::SchemaData
         (
           location = $x.fileName,
           content = $x.file->meta::external::format::protobuf::metamodel::serialization::toString()
         );
       )
    )->toOneMany();  
}

function meta::external::format::protobuf::transformation::fromPure::generateProtobufFromPure(sourceModels:Class<Any>[*], config:ModelToProtobufDataConfiguration[1]):meta::external::format::protobuf::metamodel::ProtobufSchema[*]
{
  //$config.resolveSourceModel->filter(e| !$e->instanceOf(Package))->map(e|assert($e->instanceOf(Class),' Protobuf generation of ' + $e->type().name->toOne()+ ' is not currently supported. Tried to generate Protobuf for '+ $e->elementToPath()) ;);
  let class   = $sourceModels->toOneMany();
  let options = configToOptions($config);
  $class->classToMessage($options)->map(s|
      let fileName = if($s.package->isEmpty(), | $class.name->toOne() + '.proto', | $s.package->toOne()->protoPackageToFileName());
      ^meta::external::format::protobuf::metamodel::ProtobufSchema
      (
        fileName = $fileName,
        file = $s
      );
    );
}

function meta::external::format::protobuf::transformation::fromPure::configToOptions(config:ModelToProtobufDataConfiguration[1]):Option[*]
{
  let properties = ModelToProtobufDataConfiguration.properties->filter( p | $p.name != 'customOptions');
  $properties->map( p | let res = $p->eval($config);  
                        if( !$res->isEmpty() ,| ^Option(name=$p.name->toOne()->splitOnCamelCase()->toLowerFirstCharacter()->joinStrings('_'), 
                                                        value=$res->toOne()->toOptionValue()), | []);
                        )->concatenate($config.customOptions->map(x|$x->keyValues()->sortBy(x|$x.first)->map(o |
                              ^Option(name=$o.first, value=$o.second->toOptionValue())))
                           );
}

function meta::external::format::protobuf::transformation::fromPure::toOptionValue(any:Any[1]):Literal[1]
{
  $any->match([
                s:String[1] | ^StringLiteral(value=$s),
                i:Integer[1] | ^IntLiteral(value=$i),
                f:Float[1] | ^FloatLiteral(value=$f),
                b:Boolean[1] | ^BoolLiteral(value=$b),
                e:meta::pure::metamodel::type::Enum[1] | ^IdentifierLiteral(value=$e.name)
              ])
}

function meta::external::format::protobuf::transformation::fromPure::elementToProtoPackage(e:Type[1]):String[1]
{
  $e->cast(@PackageableElement)->elementToProtoPackage();
}

function meta::external::format::protobuf::transformation::fromPure::elementToProtoPackage(e:PackageableElement[1]):String[1]
{
  $e.package->toOne()->elementToPath('.')
}

function meta::external::format::protobuf::transformation::fromPure::protoPackageToFileName(p:String[1]):String[1]
{
  $p->replace('.', '_') + '.proto'
}

function meta::external::format::protobuf::transformation::fromPure::classToMessage(class:Class<Any>[1]):ProtoFile[*]
{
  meta::external::format::protobuf::transformation::fromPure::classToMessage([$class], [])
}

function meta::external::format::protobuf::transformation::fromPure::classToMessage(class:Class<Any>[1..*], options:Option[*]):ProtoFile[*]
{
   let allClasses = $class->map(c | $c->scanTypes([]))->removeDuplicates()->remove(Any);
   let typesMap = seedPureTypeToProtoBufTypeMapping($allClasses);
   let classesByPackage = $allClasses->groupBy(c | $c->elementToProtoPackage());
   
   $classesByPackage->keyValues()->map(p | 
      let thePackage = $p.first;
      let packageClasses = $p.second.values;

      let messages = $packageClasses
        ->filter(c|$c->instanceOf(Class))->cast(@Class<Any>)
        ->map(c|
            //Collect any abstract base classes
            let oneOfs = $c->filter(c | ($c->allProperties()->isEmpty() || $c->isAbstract()) && $c->allSpecializations()->isNotEmpty())
                                      ->removeDuplicates()
                                      ->map(c | let subClasses = $c->allSpecializations()->filter(sc | $sc != $c);
                                                ^OneOf(
                                                  name=$c.name->toOne()->toLower(),
                                                  field=$subClasses->map(s | ^Field(name=$s.name->toOne()->meta::pure::functions::string::splitOnCamelCase()->joinStrings('_')->toLower(), 
                                                                                    type=$typesMap->get($s)->toOne(),
                                                                                    number=$subClasses->indexOf($s)+1,
                                                                                    repeated=false
                                                                                    ))
                                                ););
            ^Message
            (
              name = $c.name->toOne(),
              content = $oneOfs->concatenate($c.properties->map(p|$p->propertyToField($c.properties->indexOf($p)+1, $typesMap)))
            );
        );
        
      let enumerations = $packageClasses
              ->filter(c|$c->instanceOf(meta::pure::metamodel::type::Enumeration))->cast(@meta::pure::metamodel::type::Enumeration<Any>)
              ->map(e|
                    ^meta::external::format::protobuf::metamodel::Enumeration
                    (
                        name = $e->enumName(),
                        values = $e->enumValues()->map(v|^meta::external::format::protobuf::metamodel::Enum(constant=$v->toString(), constantNumber=$e->enumValues()->indexOf($v)))
                    )
                );

      let topLevelDefs = $messages->concatenate($enumerations);      

      //If this is a complex type that is not in the current package, we need to import it      
      let imports = $messages.content
        ->map(m| $m->match([
          f:Field[1]|$f.type,
          o:OneOf[1]|$o.field.type]))
        ->filter(t|$t->instanceOf(MessagePtr))->cast(@MessagePtr)
        ->filter(m | $m.package != $thePackage)
        ->map(p| if ( $p.package == 'google.protobuf', | 'google/protobuf/' + $p.name->toLower() + '.proto'
                                                                    , | $p.package->protoPackageToFileName()))
        ->removeDuplicates()->sort()->map(s | ^ProtoImport(name=$s));
        
      ^ProtoFile(syntax=Syntax.proto3, package=$thePackage, 
                 topLevelDefs=$topLevelDefs->sortBy(s|$s->match([m:Message[1]|$m.name,
                                                                 e:meta::external::format::protobuf::metamodel::Enumeration[1]|$e.name,
                                                                 s:Service[1]|$s.name])),
                 imports=$imports,
                 options=$options);
   );
}

function meta::external::format::protobuf::transformation::fromPure::propertyToField(property:Property<Nil,Any|*>[1], index:Integer[1], typesMap:Map<Type, ProtoBufType>[1]):Field[1]
{
   ^Field
   (
      name = $property.name->toOne()->meta::pure::functions::string::splitOnCamelCase()->joinStrings('_')->toLower(),
      type = if($property.genericType.rawType == Map,|^MapType(keyType=$typesMap->get($property.genericType.typeArguments->at(0).rawType->toOne())->toOne()->cast(@ScalarType),
                                                                         valueType=$typesMap->get($property.genericType.typeArguments->at(1).rawType->toOne())->toOne())
                                                              ,| $typesMap->get($property.genericType.rawType->toOne())->toOne()),
      number = $index,
      repeated = !$property.multiplicity->hasUpperBound() || ($property.multiplicity.upperBound->toOne().value > 1)
   );
}

function <<access.private>> meta::external::format::protobuf::transformation::fromPure::seedPureTypeToProtoBufTypeMapping(types:Type[*]):Map<Type, ProtoBufType>[1]
{
   newMap(
      [
         pair(Integer, ^meta::external::format::protobuf::metamodel::Int64()),
         pair(Float, ^meta::external::format::protobuf::metamodel::Double()),
         pair(String, ^meta::external::format::protobuf::metamodel::String()),
         pair(Boolean, ^meta::external::format::protobuf::metamodel::Bool()),
         pair(DateTime , ^meta::external::format::protobuf::metamodel::MessagePtr(package='google.protobuf', name='Timestamp')),
         pair(Date , ^meta::external::format::protobuf::metamodel::MessagePtr(package='google.protobuf', name='Timestamp')),
         pair(StrictDate , ^meta::external::format::protobuf::metamodel::MessagePtr(package='google.protobuf', name='Timestamp')),
         pair(Number , ^meta::external::format::protobuf::metamodel::Double()),
         pair(Any, ^meta::external::format::protobuf::metamodel::MessagePtr(package='google.protobuf', name='Any'))
      ]
      ->concatenate
      (
         $types->filter(c|$c->instanceOf(Class))->map(c|pair($c, ^meta::external::format::protobuf::metamodel::MessagePtr(package=$c->elementToProtoPackage(), name=$c.name->toOne())))
      )
      ->concatenate
      (
         $types->filter(c|$c->instanceOf(meta::pure::metamodel::type::Enumeration))->map(c|pair($c, ^meta::external::format::protobuf::metamodel::EnumPtr(package=$c->elementToProtoPackage(), name=$c.name->toOne())))
      )
   )
}


function meta::external::format::protobuf::transformation::fromPure::scanTypes(t:Type[1], processed:Type[*]):Type[*]
{
   if (!$processed->contains($t),
       |$t->concatenate(if($t->instanceOf(Class),
                         |$t->cast(@Class<Any>)->allProperties().genericType.rawType->distinct()->concatenate($t->cast(@Class<Any>)->meta::pure::functions::meta::findAllSpecializations())->filter(t|($t->instanceOf(Class) || $t->instanceOf(meta::pure::metamodel::type::Enumeration)) && $t != Any && $t != Map)->map(c|$c->meta::external::format::protobuf::transformation::fromPure::scanTypes($processed->concatenate($t)))->distinct(),
                         |[]
                       )),
       |[]
   );
}
